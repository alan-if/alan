-- TESTGAME


IMPORT 'locations.i'.
IMPORT 'classes.i'.
IMPORT 'verbs.i'.
IMPORT 'messages.i'.



THE sunny_room ISA ROOM 
	NAME 'Sunny Room' 
	DESCRIPTION "You are in a spacious room filled with bright sunlight entering through 
			the window and through an open doorway to the west leading to a small balcony. 
			A white door leads north."

	EXIT north TO closet	
		CHECK 
			IF door1 IS closed
				THEN "You can't, since the door is closed."
				ELSE "You walk into the closet. The door slams shut behind you."
					LOCATE hero AT closet.
					LOCATE door1 AT closet.
					MAKE door1 closed.
			END IF.
	END EXIT.

	EXIT west TO balcony.



END THE.



THE table ISA SUPPORTER
	AT sunny_room
	IS NOT takeable.
	VERB examine
		DOES AFTER LIST table_parts.
	END VERB.
END THE.



THE box ISA LISTABLE_CONTAINER
	AT sunny_room
	-- leaving all 'description' statements out, the default description of the box will be shown 
	-- in the room description
END THE.


THE table_parts ISA OBJECT
	AT sunny_room
	CONTAINER
		HEADER "In the table there are"
		ELSE ""
	DESCRIPTION ""  -- this way no description of the object whatsoever is shown in the room description
END THE.
	

THE drawer1 ISA LISTABLE_CONTAINER
	IN table_parts
	OPAQUE CONTAINER
	NAME top drawer
	IS closeable. IS closed.
	DESCRIPTION ""
END THE.


THE drawer2 ISA LISTABLE_CONTAINER
	IN table_parts 
	OPAQUE CONTAINER
	NAME bottom drawer
	IS closeable. IS closed.
	DESCRIPTION ""
END THE.





THE door1 ISA DOOR
	AT sunny_room
	NAME white 'door'
	DESCRIPTION ""
END THE.


THE window1 ISA WINDOW
	AT sunny_room
	NAME 'window'
	DESCRIPTION ""
END THE.


THE flower ISA SCENERY
	IN table
END THE.


THE note ISA OBJECT
	IN table
	IS writeable.
	IS readable.
	
	VERB write
		DOES BEFORE 
			IF pen NOT IN hero
				THEN "(taking the pen first)$n"
					LOCATE pen IN hero.
			END IF.
	END VERB.

	VERB burn
		CHECK match IS lit
			ELSE "How?"
		DOES ONLY "You burn the note to ashes."
				LOCATE note AT nowhere.
	END VERB.

	VERB burn_with
		WHEN obj1
		CHECK obj2 = match
			ELSE "That's not possible."
		DOES ONLY "You burn the note to ashes."
				LOCATE note AT nowhere.
	END VERB.
			

END THE.


THE pen ISA OBJECT
	IN table
END THE.


THE bottle ISA LISTABLE_CONTAINER
	In box
END THE.


THE juice ISA LIQUID
	IN bottle
	HAS vessel bottle.
	IS drinkable.
	ARTICLE "some"
END THE.


THE match ISA LIGHTSOURCE 
	IN box
	IS natural.
END THE.


THE flashlight ISA LIGHTSOURCE
	IN box
	IS NOT natural.
END THE.


THE waterpistol ISA WEAPON
	IN box
	IS fireable.
END THE.


THE blue_ball ISA OBJECT
	IN box
	NAME blue ball NAME blue
	VERB take
		DOES SCORE 1.
	END VERB.
END THE.


THE red_ball ISA OBJECT
	AT sunny_room
	NAME red ball NAME red
	VERB take
		DOES SCORE 1.
	END VERB.
END THE.


THE yellow_ball ISA OBJECT 
	AT sunny_room
	NAME yellow ball NAME yellow
	VERB take
		DOES SCORE 1.
	END VERB.
END THE.


THE felix ISA NAMED_ACTOR
	AT sunny_room
	NAME felix NAME himself
	HAS can_talk.
	MENTIONED "Felix"	
	PRONOUN 'him'
	DESCRIPTION "$pFelix, your grumpy butler, is standing off to one side."

	VERB examine 
		DOES ONLY "Felix is your grumpy butler, properly clad in a tuxedo." 
	END VERB.

END THE.


THE dog ISA ACTOR
	AT sunny_room
	DESCRIPTION "$pA dog is here, looking at you with faithful eyes." 
END THE.


THE balcony ISA ROOM

	DESCRIPTION "You're standing on a small balcony overlooking a garden. The sky is clear. In the distance you see a mountain. 
			 The room is back to the east."
	
	IF visited OF balcony = 1
		THEN "$pHaving never been on this balcony before, you pause to take in the magnificent view."
		ELSE "$pEven if you've been here before, the magnificent view never ceases to amaze you."
	END IF.

	EXIT east TO sunny_room.	

END THE.



THE mountain ISA BACKGROUND
	AT balcony
	DESCRIPTION
END THE.


THE garden ISA BACKGROUND
	AT balcony
	NAME garden NAME flowers
	DESCRIPTION "The garden is green and full of blossoming flowers."
END THE.



THE closet ISA DARK_LOCATION
	
	DESCRIPTION "This is a closet."

	EXIT south TO sunny_room	
		CHECK 
			IF door1 IS closed
				THEN "You can't, since the door is closed."
				ELSE "You walk back into the sunny room. The door slams shut behind you."
					LOCATE hero AT closet.
					LOCATE door1 AT closet.
					MAKE door1 closed.
			END IF.
	END EXIT.

END THE.


THE shirt ISA CLOTHING
	AT closet
	IS topcover 8.
END THE.


THE jacket ISA CLOTHING
	AT closet
	IS topcover 32.
END THE.


THE wallet ISA OBJECT
	IN jacket
END THE.





START AT sunny_room.
"This is a" STYLE alert. "testgame" STYLE normal. "for the new ALAN library. Experiment with
 manipulating various objects and moving around. 
Taking any ball will increase your score."
		
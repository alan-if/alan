%%OPTIONS

	Prefix 'pm';
	Listerprefix 'lm';
--	List Grammar, Statistics, Items;
	StackLimit 64;
	Pack RDS;
--	Line;
--	Debug;
	No Resolve;

%%IMPORT

#include "srcp.h"
#include "smScan.h"
#include "token.h"

#include "lmList.h"

#include "act.h"                /* ACT-nodes */
#include "adv.h"		/* ADV-node */
#include "alt.h"                /* ALT-nodes */
#include "atr.h"                /* ATR-nodes */
#include "chk.h"                /* CHK-nodes */
#include "cnt.h"		/* CNT-nodes */
#include "elm.h"                /* ELM-nodes */
#include "evt.h"                /* EVT-nodes */
#include "exp.h"                /* EXP-nodes */
#include "ext.h"                /* EXT-nodes */
#include "lim.h"		/* LIM-nodes */
#include "loc.h"                /* LOC-nodes */
#include "lst.h"		/* LST-nodes */
#include "msg.h"                /* MSG-nodes */
#include "nam.h"		/* NAM-nodes */
#include "obj.h"		/* OBJ-nodes */
#include "opt.h"		/* OPTIONS */
#include "res.h"                /* RES-nodes */
#include "rul.h"                /* RUL-nodes */
#include "sco.h"                /* SCORES */
#include "scr.h"                /* SCR-nodes */
#include "stm.h"		/* STM-nodes */
#include "stp.h"                /* STP-nodes */
#include "str.h"		/* STRINGS */
#include "stx.h"                /* STX-nodes */
#include "syn.h"                /* SYN-nodes */
#include "vrb.h"                /* VRB-nodes */
#include "whr.h"                /* WHR-nodes */
#include "wht.h"                /* WHT-nodes */


%%SCANNER

    smScan(lexContext, token);


%%INSERTSYMBOL

{
  static int idno = 1;

    /* Make the requested token */
    token->code = code;
    if (token->code == sm_MAIN_IDENT_Token) {
      sprintf(token->chars, "$genid#%d", idno++);
      strcat(insStr, " ('");
      strcat(insStr, token->chars);
      strcat(insStr, "')");
    } else
      strcpy(token->chars, symString);
}

%%DECLARATIONS

static int val(char str[])
{
  int temp;

  sscanf(str, "%d", &temp);
  return(temp);
}


%%ATTRIBUTES

	act %% ActNod *%1%%,
	acts %% List *%1%%,
	agr %% AgrKind %1%%,
	alt %% AltNod *%1%%,
	alts %% List *%1%%,
	atr %% AtrNod *%1%%,
	atrs %% List *%1%%,
        cases %% List *%1%%,
	chk %% ChkNod *%1%%,
	chks %% List *%1%%,
	classkd %% NamKind %1%%,
	cnt %% CntNod *%1%%,
	cnts %% List *%1%%,
	elm %% ElmNod *%1%%,
	elms %% List *%1%%,
	evt %% EvtNod *%1%%,
	evts %% List *%1%%,
	exp %% ExpNod *%1%%,
        expkd %%ExpKind %1%%,
	ext %% ExtNod *%1%%,
	exts %% List *%1%%,
	lim %% LimNod *%1%%,
	lims %% List *%1%%,
	loc %% LocNod *%1%%,
	locs %% List *%1%%,
	minus %% Bool %1%%,
	msg %% MsgNod *%1%%,
	msgs %% List *%1%%,
	nam %% NamNod *%1%%,
	nams %% List *%1%%,
	nams2 %% List *%1%%,
	not %% Bool %1%%,
	obj %% ObjNod *%1%%,
	objs %% List *%1%%,
	op %% OpKind %1%%,
	qual %% QualKind %1%%,
	res %% ResNod *%1%%,
	ress %% List *%1%%,
	rul %% RulNod *%1%%,
	ruls %% List *%1%%,
	scr %% ScrNod *%1%%,
	scrs %% List *%1%%,
	single %% Bool %1%%,
	bits %% int %1%%,
	srcp %% Srcp %1%%,
	stm %% StmNod *%1%%,
	stms %% List *%1%%,
	stms2 %% List *%1%%,
	stms3 %% List *%1%%,
	stp %% StpNod *%1%%,
	stps %% List *%1%%,
	str %% char *%1%%,
	stx %% StxNod *%1%%,
	stxs %% List *%1%%,
	syn %% SynNod *%1%%,
	syns %% List *%1%%,
	val %% int %1%%,
	vrb %% VrbNod *%1%%,
	vrbs %% List *%1%%,
	whr %% WhrNod *%1%%,
	wht %% WhtNod *%1%%;

%%TERMINALS

    IDENT = 2,1,1 => 'Identifier';

%%RECOVERY

    Fiducial('VERB', 'DOES', 'EXIT', 'ACTOR', 'EVENT', 'START', 'OBJECT',
		'SYNTAX', 'SCRIPT', 'LOCATION', 'SYNONYMS', 'CONTAINER',
		'MENTIONED', 'ATTRIBUTES', 'DESCRIPTION', 'END', 'CHECK');

    Separator(',');

    Skip(IDENT, Integer, '.');

%%RULES

--> ADVENTURE
<adventure>
    = <optional_options> <units> <start>
    ;

---> OPTIONS
<optional_options>	-- RETURNS: <nothing>
    =
    ! 'OPTIONS' <options>					%+('MESSAGE', 'DEFAULT')
    ;

<options>			-- RETURNS: <nothing>
    = <option>
    ! <options> <option>
    ;

<option>			-- RETURNS: <nothing>
    = ID '.'
    %%
	optBool(%ID.nam->str, `&%ID.nam->srcp, TRUE);
    %%

    ! ID ID '.'
    %%
	optenum(%ID.nam->str, `&%ID.nam->srcp, %2ID.nam->str);
    %%

    ! ID Integer '.'
    %%
	optint(%ID.nam->str, `&%ID.nam->srcp, val(%Integer.chars));
    %%
    ;

---> UNITS
<units>
    = <unit>
    ! <units> <unit>
    ;

<unit>
    = <default>
    %%
	adv.atrs = combine(adv.atrs, %<default>.atrs);
    %%

    ! <object_default>
    %%
	adv.oatrs = combine(adv.oatrs, %<object_default>.atrs);
    %%

    ! <location_default>
    %%
	adv.latrs = combine(adv.latrs, %<location_default>.atrs);
    %%

    ! <actor_default>
    %%
	adv.aatrs = combine(adv.aatrs, %<actor_default>.atrs);
    %%

    ! <synonyms>
    %%
	adv.syns = combine(adv.syns, %<synonyms>.syns);
    %%

    ! <messages>
    %%
	adv.msgs = combine(adv.msgs, %<messages>.msgs);
    %%

    ! <syntax>
    %%
	adv.stxs = combine(adv.stxs, %<syntax>.stxs);
    %%

    ! <verb>
    %%
	adv.vrbs = concat(adv.vrbs, %<verb>.vrb, VRBNOD);
    %%

    ! <location>
    %%
	adv.locs = concat(adv.locs, %<location>.loc, LOCNOD);
    %%

    ! <object>
    %%
	adv.objs = concat(adv.objs, %<object>.obj, OBJNOD);
    %%

    ! <event>
    %%
	adv.evts = concat(adv.evts, %<event>.evt, EVTNOD);
    %%

    ! <container>
    %%
	adv.cnts = concat(adv.cnts, %<container>.cnt, CNTNOD);
    %%

    ! <actor>
    %%
	adv.acts = concat(adv.acts, %<actor>.act, ACTNOD);
    %%

    ! <rule>
    %%
	adv.ruls = concat(adv.ruls, %<rule>.rul, RULNOD);
    %%
    ;

----> DEFAULTS
<default>				-- RETURNS: atrs
    = 'DEFAULT' 'ATTRIBUTES' <attributes>		%+('MESSAGE', 'DEFAULT')
    %%
	%<default>.atrs = %<attributes>.atrs;
    %%
    ;

<location_default>		-- RETURNS: atrs
    = 'LOCATION' 'ATTRIBUTES' <attributes>		%+('MESSAGE', 'DEFAULT')
    %%
	%<location_default>.atrs = %<attributes>.atrs;
    %%
    ;

<object_default>			-- RETURNS: atrs
    = 'OBJECT' 'ATTRIBUTES' <attributes>			%+('MESSAGE', 'DEFAULT')
    %%
	%<object_default>.atrs = %<attributes>.atrs;
    %%
    ;

<actor_default>			-- RETURNS: atrs
    = 'ACTOR' 'ATTRIBUTES' <attributes>			%+('MESSAGE', 'DEFAULT')
    %%
	%<actor_default>.atrs = %<attributes>.atrs;
    %%
    ;

----> ATTRIBUTES
<attributes>		-- RETURNS: atrs
    = <attribute> '.'
    %%
	%<attributes>.atrs = concat(NULL, %<attribute>.atr, ATRNOD);
    %%

    ! <attributes> <attribute> '.'
    %%
	%<attributes>.atrs = concat(%<attributes>.atrs, %<attribute>.atr, ATRNOD);
    %%
    ;

<attribute>		-- RETURNS: atr
    = ID
    %%
	%<attribute>.atr = newatr(`&%ID.nam->srcp,
				TYPBOOL,
				%ID.nam,
				TRUE, 0, 0);
    %%

    ! 'NOT' ID
    %%
	%<attribute>.atr = newatr(`&%ID.nam->srcp,
				TYPBOOL,
				%ID.nam,
				FALSE, 0, 0);
    %%

    ! ID <optional_minus> Integer
    %%
	if (%<optional_minus>.minus)
		%<attribute>.atr = newatr(`&%ID.nam->srcp,
					TYPINT,
					%ID.nam,
					-val(%Integer.chars), 0, 0);
	else
		%<attribute>.atr = newatr(`&%ID.nam->srcp,
					TYPINT,
					%ID.nam,
					val(%Integer.chars), 0, 0);
    %%

    ! ID STRING	
    %%
	%<attribute>.atr = newatr(`&%ID.nam->srcp,
				TYPSTR,
				%ID.nam,
				0, %STRING.fpos, %STRING.len);
    %%
    ;

---> <SYNONYMS>
<synonyms>			-- RETURNS: syns
    = 'SYNONYMS' <synonym_list>	       			%+('MESSAGE', 'DEFAULT')
    %%
	%<synonyms>.syns = %<synonym_list>.syns;
    %%
    ;

<synonym_list>			-- RETURNS: syns
    = <synonym>
    %%
	%<synonym_list>.syns = concat(NULL, %<synonym>.syn, SYNNOD);
    %%

    ! <synonym_list> <synonym>
    %%
	%<synonym_list>.syns = concat(%<synonym_list>.syns, %<synonym>.syn, SYNNOD);
    %%
    ;

<synonym>				-- RETURNS: syn
    = <id_list> '=' ID '.'
    %%
	%<synonym>.syn = newsyn(`&%'='.srcp,
			      %<id_list>.nams,
			      %ID.nam);
    %%
    ;

---> <MESSAGES>
<messages>			-- RETURNS: msgs
    = 'MESSAGE' <message_list>				%+('MESSAGE', 'DEFAULT')
    %%
	%<messages>.msgs = %<message_list>.msgs;
    %%
    ;

<message_list>			-- RETURNS: msgs
    = <message>
    %%
	%<message_list>.msgs = concat(NULL, %<message>.msg, MSGNOD);
    %%

    ! <message_list> <message>
    %%
	%<message_list>.msgs = concat(%<message_list>.msgs, %<message>.msg, MSGNOD);
    %%
    ;

<message>				-- RETURNS: msg
    = ID ':' <statements>			%-('QUIT', 'SAVE', 'RESTART', 'RESTORE')
    %%
	%<message>.msg = newmsg(`&%':'.srcp,
			      %ID.nam,
			      %<statements>.stms);
    %%
    ;

---> <SYNTAX>				-- RETURNS: stxs
<syntax>
    = 'SYNTAX' <syntax_list>			%+('MESSAGE', 'DEFAULT')
    %%
	%<syntax>.stxs = %<syntax_list>.stxs;
    %%
    ;

<syntax_list>
    = <syntax_item>
    %%
	%<syntax_list>.stxs = concat(NULL, %<syntax_item>.stx, STXNOD);
    %%

    ! <syntax_list> <syntax_item>
    %%
	%<syntax_list>.stxs = concat(%<syntax_list>.stxs, %<syntax_item>.stx, STXNOD);
    %%
    ;

<syntax_item>
    = ID '=' <syntax_elements> <optional_class_restrictions>
    %%
	%<syntax_item>.stx = newstx(`&%'='.srcp,
				  %ID.nam,
				  concat(%<syntax_elements>.elms,
				         newelm(`&%ID.nam->srcp, ELMEOS,
				                NULL,
				                FALSE),
				         ELMNOD),
				  %<optional_class_restrictions>.ress);
    %%
    ;

<syntax_elements>
    = <syntax_element>
    %%
	%<syntax_elements>.elms = concat(NULL, %<syntax_element>.elm, ELMNOD);
    %%

    ! <syntax_elements> <syntax_element>
    %%
	%<syntax_elements>.elms = concat(%<syntax_elements>.elms,
				       %<syntax_element>.elm,
	                                ELMNOD);
    %%
    ;

<syntax_element>
    = ID
    %%
	%<syntax_element>.elm = newelm(`&%ID.nam->srcp, ELMWRD,
				     %ID.nam,
				     FALSE);
    %%

    ! '(' ID ')' <optional_indicators>

    %%
	%<syntax_element>.elm = newelm(`&%ID.nam->srcp, ELMPAR,
				     %ID.nam,
				     %<optional_indicators>.bits);
    %%
    ;

<optional_indicators>
    =
    %%
	%<optional_indicators>.bits = 0;
    %%

    ! <optional_indicators> <indicator>
    %%
	%<optional_indicators>.bits |= %<indicator>.bits;
    %%
    ;

<indicator>
    = '*'
    %%
	%<indicator>.bits = 0x1;
    %%

    ! '!'
    %%
	%<indicator>.bits = 0x2;
    %%
    ;

<optional_class_restrictions>
    = '.'
    %%
	%<optional_class_restrictions>.ress = NULL;
    %%

    ! 'WHERE' <class_restrictions>
    %%
	%<optional_class_restrictions>.ress = %<class_restrictions>.ress;
    %%
    ;

<class_restrictions>
    = <class_restriction>
    %%
	%<class_restrictions>.ress = concat(NULL, %<class_restriction>.res, RESNOD);
    %%

    ! <class_restrictions> 'AND' <class_restriction>
    %%
	%<class_restrictions>.ress = concat(%<class_restrictions>.ress,
				       %<class_restriction>.res, RESNOD);
    %%
    ;

<class_restriction>
    = ID 'ISA' <classes> 'ELSE' <statements>		%-('QUIT', 'SAVE', 'RESTORE', 'EMPTY', 'RESTART')
    %%
	%<class_restriction>.res = newres(`&%'ISA'.srcp,
				    %ID.nam,
				    %<classes>.single,
				    %<classes>.classkd,
				    %<statements>.stms);
    %%
    ;

<classes>
    = <class_identifier>
    %%
	%<classes>.classkd = %<class_identifier>.classkd;
        %<classes>.single = TRUE;
    %%

    ! <classes> 'OR' <class_identifier>
    %%
	%<classes>.classkd = %<classes>.classkd | %<class_identifier>.classkd;
        %<classes>.single = FALSE;
    %%
    ;

<class_identifier>
    = 'OBJECT'
    %%
	%<class_identifier>.classkd = NAMOBJ;
    %%

    ! 'ACTOR'
    %%
	%<class_identifier>.classkd = NAMACT;
    %%

    ! 'CONTAINER'
    %%
	%<class_identifier>.classkd = NAMCNT;
    %%

    ! 'CONTAINER' 'OBJECT'
    %%
	%<class_identifier>.classkd = NAMCOBJ;
    %%

    ! 'CONTAINER' 'ACTOR'
    %%
	%<class_identifier>.classkd = NAMCACT;
    %%

    ! 'INTEGER'
    %%
	%<class_identifier>.classkd = NAMNUM;
    %%

    ! 'STRING'
    %%
	%<class_identifier>.classkd = NAMSTR;
    %%
    ;    


---> VERBS
<optional_verbs>		-- RETURNS: vrbs
    =
    %%
	%<optional_verbs>.vrbs = NULL;
    %%

    ! <optional_verbs> <verb>
    %%
	%<optional_verbs>.vrbs = concat(%<optional_verbs>.vrbs, %<verb>.vrb, VRBNOD);
    %%
    ;

<verb>			-- RETURNS: vrb
    = <verb_header> <verb_body> <verb_tail>
    %%
	%<verb>.vrb = newvrb(`&%<verb_header>.srcp,
			   %<verb_header>.nams,
			   %<verb_body>.alts);
	if (%<verb_tail>.nam != NULL) { /* END-id given */
	    if (strcmp(%<verb_header>.str, %<verb_tail>.nam->str) != 0)
		lmLog(`&%<verb_tail>.nam->srcp, 201, sevWAR, %<verb_header>.str);
	}
    %%
    ;

<verb_header>		-- RETURNS: srcp, str, nams
    = 'VERB' <id_list>
    %%
	%<verb_header>.srcp   = %'VERB'.srcp;
	%<verb_header>.str    = %<id_list>.str;
	%<verb_header>.nams   = %<id_list>.nams;
    %%
    ;

<verb_body>
    = <simple_verb_body>
    %%
	%<verb_body>.alts = concat(NULL,
				 newalt(`&nulsrcp,
					NULL,
					%<simple_verb_body>.chks,
					%<simple_verb_body>.qual,
					%<simple_verb_body>.stms),
				   ALTNOD);
    %%

    ! <verb_alternatives>
    %%
	%<verb_body>.alts = %<verb_alternatives>.alts;
    %%
    ;
<verb_alternatives>
    = <verb_alternative>
    %%
	%<verb_alternatives>.alts = concat(NULL, %<verb_alternative>.alt, ALTNOD);
    %%

    ! <verb_alternatives> <verb_alternative>
    %%
	%<verb_alternatives>.alts = concat(%<verb_alternatives>.alts,
					 %<verb_alternative>.alt,
	     				ALTNOD);
    %%
    ;

<verb_alternative>
    = 'WHEN' ID <simple_verb_body>
    %%
	%<verb_alternative>.alt = newalt(`&%'WHEN'.srcp,
				       %ID.nam,
				       %<simple_verb_body>.chks,
				       %<simple_verb_body>.qual,
				       %<simple_verb_body>.stms);
    %%
    ;

<simple_verb_body>		-- RETURNS: chks, stms, qual
    = <optional_checks> <optional_does>
    %%
	%<simple_verb_body>.chks = %<optional_checks>.chks;
	%<simple_verb_body>.qual = %<optional_does>.qual;
	%<simple_verb_body>.stms = %<optional_does>.stms;
    %%
    ;

<verb_tail>		-- RETURNS: nam
    = 'END' 'VERB' <optional_id> '.'
    %%
	%<verb_tail>.nam = %<optional_id>.nam;
    %%
    ;

----> CHECKS
<optional_checks>		-- RETURNS: chks
    =
    %%
	%<optional_checks>.chks = NULL;
    %%

    ! 'CHECK' <statements>
    %%
	%<optional_checks>.chks = concat(NULL, newchk(NULL, %<statements>.stms), STMNOD);
    %%

    ! 'CHECK' <check_list>
    %%
	%<optional_checks>.chks = %<check_list>.chks;
    %%
    ;

<check_list>		-- RETURNS: chks
    = <check>
    %%
	%<check_list>.chks = concat(NULL, %<check>.chk, CHKNOD);
    %%

    ! <check_list> 'AND' <check>
    %%
	%<check_list>.chks = concat(%<check_list>.chks, %<check>.chk, CHKNOD);
    %%
    ;

<check>			-- RETURNS: chk
    = <expression> 'ELSE' <statements>
    %%
	%<check>.chk = newchk(%<expression>.exp, %<statements>.stms);
    %%
    ;

----> DOES
<optional_does>		-- RETURNS: qual, srcp, stms
    =
    %%
	%<optional_does>.qual = QUAL_DEFAULT;
	%<optional_does>.stms = NULL;
    %%

    ! <does>
    -- Propagate!
    ;

<does>
    = 'DOES' <optional_qual> <statements>
    %%
	%<does>.qual = %<optional_qual>.qual;
	%<does>.srcp = %'DOES'.srcp;
	%<does>.stms = %<statements>.stms;
    %%
    ;


---> LOCATION
<location>		-- RETURNS: loc
    = <location_header> <location_body> <location_tail>
    %%
	%<location>.loc = newloc(`&%<location_header>.srcp,
			       %<location_header>.nam,
			       %<location_header>.nams,
			       %<location_body>.atrs,
			       %<location_body>.stms,
			       %<location_body>.stms2,
			       %<location_body>.exts,
			       %<location_body>.vrbs);
	if (%<location_tail>.nam != NULL) { /* END-id given */
	    if (!eqnams(%<location_header>.nam, %<location_tail>.nam))
		lmLog(`&%<location_tail>.nam->srcp, 201, sevWAR,
			%<location_header>.nam->str);
	}
    %%
    ;


<location_header>		-- RETURNS: srcp, nam, nams
    = 'LOCATION' ID <optional_name>
    %%
	%<location_header>.srcp = %'LOCATION'.srcp;
	%<location_header>.nam  = %ID.nam;
	%<location_header>.nams = %<optional_name>.nams;
    %%
    ;

<location_body>		-- RETURNS: atrs, stms, stms2, exts, vrbs
    =
    %%
	%<location_body>.atrs  = NULL;
	%<location_body>.stms  = NULL;
	%<location_body>.stms2 = NULL;
	%<location_body>.exts  = NULL;
	%<location_body>.vrbs  = NULL;
    %%

    ! <location_body> <location_body_part>
    %%
      if (%<location_body_part>.atrs != NULL)
	%<location_body>.atrs = combine(%<location_body>.atrs, %<location_body_part>.atrs);
      else if (%<location_body_part>.stms != NULL)
	if (%<location_body>.stms != NULL)
          /* DESCRIPTION Already declared */
	  lmLogv(`&%<location_body_part>.srcp, 204, sevERR, "DESCRIPTION", "location", NULL);
        else
	  %<location_body>.stms = %<location_body_part>.stms;
      else if (%<location_body_part>.stms2 != NULL)
	if (%<location_body>.stms2 != NULL)
          /* DOES Already declared */
	  lmLogv(`&%<location_body_part>.srcp, 204, sevERR, "DOES", "location", NULL);
        else {
	  %<location_body>.stms2 = %<location_body_part>.stms2;
	  if (%<location_body_part>.qual != QUAL_DEFAULT)
	    lmLog(`&%<location_body_part>.srcp, 210, sevERR, "");
	}
      else if (%<location_body_part>.ext != NULL)
	%<location_body>.exts = concat(%<location_body>.exts, %<location_body_part>.ext, EXTNOD);
      else
	%<location_body>.vrbs = concat(%<location_body>.vrbs, %<location_body_part>.vrb, VRBNOD);
    %%	
    ;

<location_body_part>		-- RETURNS: atrs, stms, stms2, ext, vrb
                                -- (srcp, qual if <description> or <does>)
    = <is> <attributes>
    %%
        %<location_body_part>.atrs = %<attributes>.atrs;
        %<location_body_part>.stms = NULL;
        %<location_body_part>.stms2 = NULL;
        %<location_body_part>.ext = NULL;
        %<location_body_part>.vrb = NULL;
    %%

    ! <description>
    %%
        %<location_body_part>.stms = %<description>.stms;
        %<location_body_part>.srcp = %<description>.srcp;
        %<location_body_part>.stms2 = NULL;
        %<location_body_part>.ext = NULL;
        %<location_body_part>.vrb = NULL;
        %<location_body_part>.atrs = NULL;
    %%

    ! <does>
    %%
        %<location_body_part>.stms2 = %<does>.stms;
        %<location_body_part>.srcp = %<does>.srcp;
	%<location_body_part>.qual = %<does>.qual;
        %<location_body_part>.ext = NULL;
        %<location_body_part>.vrb = NULL;
        %<location_body_part>.atrs = NULL;
        %<location_body_part>.stms = NULL;
    %%

    ! <exit>
    %%
        %<location_body_part>.ext = %<exit>.ext;
        %<location_body_part>.vrb = NULL;
        %<location_body_part>.atrs = NULL;
        %<location_body_part>.stms = NULL;
        %<location_body_part>.stms2 = NULL;
    %%

    ! <verb>
    %%
        %<location_body_part>.atrs = NULL;
        %<location_body_part>.stms = NULL;
        %<location_body_part>.stms2 = NULL;
        %<location_body_part>.ext = NULL;
        %<location_body_part>.vrb = %<verb>.vrb;
    %%
    ;

<location_tail>		-- RETURNS: nam
    = 'END' 'LOCATION' <optional_id> '.'
    %%
	%<location_tail>.nam = %<optional_id>.nam;
    %%
    ;

----> EXITS
<optional_exits>		-- RETURNS: exts
    =
    %%
	%<optional_exits>.exts = NULL;
    %%

    ! <optional_exits> <exit>
    %%
	%<optional_exits>.exts = concat(%<optional_exits>.exts, %<exit>.ext, EXTNOD);
    %%
    ;

<exit>			-- RETURNS: ext
    = 'EXIT' <id_list> 'TO' ID <optional_exit_body> '.'
    %%
	%<exit>.ext = newext(`&%'EXIT'.srcp,
			   %<id_list>.nams,
			   %ID.nam,
			   %<optional_exit_body>.chks,
			   %<optional_exit_body>.stms);
	if (%<optional_exit_body>.nam != NULL) { /* END-id given */
	    if (strcmp(%<id_list>.str, %<optional_exit_body>.nam->str) != 0)
		lmLog(`&%<optional_exit_body>.nam->srcp, 201, sevWAR, %<id_list>.str);
	}
    %%
    ;

<optional_exit_body>	-- RETURNS: chks, stms, nam
    =
    %%
	%<optional_exit_body>.chks = NULL;
	%<optional_exit_body>.stms = NULL;
	%<optional_exit_body>.nam	 = NULL;
    %%

    ! <optional_checks> <optional_does> 'END' 'EXIT' <optional_id>
    %%
	%<optional_exit_body>.chks = %<optional_checks>.chks;
	%<optional_exit_body>.stms = %<optional_does>.stms;
	if (%<optional_does>.qual != QUAL_DEFAULT)
	     lmLog(`&%<optional_does>.srcp, 210, sevERR, "");
	%<optional_exit_body>.nam = %<optional_id>.nam;
    %%
    ;

---> OBJECTS
<object>			-- RETURNS: obj
    = <object_header> <object_body> <object_tail>
    %%
	%<object>.obj = newobj(`&%<object_header>.srcp,
			     %<object_header>.nam,
			     %<object_header>.nams2,
			     %<object_header>.whr,
			     %<object_body>.cnt,
			     %<object_body>.atrs,
			     %<object_body>.stms,
			     %<object_body>.stms3,
			     %<object_body>.stms2,
			     %<object_body>.vrbs);
	if (%<object_tail>.nam != NULL) { /* END-id given */
	    if (!eqnams(%<object_header>.nam, %<object_tail>.nam))
		lmLog(`&%<object_tail>.nam->srcp, 201, sevWAR, %<object_header>.nam->str);
	}
    %%
    ;


<object_header>		-- RETURNS: srcp, nam, nams2, whr
    = 'OBJECT' ID <optional_where> <optional_names> <optional_where>
    %%
	%<object_header>.srcp = %'OBJECT'.srcp;
	%<object_header>.nam  = %ID.nam;
	%<object_header>.nams2 = %<optional_names>.nams2;
        if (%1<optional_where>.whr->whr != WHR_DEFAULT) {
          %<object_header>.whr  = %1<optional_where>.whr;
          if (%2<optional_where>.whr->whr != WHR_DEFAULT)
            lmLog(`&%2<optional_where>.whr->srcp, 340, sevWAR, %ID.nam->str);
        } else
          %<object_header>.whr  = %2<optional_where>.whr;
    %%
    ;

<object_body>		-- RETURNS: cnt, atrs, stms, stms2, stms3, vrbs
    = 
    %%
	%<object_body>.cnt   = NULL;
	%<object_body>.atrs  = NULL;
	%<object_body>.stms  = NULL; /* Description */
        %<object_body>.stms2 = NULL; /* Mentioned */
	%<object_body>.stms3 = NULL; /* The article */
	%<object_body>.vrbs  = NULL;
    %%

    ! <object_body> <object_body_part>
    %%
	if (%<object_body_part>.cnt != NULL)
          if (%<object_body>.cnt != NULL)
            /* CONTAINER Already declared */
	    lmLogv(`&%<object_body_part>.srcp, 204, sevERR, "CONTAINER", "object", NULL);
          else
	    %<object_body>.cnt = %<object_body_part>.cnt;
        else if (%<object_body_part>.atrs != NULL)
          %<object_body>.atrs = combine(%<object_body>.atrs, %<object_body_part>.atrs);
	else if (%<object_body_part>.stms != NULL)
          if (%<object_body>.stms != NULL)
            /* DESCRIPTION Already declared */
	    lmLogv(`&%<object_body_part>.srcp, 204, sevERR, "DESCRIPTION", "object", NULL);
	  else
	    %<object_body>.stms = %<object_body_part>.stms;
        else if (%<object_body_part>.stms2 != NULL)
          if (%<object_body>.stms2 != NULL)
            /* MENTIONED Already declared */
	    lmLogv(`&%<object_body_part>.srcp, 204, sevERR, "MENTIONED", "object", NULL);
          else
	    %<object_body>.stms2 = %<object_body_part>.stms2;
        else if (%<object_body_part>.stms3 != NULL)
          if (%<object_body>.stms3 != NULL)
            /* ARTICLE Already declared */
	    lmLogv(`&%<object_body_part>.srcp, 204, sevERR, "ARTICLE", "object", NULL);
          else
	    %<object_body>.stms3 = %<object_body_part>.stms3;
        else
 	  %<object_body>.vrbs  = concat(%<object_body>.vrbs, %<object_body_part>.vrb, VRBNOD);
    %%
    ;

<object_body_part>		-- RETURNS: cnt, atrs, stm, stm2, stm3, vrb
    = <properties>
    %%
	%<object_body_part>.cnt   = %<properties>.cnt;
	%<object_body_part>.srcp  = %<properties>.srcp;
	%<object_body_part>.atrs  = NULL;
	%<object_body_part>.stms  = NULL;
        %<object_body_part>.stms2 = NULL;
	%<object_body_part>.stms3 = NULL;
        %<object_body_part>.vrb   = NULL;
    %%

    ! <is> <attributes>					%+('ARTICLE')
    %%
	%<object_body_part>.atrs  = %<attributes>.atrs;
	%<object_body_part>.stms  = NULL;
        %<object_body_part>.stms2 = NULL;
	%<object_body_part>.stms3 = NULL;
	%<object_body_part>.vrb   = NULL;
	%<object_body_part>.cnt   = NULL;
    %%

    ! <description>
    %%
	%<object_body_part>.stms  = %<description>.stms;
	%<object_body_part>.srcp  = %<description>.srcp;
        %<object_body_part>.stms2 = NULL;
	%<object_body_part>.stms3 = NULL;
	%<object_body_part>.vrb   = NULL;
	%<object_body_part>.cnt   = NULL;
	%<object_body_part>.atrs  = NULL;
    %%

    ! <mentioned>
    %%
        %<object_body_part>.stms2 = %<mentioned>.stms;
	%<object_body_part>.srcp  = %<mentioned>.srcp;
	%<object_body_part>.stms3 = NULL;
	%<object_body_part>.vrb   = NULL;
	%<object_body_part>.cnt   = NULL;
	%<object_body_part>.atrs  = NULL;
	%<object_body_part>.stms  = NULL;
    %%

    ! <article>
    %%
	%<object_body_part>.stms3 = %<article>.stms;
	%<object_body_part>.srcp  = %<article>.srcp;
	%<object_body_part>.vrb   = NULL;
	%<object_body_part>.cnt   = NULL;
	%<object_body_part>.atrs  = NULL;
	%<object_body_part>.stms  = NULL;
        %<object_body_part>.stms2 = NULL;
    %%

    ! <verb>
    %%
	%<object_body_part>.vrb   = %<verb>.vrb;
	%<object_body_part>.cnt   = NULL;
	%<object_body_part>.atrs  = NULL;
	%<object_body_part>.stms  = NULL;
        %<object_body_part>.stms2 = NULL;
	%<object_body_part>.stms3 = NULL;
    %%
    ;

<object_tail>		-- RETURNS: nam
    = 'END' 'OBJECT' <optional_id> '.'
    %%
	%<object_tail>.nam = %<optional_id>.nam;
    %%
    ;


--
-- Attributes
--

<optional_attributes>	-- RETURNS: atrs
    =
    %%
	%<optional_attributes>.atrs = NULL;
    %%

    ! <optional_attributes> <is> <attributes>			%+('ARTICLE')
    %%
	%<optional_attributes>.atrs = combine(%<optional_attributes>.atrs, %<attributes>.atrs);
    %%
    ;

<is>			-- RETURNS: srcp
    = 'IS'
    %%
	%<is>.srcp = %'IS'.srcp;
    %%

    ! 'ARE'
    %%
	%<is>.srcp = %'ARE'.srcp;
    %%

    ! 'HAS'
    %%
	%<is>.srcp = %'HAS'.srcp;
    %%
    ;

<optional_description>	-- RETURNS: stms
    =
    %%
	%<optional_description>.stms = NULL;
    %%

    ! <description> 
    -- Propagate!
    ;

<description>		-- RETURNS: stms, srcp
    = 'DESCRIPTION' 
    %%
	%<description>.stms = concat(NULL, newstm(`&%'DESCRIPTION'.srcp,
							STM_NOP), STMNOD);
	%<description>.srcp = %'DESCRIPTION'.srcp;
    %%

    ! 'DESCRIPTION' <statements>
    %%
	%<description>.stms = %<statements>.stms;
	%<description>.srcp = %'DESCRIPTION'.srcp;
    %%
    ;

<article>			-- RETURNS: stms, srcp
    = 'ARTICLE' 
    %%
	%<article>.stms = concat(NULL, newstm(`&%'ARTICLE'.srcp,
							STM_NOP), STMNOD);
	%<article>.srcp = %'ARTICLE'.srcp;
    %%

    ! 'ARTICLE' <statements>
    %%
	%<article>.stms = %<statements>.stms;
	%<article>.srcp = %'ARTICLE'.srcp;
    %%
    ;

<mentioned>		-- RETURNS: stms, srcp
    = 'MENTIONED' <statements>
    %%
	%<mentioned>.stms = %<statements>.stms;
	%<mentioned>.srcp = %'MENTIONED'.srcp;
    %%
    ;


<optional_name>		-- RETURNS: nams
    =
    %%
	%<optional_name>.nams = NULL;
    %%

    ! <name>
    %%
	%<optional_name>.nams = %<name>.nams;
    %%
    ;

<optional_names>		-- RETURNS: nams2 (a list of lists)
    =
    %%
	%<optional_names>.nams2 = NULL;
    %%

    ! <optional_names> <name>
    %%
	%<optional_names>.nams2 = concat(%<optional_names>.nams2,
					     %<name>.nams, NAMNOD);
    %%
    ;

<name>			-- RETURNS: nams
    = 'NAME' <ids>					%+('ARTICLE', 'IN')
    %%
	%<name>.nams = %<ids>.nams;
    %%
    ;


---> CONTAINERS
<properties>					-- RETURNS: cnt, srcp
    = 'CONTAINER' <container_body>
    %%
	%<properties>.cnt = newcnt(`&%'CONTAINER'.srcp,
					  NULL,
					  %<container_body>.lims,
					  %<container_body>.stms,
					  %<container_body>.stms2);
        %<properties>.srcp = %'CONTAINER'.srcp;
	adv.cnts = concat(adv.cnts, %<properties>.cnt, CNTNOD);
    %%
    ;

<container>		-- RETURNS: cnt
    = <container_header> <container_body> <container_tail>
    %%
	%<container>.cnt = newcnt(`&%<container_header>.srcp,
				%<container_header>.nam,
				%<container_body>.lims,
				%<container_body>.stms,
				%<container_body>.stms2);
	if (%<container_tail>.nam != NULL) { /* END-id given */
	    if (!eqnams(%<container_header>.nam, %<container_tail>.nam))
		lmLog(`&%<container_tail>.nam->srcp, 201, sevWAR, %<container_header>.nam->str);
	}
    %%
    ;

<container_header>	-- RETURNS: srcp, nam
    = 'CONTAINER' ID
    %%
	%<container_header>.srcp = %'CONTAINER'.srcp;
	%<container_header>.nam  = %ID.nam;
    %%
    ;

<container_body>		-- RETURNS: lims, stms, stms2
    =  <optional_limits> <optional_header> <optional_empty>
    %%
	%<container_body>.lims  = %<optional_limits>.lims;
	%<container_body>.stms  = %<optional_header>.stms;
	%<container_body>.stms2 = %<optional_empty>.stms;
    %%
    ;

<container_tail>		-- RETURNS: nam
    = 'END' 'CONTAINER' <optional_id> '.'
    %%
	%<container_tail>.nam  = %<optional_id>.nam;
    %%
    ;

<optional_limits>		-- RETURNS: lims
    =
    %%
	%<optional_limits>.lims = NULL;
    %%

    ! 'LIMITS' <limits>					%+('ARTICLE')
    %%
	%<optional_limits>.lims = %<limits>.lims;
    %%
    ;

<limits>			-- RETURNS: lims
    = <limit>
    %%
	%<limits>.lims = concat(NULL, %<limit>.lim, LIMNOD);
    %%

    ! <limits> <limit>
    %%
	%<limits>.lims = concat(%<limits>.lims, %<limit>.lim, LIMNOD);
    %%
    ;

<limit>			-- RETURNS: lim
    = <limit_attribute> 'THEN' <statements>			%-('QUIT', 'SAVE', 'RESTORE', 'EMPTY', 'RESTART')
    %%
	%<limit>.lim = newlim(`&%'THEN'.srcp,
			    %<limit_attribute>.atr,
			    %<statements>.stms);
    %%
    ;

<limit_attribute>		-- RETURNS: atr
    = <attribute>
    %%
	%<limit_attribute>.atr = %<attribute>.atr;
    %%

    ! 'COUNT' Integer
    %%
	%<limit_attribute>.atr = newatr(`&%'COUNT'.srcp,
				      TYPINT,
				      newnam(`&%'COUNT'.srcp, "count"),
				      val(%Integer.chars), 0, 0);
    %%
    ;

<optional_header>		-- RETURNS: stms
    =
    %%
	%<optional_header>.stms = NULL;
    %%

    ! 'HEADER' <statements>
    %%
	%<optional_header>.stms = %<statements>.stms;
    %%
    ;

<optional_empty>		-- RETURNS: stms
    =
    %%
	%<optional_empty>.stms = NULL;
    %%

    ! 'ELSE' <statements>
    %%
	%<optional_empty>.stms = %<statements>.stms;
    %%
    ;

---> EVENTS
<event>			-- RETURNS: evt
    = <event_header> <statements> <event_tail>
    %%
	%<event>.evt = newevt(`&%<event_header>.srcp,
			    %<event_header>.nam,
			    %<statements>.stms);
	if (%<event_tail>.nam != NULL) { /* END-id given */
	    if (!eqnams(%<event_header>.nam, %<event_tail>.nam))
		lmLog(`&%<event_tail>.nam->srcp, 201, sevWAR, %<event_header>.nam->str);
	}
    %%
    ;

<event_header>		-- RETURNS: srcp, nam
    ='EVENT' ID
    %%
	%<event_header>.srcp = %'EVENT'.srcp;
	%<event_header>.nam  = %ID.nam;
    %%
    ;

<event_tail>		-- RETURNS: nam
    = 'END' 'EVENT' <optional_id> '.'
    %%
	%<event_tail>.nam = %<optional_id>.nam;
    %%
    ;

---> ACTORS
<actor>			-- RETURNS: act
    = <actor_header> <actor_body> <actor_tail>
    %%
	%<actor>.act = newact(`&%<actor_header>.srcp,
			    %<actor_header>.nam,
			    %<actor_header>.nams2,
			    %<actor_header>.whr,
			    %<actor_body>.cnt,
			    %<actor_body>.atrs,
			    %<actor_body>.stms,
			    %<actor_body>.vrbs,
			    %<actor_body>.scrs);
	if (%<actor_tail>.nam != NULL) { /* END-id given */
	    if (!eqnams(%<actor_header>.nam, %<actor_tail>.nam))
		lmLog(`&%<actor_tail>.nam->srcp, 201, sevWAR, %<actor_header>.nam->str);
	}
    %%
    ;

<actor_header>		-- RETURNS: srcp, nam, nams2, whr
    = 'ACTOR' ID <optional_where> <optional_names> <optional_where>
    %%
	%<actor_header>.srcp = %'ACTOR'.srcp;
	%<actor_header>.nam  = %ID.nam;
	%<actor_header>.nams2 = %<optional_names>.nams2;
        if (%1<optional_where>.whr->whr != WHR_DEFAULT) {
          %<actor_header>.whr  = %1<optional_where>.whr;
          if (%2<optional_where>.whr->whr != WHR_DEFAULT)
            lmLog(`&%2<optional_where>.whr->srcp, 340, sevWAR, %ID.nam->str);
        } else
          %<actor_header>.whr  = %2<optional_where>.whr;
    %%
    ;

<actor_body>		-- RETURNS: cnt, atrs, stms, vrbs, scrs
    = 
    %%
	%<actor_body>.cnt   = NULL;
	%<actor_body>.atrs  = NULL;
	%<actor_body>.stms  = NULL; /* Description */
	%<actor_body>.vrbs  = NULL;
        %<actor_body>.scrs  = NULL; /* The scripts */
    %%

    ! <actor_body> <actor_body_part>
    %%
	if (%<actor_body_part>.cnt != NULL)
          if (%<actor_body>.cnt != NULL)
            /* CONTAINER Already declared */
	    lmLogv(`&%<actor_body_part>.srcp, 204, sevERR, "CONTAINER", "actor", NULL);
          else
	    %<actor_body>.cnt = %<actor_body_part>.cnt;
        else if (%<actor_body_part>.atrs != NULL)
          %<actor_body>.atrs = combine(%<actor_body>.atrs, %<actor_body_part>.atrs);
	else if (%<actor_body_part>.stms != NULL)
          if (%<actor_body>.stms != NULL)
            /* DESCRIPTION Already declared */
	    lmLogv(`&%<actor_body_part>.srcp, 204, sevERR, "DESCRIPTION", "actor", NULL);
	  else
	    %<actor_body>.stms = %<actor_body_part>.stms;
        else if (%<actor_body_part>.vrb != NULL)
 	  %<actor_body>.vrbs  = concat(%<actor_body>.vrbs, %<actor_body_part>.vrb, VRBNOD);
        else
 	  %<actor_body>.scrs  = concat(%<actor_body>.scrs, %<actor_body_part>.scr, VRBNOD);
    %%
    ;

<actor_body_part>		-- RETURNS: cnt, atrs, stms, vrb, scr
    = <properties>
    %%
	%<actor_body_part>.cnt   = %<properties>.cnt;
	%<actor_body_part>.srcp  = %<properties>.srcp;
	%<actor_body_part>.atrs  = NULL;
	%<actor_body_part>.stms  = NULL;
        %<actor_body_part>.vrb   = NULL;
	%<actor_body_part>.scr   = NULL;
    %%

    ! <is> <attributes>
    %%
	%<actor_body_part>.atrs  = %<attributes>.atrs;
	%<actor_body_part>.stms  = NULL;
	%<actor_body_part>.vrb   = NULL;
	%<actor_body_part>.scr   = NULL;
	%<actor_body_part>.cnt   = NULL;
    %%

    ! <description>
    %%
	%<actor_body_part>.stms  = %<description>.stms;
	%<actor_body_part>.srcp  = %<description>.srcp;
        %<actor_body_part>.stms2 = NULL;
	%<actor_body_part>.stms3 = NULL;
	%<actor_body_part>.vrb   = NULL;
	%<actor_body_part>.scr   = NULL;
	%<actor_body_part>.cnt   = NULL;
	%<actor_body_part>.atrs  = NULL;
    %%

    ! <verb>
    %%
	%<actor_body_part>.vrb   = %<verb>.vrb;
	%<actor_body_part>.scr   = NULL;
	%<actor_body_part>.cnt   = NULL;
	%<actor_body_part>.atrs  = NULL;
	%<actor_body_part>.stms  = NULL;
    %%

    ! <script>
    %%
	%<actor_body_part>.scr   = %<script>.scr;
	%<actor_body_part>.cnt   = NULL;
	%<actor_body_part>.atrs  = NULL;
	%<actor_body_part>.stms  = NULL;
	%<actor_body_part>.vrb   = NULL;
    %%
    ;

<actor_tail>		-- RETURNS: nam
    = 'END' 'ACTOR' <optional_id> '.'
    %%
	%<actor_tail>.nam = %<optional_id>.nam;
    %%
    ;

----> SCRIPTS
<optional_actor_script>	-- RETURNS: scrs
    = 
    %%
	%<optional_actor_script>.scrs = NULL;
    %%

    ! <optional_actor_script> <script>
    %%
	%<optional_actor_script>.scrs = concat(%<optional_actor_script>.scrs,
					     %<script>.scr, SCRNOD);
    %%
    ;

<script>			-- RETURNS: scr
    = 'SCRIPT' <integer_or_id> '.' <optional_description> <step_list>
    %%
	%<script>.scr = newscr(`&%'SCRIPT'.srcp,
			     %<integer_or_id>.nam,
			     %<integer_or_id>.val,
			     %<optional_description>.stms,
			     %<step_list>.stps);
    %%
    ;

<step_list>		-- RETURNS: stps
    = <step>
    %%
	%<step_list>.stps = concat(NULL, %<step>.stp, STPNOD);
    %%

    ! <step_list> <step>
    %%
	%<step_list>.stps = concat(%<step_list>.stps, %<step>.stp, STPNOD);
    %%
    ;

<step>			-- RETURNS: stp
    = 'STEP' <statements>
    %%
	%<step>.stp = newstp(`&%'STEP'.srcp,
			   0,
			   NULL,
			   %<statements>.stms);
    %%

    ! 'STEP' 'AFTER' Integer <statements>
    %%
	%<step>.stp = newstp(`&%'STEP'.srcp,
			   val(%Integer.chars),
			   NULL,
			   %<statements>.stms);
    %%

    ! 'STEP' 'WAIT' 'UNTIL' <expression> <statements>
    %%
	%<step>.stp = newstp(`&%'STEP'.srcp,
			   0,
			   %<expression>.exp,
			   %<statements>.stms);
    %%
    ;

----> RULES
<rule>			-- RETURNS: rul
    = 'WHEN' <expression> '=>' <statements>
    %%
	%<rule>.rul = newrul(`&%'WHEN'.srcp,
			   %<expression>.exp,
			   %<statements>.stms);
    %%
    ;

---> START
<start>
    = 'START' <where> '.' <optional_statements>
    %%
	adv.whr	 = %<where>.whr;
	adv.stms = %<optional_statements>.stms;
    %%
    ;

---> STATEMENTS
<optional_statements>	-- RETURNS: stms
    =
    %%
	%<optional_statements>.stms = NULL;
    %%

    ! <statements>
    %%
	%<optional_statements>.stms = %<statements>.stms;
    %%
    ;

<statements>		-- RETURNS: stms
    = <statement>
    %%
	%<statements>.stms = concat(NULL, %<statement>.stm, STMNOD);
    %%

    ! <statements> <statement>
    %%
	%<statements>.stms = concat(%<statements>.stms, %<statement>.stm, STMNOD);
    %%
    ;

<statement>		-- RETURNS: stm
    = <output_statement>
    %%
	%<statement>.stm = %<output_statement>.stm;
    %%

    ! <manipulation_statement>
    %%
	%<statement>.stm = %<manipulation_statement>.stm;
    %%

    ! <assignment_statement>
    %%
	%<statement>.stm = %<assignment_statement>.stm;
    %%

    ! <comparison_statement>
    %%
	%<statement>.stm = %<comparison_statement>.stm;
    %%

    ! <actor_statement>
    %%
	%<statement>.stm = %<actor_statement>.stm;
    %%

    ! <event_statement>
    %%
	%<statement>.stm = %<event_statement>.stm;
    %%

    ! <special_statement>
    %%
	%<statement>.stm = %<special_statement>.stm;
    %%
    ;

----> Output Statements
<output_statement>		-- RETURNS: stm
    = STRING
    %%
	%<output_statement>.stm = newstm(`&%STRING.srcp, STM_PRINT);
	%<output_statement>.stm->fields.print.fpos = %STRING.fpos;
	%<output_statement>.stm->fields.print.len = %STRING.len;
    %%

    ! 'DESCRIBE' <what> '.'
    %%
	%<output_statement>.stm = newstm(`&%'DESCRIBE'.srcp, STM_DESCRIBE);
	%<output_statement>.stm->fields.describe.wht = %<what>.wht;
    %%

    ! 'SAY' <expression> '.'
    %%
	%<output_statement>.stm = newstm(`&%'SAY'.srcp, STM_SAY);
	%<output_statement>.stm->fields.say.exp = %<expression>.exp;
    %%

    ! 'LIST' <what> '.'
    %%
	%<output_statement>.stm = newstm(`&%'LIST'.srcp, STM_LIST);
	%<output_statement>.stm->fields.list.wht = %<what>.wht;
    %%	  
    ;

<special_statement>	-- RETURNS: stm
    = 'QUIT' '.'
    %%
	%<special_statement>.stm = newstm(`&%'QUIT'.srcp, STM_QUIT);
    %%

    ! 'LOOK' '.'
    %%
	%<special_statement>.stm = newstm(`&%'LOOK'.srcp, STM_LOOK);
    %%

    ! 'SAVE' '.'
    %%
	%<special_statement>.stm = newstm(`&%'SAVE'.srcp, STM_SAVE);
    %%

    ! 'RESTORE' '.'
    %%
	%<special_statement>.stm = newstm(`&%'RESTORE'.srcp, STM_RESTORE);
    %%

    ! 'RESTART' '.'
    %%
	%<special_statement>.stm = newstm(`&%'RESTART'.srcp, STM_RESTART);
    %%

    ! 'SCORE' <optional_integer> '.'
    %%
	%<special_statement>.stm = newstm(`&%'SCORE'.srcp, STM_SCORE);
	if (%<optional_integer>.str != NULL) {
	    %<special_statement>.stm->fields.score.score =
						val(%<optional_integer>.str);
	    %<special_statement>.stm->fields.score.count = ++scocnt;
	} else
	    %<special_statement>.stm->fields.score.count = 0;
    %%

    ! 'VISITS' Integer '.'
    %%
	%<special_statement>.stm = newstm(`&%'VISITS'.srcp, STM_VISITS);
	%<special_statement>.stm->fields.visits.count = val(%Integer.chars);
    %%

    ! 'SYSTEM' STRING '.'
    %%
	%<special_statement>.stm = newstm(`&%'SYSTEM'.srcp, STM_SYSTEM);
	%<special_statement>.stm->fields.system.fpos = %STRING.fpos;
	%<special_statement>.stm->fields.system.len = %STRING.len;
    %%
    ;
	

----> Manipulation Statements
<manipulation_statement>			-- RETURNS: stm
    = 'EMPTY' <what> <optional_where> '.'
    %%
	%<manipulation_statement>.stm = newstm(`&%'EMPTY'.srcp, STM_EMPTY);
	%<manipulation_statement>.stm->fields.empty.wht = %<what>.wht;
	%<manipulation_statement>.stm->fields.empty.whr = %<optional_where>.whr;
    %%	  

    ! 'LOCATE' <what> <where> '.'
    %%
	%<manipulation_statement>.stm = newstm(`&%'LOCATE'.srcp, STM_LOCATE);
	%<manipulation_statement>.stm->fields.locate.wht = %<what>.wht;
	%<manipulation_statement>.stm->fields.locate.whr = %<where>.whr;
    %%	  
    ;


----> Event Statements
<event_statement>			-- RETURNS: stm
    = 'SCHEDULE' ID <optional_where> 'AFTER' <expression> '.'
    %%
	%<event_statement>.stm = newstm(`&%'SCHEDULE'.srcp, STM_SCHEDULE);
	%<event_statement>.stm->fields.schedule.nam  = %ID.nam;
	%<event_statement>.stm->fields.schedule.whr  = %<optional_where>.whr;
	%<event_statement>.stm->fields.schedule.when = %<expression>.exp;
    %%	  

    ! 'CANCEL' ID '.'
    %%
	%<event_statement>.stm = newstm(`&%'CANCEL'.srcp, STM_CANCEL);
	%<event_statement>.stm->fields.cancel.nam = %ID.nam;
    %%	  
    ;


----> Assignment Statements
<assignment_statement>		-- RETURNS: stm
    = 'MAKE' <what> <something> '.'
    %%
	%<assignment_statement>.stm = newstm(`&%'MAKE'.srcp, STM_MAKE);
	%<assignment_statement>.stm->fields.make.wht = %<what>.wht;
	%<assignment_statement>.stm->fields.make.not = %<something>.not;
	%<assignment_statement>.stm->fields.make.atr = %<something>.nam;
    %%

    ! 'SET' <attribute_reference> 'TO' <expression> '.'
    %%
	%<assignment_statement>.stm = newstm(`&%'SET'.srcp, STM_SET);
	%<assignment_statement>.stm->fields.set.atr = %<attribute_reference>.nam;
	%<assignment_statement>.stm->fields.set.wht = %<attribute_reference>.wht;
	%<assignment_statement>.stm->fields.set.exp = %<expression>.exp;
    %%

    ! 'INCREASE' <attribute_reference> <optional_by_clause> '.'
    %%
	%<assignment_statement>.stm = newstm(`&%'INCREASE'.srcp, STM_INCR);
	%<assignment_statement>.stm->fields.incr.atr = %<attribute_reference>.nam;
	%<assignment_statement>.stm->fields.incr.wht = %<attribute_reference>.wht;
	%<assignment_statement>.stm->fields.incr.step = %<optional_by_clause>.exp;
    %%

    ! 'DECREASE' <attribute_reference> <optional_by_clause> '.'
    %%
	%<assignment_statement>.stm = newstm(`&%'DECREASE'.srcp, STM_DECR);
	%<assignment_statement>.stm->fields.incr.atr = %<attribute_reference>.nam;
	%<assignment_statement>.stm->fields.incr.wht = %<attribute_reference>.wht;
	%<assignment_statement>.stm->fields.incr.step = %<optional_by_clause>.exp;
    %%
    ;

<optional_by_clause>
    =
    %%
	%<optional_by_clause>.exp = NULL;
    %%

    ! 'BY' <expression>
    %%
	%<optional_by_clause>.exp = %<expression>.exp;
    %%
    ;


----> Comparison Statements
<comparison_statement>
    = <if_statement>
    %%
	%<comparison_statement>.stm = %<if_statement>.stm;
    %%

    ! <depending_statement>
    %%
	%<comparison_statement>.stm = %<depending_statement>.stm;
    %%
    ;


<if_statement>			-- RETURNS: stm
    = 'IF' <expression> 'THEN' <statements> <optional_elsif_list> <optional_else_part> 'END' 'IF' '.'
    %%
	%<if_statement>.stm = newstm(`&%'IF'.srcp, STM_IF);
	%<if_statement>.stm->fields.iff.exp = %<expression>.exp;
	%<if_statement>.stm->fields.iff.thn = %<statements>.stms;
	if (%<optional_elsif_list>.stms == NULL)
	    %<if_statement>.stm->fields.iff.els = %<optional_else_part>.stms;
	else {
	    /* Connect the else_part to the elsif_list */
	    %<optional_elsif_list>.stms->tail->element.stm->fields.iff.els =
			%<optional_else_part>.stms;
	    %<optional_elsif_list>.stms->next = NULL;
	    %<if_statement>.stm->fields.iff.els = %<optional_elsif_list>.stms;
	}
    %%
    ;

<optional_elsif_list>
    =
    %%
	%<optional_elsif_list>.stms = NULL;
    %%

    ! <elsif_list>
    %%
	%<optional_elsif_list>.stms = %<elsif_list>.stms;
    %%
    ;
    
<elsif_list>		-- RETURNS: stms
    = 'ELSIF' <expression> 'THEN' <statements>
    %%{
	StmNod *stm;

	stm = newstm(`&%'ELSIF'.srcp, STM_IF);
	stm->fields.iff.exp = %<expression>.exp;
	stm->fields.iff.thn = %<statements>.stms;
	stm->fields.iff.els = NULL;

	/* Now make a list of the ELSIF */
	%<elsif_list>.stms = concat(NULL, stm, STMNOD);
    }%%

    ! <elsif_list> 'ELSIF' <expression> 'THEN' <statements>
    %%{
	StmNod *stm;

	stm = newstm(`&%'ELSIF'.srcp, STM_IF);
	stm->fields.iff.exp = %<expression>.exp;
	stm->fields.iff.thn = %<statements>.stms;
	stm->fields.iff.els = NULL;

	/* Now connect it to the previous ELSIFs, I.e connect the */
	/* else-part of the last element in the elsif_list to this */
	/* IF-statement and also concat this element to the list so */
	/* we'll know were the end is next time */
	%<elsif_list>.stms->tail->element.stm->fields.iff.els = concat(NULL, stm, STMNOD);
	%<elsif_list>.stms = concat(%<elsif_list>.stms, stm, STMNOD);
    }%%
    ;

<optional_else_part>		-- RETURNS: stms
    =
    %%
	%<optional_else_part>.stms = NULL;
    %%

    ! 'ELSE' <statements>
    %%
	%<optional_else_part>.stms = %<statements>.stms;
    %%
    ;

<depending_statement>
    = 'DEPENDING' 'ON' <primary> <depend_cases> 'END' 'DEPEND' '.'
    %%{ StmNod *stm;
	stm = newstm(`&%'DEPENDING'.srcp, STM_DEPEND);
	stm->fields.depend.exp = %<primary>.exp;
	stm->fields.depend.cases = %<depend_cases>.cases;

	%<depending_statement>.stm = stm;
    }%%
    ;

<depend_cases>				-- RETURNS: cases
    = <depend_case>
    %%
	%<depend_cases>.cases = concat(NULL, %<depend_case>.stm, CASENOD);
    %%

    ! <depend_cases> <depend_case>
    %%
	%<depend_cases>.cases = concat(%<depend_cases>.cases, %<depend_case>.stm, CASENOD);
    %%
    ;

<depend_case>				-- RETURNS: stm
    = <right_hand_side> ':' <statements>
    %%
        %<depend_case>.stm = newstm(`&%':'.srcp, STM_DEPCASE);
        %<depend_case>.stm->fields.depcase.exp = %<right_hand_side>.exp;
        %<depend_case>.stm->fields.depcase.stms = %<statements>.stms;
    %%

    ! 'ELSE' <statements>
    %%
        %<depend_case>.stm = newstm(`&%'ELSE'.srcp, STM_DEPCASE);
        %<depend_case>.stm->fields.depcase.exp = NULL;
        %<depend_case>.stm->fields.depcase.stms = %<statements>.stms;
    %%
    ;

----> Actor statement
<actor_statement>			-- RETURNS: stm
    = 'USE' 'SCRIPT' <integer_or_id> <optional_for_actor> '.'
    %%
	%<actor_statement>.stm = newstm(`&%'USE'.srcp, STM_USE);
	%<actor_statement>.stm->fields.use.script = %<integer_or_id>.nam;
	%<actor_statement>.stm->fields.use.scriptno = %<integer_or_id>.val;
	%<actor_statement>.stm->fields.use.actor = %<optional_for_actor>.nam;
    %%
    ;

<optional_for_actor>	-- RETURNS: nam
    =
    %%
	%<optional_for_actor>.nam = NULL;
    %%

    ! 'FOR' ID
    %%
	%<optional_for_actor>.nam = %ID.nam;
    %%
    ;


---> EXPRESSIONS
<expression>		-- RETURNS: exp
    = <term>
    %%
	%<expression>.exp = %<term>.exp;
    %%

    ! <expression> 'OR' <term>
    %% { ExpNod *exp;
	exp = newexp(`&%'OR'.srcp, EXPBIN);
	exp->not	      = FALSE;
	exp->fields.bin.op    = OP_OR;;
	exp->fields.bin.left  = %<expression>.exp;
	exp->fields.bin.right = %<term>.exp;
	%<expression>.exp = exp;
    } %%
    ;

<term>			-- RETURNS: exp
    = <factor>
    %%
	%<term>.exp = %<factor>.exp;
    %%

    ! <term> 'AND' <factor>
    %% { ExpNod *exp;
	exp = newexp(`&%'AND'.srcp, EXPBIN);
	exp->not	      = FALSE;
	exp->fields.bin.op    = OP_AND;
	exp->fields.bin.left  = %<term>.exp;
	exp->fields.bin.right = %<factor>.exp;
	%<term>.exp = exp;
    } %%
    ;

<factor>			-- RETURNS: exp
    = <primary>
    %%
	%<factor>.exp = %<primary>.exp;
    %%

    ! <primary> <right_hand_side>
    %%
        /* <right_hand_side> contains the top expr, hang in the <primary> and
           propagate that node */
        switch (%<right_hand_side>.expkd) {
	case EXPBIN: %<right_hand_side>.exp->fields.bin.left = %<primary>.exp; break;
	case EXPWHR: %<right_hand_side>.exp->fields.whr.wht = %<primary>.exp; break;
	case EXPATR: %<right_hand_side>.exp->fields.atr.wht = %<primary>.exp; break;
	case EXPBTW: %<right_hand_side>.exp->fields.btw.val = %<primary>.exp; break;
	default: syserr("Unrecognized switch in <right_hand_side> semantic rule.");
	}
        %<factor>.exp = %<right_hand_side>.exp;
    %%
    ;

<right_hand_side>
    = <binop> <primary>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPBIN;
	exp = newexp(`&%<binop>.srcp, EXPBIN);
	exp->fields.bin.op = %<binop>.op;
	exp->fields.bin.right = %<primary>.exp;
	%<right_hand_side>.exp = exp;
    } %%

    ! <optional_not> <relop> <primary>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPBIN;
	exp = newexp(`&%<relop>.srcp, EXPBIN);
	exp->not = %<optional_not>.not;
	exp->fields.bin.op = %<relop>.op;
	exp->fields.bin.right = %<primary>.exp;
	%<right_hand_side>.exp = exp;
    } %%

    ! <optional_not> <where>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPWHR;
	exp = newexp(`&%<where>.srcp, EXPWHR);
	exp->not = %<optional_not>.not;
	exp->fields.whr.whr = %<where>.whr;
	%<right_hand_side>.exp = exp;
    } %%

    ! <is> <something>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPATR;
	exp = newexp(`&%<is>.srcp, EXPATR);
	exp->not = %<something>.not;
	exp->fields.atr.atr = %<something>.nam;
	%<right_hand_side>.exp = exp;
    } %%

    ! <optional_not> 'BETWEEN' <factor> 'AND' <factor>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPBTW;
	exp = newexp(`&%'BETWEEN'.srcp, EXPBTW);
	exp->not = %<optional_not>.not;
	exp->fields.btw.low = %1<factor>.exp;
	exp->fields.btw.high = %2<factor>.exp;
	%<right_hand_side>.exp = exp;
    } %%

    ! <optional_not> 'CONTAINS' <factor>
    %% { ExpNod *exp;
        %<right_hand_side>.expkd = EXPBIN;
	exp = newexp(`&%'CONTAINS'.srcp, EXPBIN);
	exp->not = %<optional_not>.not;
	exp->fields.bin.op = OP_CONTAINS;
	exp->fields.bin.right = %<factor>.exp;
	%<right_hand_side>.exp = exp;
    } %%
    ;


<primary>				-- RETURNS: exp
    = '(' <expression> ')'
    %%
	%<primary>.exp = %<expression>.exp;
    %%

    ! <optional_minus> Integer
    %%
	%<primary>.exp = newexp(`&%Integer.srcp, EXPINT);
	if (%<optional_minus>.minus)
	  %<primary>.exp->fields.val.val = -val(%Integer.chars);
	else
	  %<primary>.exp->fields.val.val = val(%Integer.chars);
    %%

    ! STRING
    %%
	%<primary>.exp = newexp(&%STRING.srcp, EXPSTR);
	%<primary>.exp->fields.str.fpos = %STRING.fpos;
	%<primary>.exp->fields.str.len = %STRING.len;
    %%

    ! <what>
    %%
	%<primary>.exp = newexp(`&%<what>.srcp, EXPWHT);
	%<primary>.exp->fields.wht.wht = %<what>.wht;
    %%

    ! <attribute_reference>
    %%
	%<primary>.exp = newexp(`&%<attribute_reference>.srcp, EXPATR);
	%<primary>.exp->fields.atr.atr = %<attribute_reference>.nam;
	%<primary>.exp->fields.atr.wht = newexp(`&%<attribute_reference>.srcp, EXPWHT);
	%<primary>.exp->fields.atr.wht->fields.wht.wht = %<attribute_reference>.wht;
    %%

    ! <aggregate> <where>
    %%
	%<primary>.exp = newexp(`&%<aggregate>.srcp, EXPAGR);
	%<primary>.exp->fields.agr.agr	= %<aggregate>.agr;
	%<primary>.exp->fields.agr.atrnam = %<aggregate>.nam;
	%<primary>.exp->fields.agr.whr	= %<where>.whr;
    %%

    ! 'RANDOM' <primary> 'TO' <primary>
    %%
	%<primary>.exp = newexp(`&%'RANDOM'.srcp, EXPRND);
	%<primary>.exp->fields.rnd.from	= %2<primary>.exp;
	%<primary>.exp->fields.rnd.to	= %3<primary>.exp;
    %%

    ! 'SCORE'
    %%
	%<primary>.exp = newexp(`&%'SCORE'.srcp, EXPSCORE);
    %%
    ;


<aggregate>			-- RETURNS: srcp, agr, nam
    = 'SUM' 'OF' ID
    %%
	%<aggregate>.srcp = %'SUM'.srcp;
	%<aggregate>.agr = AGR_SUM;
	%<aggregate>.nam = %ID.nam;
    %%

    ! 'MAX' 'OF' ID
    %%
	%<aggregate>.srcp = %'MAX'.srcp;
	%<aggregate>.agr = AGR_MAX;
	%<aggregate>.nam = %ID.nam;
    %%

    ! 'COUNT'
    %%
	%<aggregate>.srcp = %'COUNT'.srcp;
	%<aggregate>.agr = AGR_COUNT;
	%<aggregate>.nam = NULL;
    %%
    ;

---> MISCELLANEOUS
<something>		-- RETURNS: not, nam
    = <optional_not> ID
    %%
	%<something>.not = %<optional_not>.not;
	%<something>.nam = %ID.nam;
    %%
    ;

<what>			-- RETURNS: srcp, wht
    = 'OBJECT'
    %%
	%<what>.wht = newwht(`&%'OBJECT'.srcp, WHT_OBJ, NULL);
	%<what>.srcp = %'OBJECT'.srcp;
    %%

    ! 'LOCATION'
    %%
	%<what>.wht = newwht(`&%'LOCATION'.srcp, WHT_LOC, NULL);
	%<what>.srcp = %'LOCATION'.srcp;
    %%

    ! 'ACTOR'
    %%
	%<what>.wht = newwht(`&%'ACTOR'.srcp, WHT_ACT, NULL);
	%<what>.srcp = %'ACTOR'.srcp;
    %%

    ! ID
    %%
	%<what>.wht = newwht(`&%ID.nam->srcp, WHT_ID, %ID.nam);
	%<what>.srcp = %ID.nam->srcp;
    %%
    ;


<optional_where>		-- RETURNS: whr
    =					%-('HERE', 'NEARBY', 'AT', 'IN')
    %%
        %<optional_where>.whr = newwhr(`&nulsrcp, WHR_DEFAULT, NULL);
    %%
    
    ! <where>
    %%
        %<optional_where>.whr = %<where>.whr;
    %%
    ;

<where>			-- RETURNS: srcp, whr
    = 'HERE'
    %%
        %<where>.whr = newwhr(`&%'HERE'.srcp, WHR_HERE, NULL);
	%<where>.srcp = %'HERE'.srcp;
    %%

    ! 'NEARBY'
    %%
        %<where>.whr = newwhr(`&%'NEARBY'.srcp, WHR_NEAR, NULL);
	%<where>.srcp = %'NEARBY'.srcp;
    %%

    ! 'AT' <what>
    %%
        %<where>.whr = newwhr(`&%'AT'.srcp, WHR_AT, %<what>.wht);
	%<where>.srcp = %'AT'.srcp;
    %%

    ! 'IN' <what>
    %%
        %<where>.whr = newwhr(`&%'IN'.srcp, WHR_IN, %<what>.wht);
	%<where>.srcp = %'IN'.srcp;
    %%
    ;

<binop>			-- RETURNS: op, srcp
    = '+'
    %%
	%<binop>.op = OP_PLUS;
	%<binop>.srcp = %'+'.srcp;
    %%

    ! '-'
    %%
	%<binop>.op = OP_MINUS;
	%<binop>.srcp = %'-'.srcp;
    %%

    ! '*'
    %%
	%<binop>.op = OP_MULT;
	%<binop>.srcp = %'*'.srcp;
    %%

    ! '/'
    %%
	%<binop>.op = OP_DIV;
	%<binop>.srcp = %'/'.srcp;
    %%
    ;


<relop>			-- RETURNS: op, srcp
    = '<>'
    %%
        %<relop>.op   = OP_NE;
	%<relop>.srcp = %'<>'.srcp;
    %%

    ! '='
    %%
        %<relop>.op   = OP_EQ;
	%<relop>.srcp = %'='.srcp;
    %%

    ! '=='
    %%
        %<relop>.op   = OP_EXACT;
	%<relop>.srcp = %'=='.srcp;
    %%

    ! '>='
    %%
        %<relop>.op   = OP_GE;
	%<relop>.srcp = %'>='.srcp;
    %%

    ! '<='
    %%
        %<relop>.op   = OP_LE;
	%<relop>.srcp = %'<='.srcp;
    %%

    ! '>'
    %%
        %<relop>.op   = OP_GT;
	%<relop>.srcp = %'>'.srcp;
    %%

    ! '<'
    %%
        %<relop>.op   = OP_LT;
	%<relop>.srcp = %'<'.srcp;
    %%
    ;



<optional_qual>		-- RETURNS: qual, srcp
    =
    %%
        %<optional_qual>.qual = QUAL_DEFAULT;
    %%

    ! 'BEFORE'
    %%
        %<optional_qual>.qual = QUAL_BEFORE;
        %<optional_qual>.srcp = %'BEFORE'.srcp;
    %%

    ! 'AFTER'
    %%
        %<optional_qual>.qual = QUAL_AFTER;
        %<optional_qual>.srcp = %'AFTER'.srcp;
    %%

    ! 'ONLY'
    %%
        %<optional_qual>.qual = QUAL_ONLY;
        %<optional_qual>.srcp = %'ONLY'.srcp;
    %%
    ;


<optional_not>
    =
    %%
        %<optional_not>.not = FALSE;
    %%

    ! 'NOT'
    %%
        %<optional_not>.not = TRUE;
    %%
    ;

<optional_id>
    =
    %%
        %<optional_id>.nam = NULL;
    %%

    ! ID
    %%
        %<optional_id>.nam = %ID.nam;
    %%
    ;

<ids>			-- RETURNS: nams
    = ID
    %%
        %<ids>.nams = concat(NULL, %ID.nam, NAMNOD);
    %%

    ! <ids> ID
    %%
        %<ids>.nams = concat(%<ids>.nams, %ID.nam, NAMNOD);
    %%
    ;

<id_list>			-- RETURNS: str, nams
    = ID
    %%
        %<id_list>.str = newstr(%ID.nam->str);
        %<id_list>.nams = concat(NULL, %ID.nam, NAMNOD);
    %%

    ! <id_list> ',' ID
    %%
        %<id_list>.str = %<id_list>.str;
        %<id_list>.nams = concat(%<id_list>.nams, %ID.nam, NAMNOD);
    %%
    ;

<optional_integer>	-- RETURNS: str
    =
    %%
        %<optional_integer>.str = NULL;
    %%

    ! Integer
    %%
        %<optional_integer>.str = newstr(%Integer.chars);
    %%
    ;

<optional_minus>			-- RETURNS: minus
    =
    %%
	%<optional_minus>.minus = FALSE;
    %%

    ! '-'
    %%
	%<optional_minus>.minus = TRUE;
    %%
    ;

<attribute_reference>		-- RETURNS: wht, nam
    = ID 'OF' <what>
    %%
	%<attribute_reference>.srcp = %'OF'.srcp;
	%<attribute_reference>.nam = %ID.nam;
	%<attribute_reference>.wht = %<what>.wht;
    %%
    ;

<integer_or_id>			-- RETURNS: nam, val
    = Integer
    %%
	%<integer_or_id>.val = val(%Integer.chars);
	%<integer_or_id>.nam = NULL;
    %%

    ! ID
    %%
	%<integer_or_id>.val = 0;
	%<integer_or_id>.nam = %ID.nam;
    %%
    ;

ID
    = IDENT
    %%
        %ID.nam = newnam(`&%IDENT.srcp, %IDENT.chars);
    %%

    ! 'DEFAULT'
    %%
        %ID.nam = newnam(`&%'DEFAULT'.srcp, "default");
    %%

    ! 'ARTICLE'
    %%
        %ID.nam = newnam(`&%'ARTICLE'.srcp, "article");
    %%

    ! 'MESSAGE'
    %%
        %ID.nam = newnam(`&%'MESSAGE'.srcp, "message");
    %%

    ! 'QUIT'
    %%
        %ID.nam = newnam(`&%'QUIT'.srcp, "quit");
    %%

    ! 'SAVE'
    %%
        %ID.nam = newnam(`&%'SAVE'.srcp, "save");
    %%

    ! 'RESTORE'
    %%
        %ID.nam = newnam(`&%'RESTORE'.srcp, "restore");
    %%

    ! 'RESTART'
    %%
        %ID.nam = newnam(`&%'RESTART'.srcp, "restart");
    %%

    ! 'WAIT'
    %%
        %ID.nam = newnam(`&%'WAIT'.srcp, "wait");
    %%

    ! 'BETWEEN'
    %%
        %ID.nam = newnam(`&%'BETWEEN'.srcp, "between");
    %%

    ! 'CONTAINS'
    %%
        %ID.nam = newnam(`&%'CONTAINS'.srcp, "contains");
    %%

    ! 'ON'
    %%
        %ID.nam = newnam(`&%'ON'.srcp, "on");
    %%

    ! 'IN'
    %%
        %ID.nam = newnam(`&%'IN'.srcp, "in");
    %%
    ;

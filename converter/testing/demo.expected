########## demo ##########
Options
  width 65.
  


Add To Every object
  Is
    takeable.
    Not readable.
    Not openable.
    weight  1.
End Add To.

Add To Every location
  Is
    Not lit.
End Add To.

The inventory
  Container
    Limits
      weight  10
        Else
          "It is too heavy."
End The inventory.

Syntax
  'look' = 'look'.

Verb 'look'
  Does
    If Current Location Is lit Then
      Look.
    Else 
      "It is much too dark in here to see anything."
    End If.
End Verb.

Syntax
  q = q.

Verb q
  Does
    Quit.
End Verb.

Verb take, get, pick
  Check object Here
    Else 
      "I can't see any $o here!"
  And object Not In inventory
    Else 
      "You already have the $o."
  And object Is takeable
    Else 
      "You can't take the $o."
  Does
    Locate object In inventory.
    "Taken."
End Verb.

Verb drop
  Check object In inventory
    Else 
      "You don't have the $o!"
  Does
    Locate object Here.
    "Dropped."
End Verb.

Syntax
  i = i.

Verb i
  Does
    List inventory.
End Verb.

Verb read
  Check object Here Or object In inventory
    Else 
      "I can't see any $o here!"
  And object Is readable
    Else 
      "There is nothing written on the $o."
End Verb.

Verb open
  Check object Here Or object In inventory
    Else 
      "I can't see any $o here!"
  And object Is openable
    Else 
      "There is no way to open the $o."
End Verb.


The round_room Isa location
  Name round room
  
  Description
    "This is a round room with no descernible corners, 'G} och st{ll dig i
     h|rnet Nisse!'"
  
  Exit w, e, s, n To square_room.
  
End The round_room.

The book1 Isa object
  Name dusty old book
  At round_room 
  Is 
    readable.
    openable.
    Not open.
  Description
    "On the floor there is a dusty book."
    If book1 Is Not open Then
      "It is closed."
    Else 
      "It is wide open but the text is barely readable."
    End If.
  
  Verb read, examine
    Does
      If book1 Is open Then
        "The text is written in unreadable characters."
      Else 
        "The book is closed."
      End If.
  End Verb.
  
End The book1.

The square_room Isa location
  Name square room
  
  Is 
    dark.
    filled.
  Description
    If square_room Is dark Then
      "It is very dark in here."
    End If.
  
  Exit n To round_room
    Check door1 Is open
      Else 
        "You can't just walk straight through the closed door!"
  End Exit.
  
  Verb light
    Does
      Make square_room Not dark.
  End Verb.
  
End The square_room.

The door1 Isa object
  Name dusty iron door
  At square_room 
  Is 
    Not takeable.
    Not open.
    openable.
  Description
    "In the north wall of the room there is a door."
    If door1 Is open Then
      "It is open revealing a passage."
    End If.
  
End The door1.

The lamp Isa object
  At square_room 
  Is 
    Not lit.
  Article
    "an"
  
  Mentioned
    "old"
    If lamp Is lit Then
      "lit"
    End If.
    "lamp"
  
  Verb light
    Does
      Make lamp lit.
      "The lamp is now lit."
  End Verb.
  
End The lamp.

Start At round_room.
  "$pWelcome to a small ADVENTURE written in ALAN.$p"
  

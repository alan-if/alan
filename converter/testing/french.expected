########## french ##########

Add To Every object
  Is
    takable.
    real_object.
    Not consumable.
End Add To.


The world Isa object
  
End The world.

The self Isa object
  
  Is 
    Not seated.
  Is 
    Not ordered.
  Description
    If self Is seated Then
      "You are uncomfortably seated."
    End If.
    If coat In inventory Then
      Describe coat.
    End If.
  
End The self.

The coat Isa object
  Name brown fur coat
  In inventory 
  Description
    If coat In inventory Then
      "You are wearing a warm brown fur coat."
    Else 
      "A brown coat is in a heap on the floor."
    End If.
  
End The coat.

The tag Isa object
  In wardrobe 
  Description
    "This is a tag that you'll need to claim your checked belongings."
  
  Verb 'show'
    Check tag In inventory
      Else 
        "You can't find your wardrobe tag."
    Does
      "You examine your pockets one after one and finally find your
       wardrobe tag. You pick it up and wave it in front of you."
  End Verb.
  
End The tag.

The wardrobe Isa object
  At coat_check 
  With Container
  Is 
    Not takable.
End The wardrobe.

The thing Isa object
  At restaurant_entrance 
End The thing.

The escargot Isa object
  At window_table 
  Is 
    Not takable.
  Is 
    Not real_object.
  Is 
    consumable.
  Description
    ""
  
  Verb order, take
    Does
      "In your best kitchen french you ask for some $o."
  End Verb.
  
End The escargot.

The poisson Isa object
  At window_table 
  Is 
    Not takable.
  Is 
    Not real_object.
  Is 
    consumable.
  Description
    ""
  
  Verb take, order
    Does
      "In your best kitchen french you ask for some $o."
  End Verb.
  
End The poisson.

The ourse Isa object
  At window_table 
  Is 
    Not takable.
  Is 
    Not real_object.
  Is 
    consumable.
  Description
    ""
  
  Verb order
    Does
      "In your best kitchen french you ask for some $o."
  End Verb.
  
End The ourse.

The menu Isa object
  
  Description
    "A menu with the name 'Le Grande Ourse' is here."
  
  Verb examine, read
    Check menu In inventory
      Else 
        "You can't read the menu at that distance!"
    Does
      "You scan through the menu and realizes that french is a tough
       language. However, three things stays in your mind: 'Escargot',
       'Poisson' and 'Ourse'"
  End Verb.
  
End The menu.
The inventory
  Container
    Header
      "You are carrying "
    Else
      "You are not carrying anything."
End The inventory.

Syntax
  sit = 'sit'.

Syntax
  l = 'look'.

Synonyms
  l = 'look'.
  

Verb l
  Does
    Look.
End Verb.

Syntax
  q = 'quit'.

Verb q
  Does
    Quit.
End Verb.

Syntax
  'score' = 'score'.

Synonyms
  sco, sc = 'score'.
  

Verb 'score'
  Does
    Score .
End Verb.

Syntax
  stand = 'stand'.

Verb stand
  Check self Is seated
    Else 
      "You are not sitting down."
  Does
    "With a tremendous gathering of strength you raise to an upright
     position."
    Make self Not seated.
End Verb.

Syntax
  i = 'i'.

Synonyms
  invent = i.
  

Verb i
  Does
    List inventory.
End Verb.

Verb order
End Verb.

Verb examine
  Check object Is real_object
    Else 
      "That's not an object."
  And object Here
    Else 
      "I cant see any $o here."
  Does
    Describe object.
End Verb.

Synonyms
  pick = get.
  

Verb get, take
  Check object Here
    Else 
      "I cant see any $o here."
  And object Is takable
    Else 
      "You can't take that!"
  And object Not In inventory
    Else 
      "You are already carrying that."
  Does
    Locate object In inventory.
    "OK. Taken."
End Verb.

Synonyms
  put = drop.
  

Verb drop, leave
  Check object Here
    Else 
      "I cant see any $o here."
  And object In inventory
    Else 
      "You are not carrying that."
  Does
    Locate object Here.
    "OK. Dropped."
End Verb.


The hero Isa actor
  
  Is 
    Not seated.
  Is 
    Not ordered.
  Description
    If hero Is seated Then
      "You are uncomfortably seated."
    End If.
    If coat In inventory Then
      Describe coat.
    End If.
  
End The hero.


The waiter1 Isa actor
  Name waiter
  
  Is 
    sulking.
  Is 
    Not angry.
  Is 
    Not raving.
  Description
    "There is a waiter here."
    If waiter1 Is raving Then
      "He is raving mad."
    Elsif waiter1 Is angry Then 
      "He seems rather angry."
    Elsif waiter1 Is sulking Then 
      "He looks like somebody who's trying hard NOT to see you, but
       unfortunatly failed."
    End If.
  
  Script s1.
    Step
      Locate waiter1 At private_hell.
  Script s2.
    Step After 1
      Locate waiter1 At hero.
      "'Bonjour, monsieur' a voice says somewhere behind you."
    Step
      "The waiter waves at you and goes east into the restaurant."
      Locate waiter1 At first_room.
    Step Wait Until hero Here
      "The waiter waves impatiently at you and vanish behind the tables at
       the east end of the room."
      Locate waiter1 At second_room.
      Use Script s3.
  Script s3.
    Step Wait Until hero Here
      "The waiter continues towards a small table at the eastmost part of
       the restaurant."
      Locate waiter1 At window_table.
    Step Wait Until self Is ordered
      "The waiter makes a note and vanish through the kitchen door."
      Locate waiter1 At kitchen.
  Script s4.
    Step After 1
      If self Is seated Then
        Make waiter1 angry.
      End If.
      Locate waiter1 At hero.
    Step
      If self Is seated Then
        Make waiter1 raving.
      End If.
      Use Script s3.
End The waiter1.


The outside_restaurant Isa location
  Name 'Outside Le Grande Ourse'
  
  Description
    "You are standing on the sidewalk outside a little french restaurant.
     Behind you (to the south) is a street with cars driving wildly,
     seemingly at random."
  
  Exit s, south, street To busy_street.
  
  Exit n, north, 'in' To restaurant_entrance
    Does
      "In spite of the frighting french name of the restaurant you decide
       to try it. You gather all your mental strength and walk into the
       restaurant."
      Use Script s2 For waiter1.
      Score 10.
  End Exit.
  
  Exit e, east To busy_street
    Does
      "You try to walk down the sidewalk, but in the rush somebody pushes
       you straight into to street."
  End Exit.
  
  Exit w, west To busy_street
    Does
      "You try to walk down the sidewalk, but in the rush somebody pushes
       you straight into to street."
  End Exit.
  
End The outside_restaurant.

The restaurant_entrance Isa location
  Name 'Inside Le Grande Ourse'
  
  Description
    "You are standing just inside the main entrance of Le Grande Ourse,
     wondering what do do now."
  
  Exit s, south, out To outside_restaurant
    Does
      Use Script s1 For waiter1.
  End Exit.
  
  Exit e, east To first_room.
  
  Exit w, west To coat_check.
  
End The restaurant_entrance.

The busy_street Isa location
  Name middle 'of' 'the' road
  
  Description
    "This is the middle of a busy street. It seems like a pretty dangerous
     place to me!"
  
  Entered
    "Bad luck! You just got squashed by a cement truck."
    Locate hero At private_hell.
    Score 15.
End The busy_street.

The private_hell Isa location
  Name flaming hell
  
  Description
    "Unfortunately you seem to have died and wound up in Hell."
  
End The private_hell.

The window_table Isa location
  Name 'Dirty Little Table By The Window'
  
  Description
    "In a small corridor near the kitchen entrance a dirty little table is
     standing all alone by the window."
  
  Exit w, west To second_room
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Exit n, north To kitchen
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Verb sit
    Does
      "You choose the chair that seems least probably to break down for the
       next hour and sit down."
      Make self seated.
      Score 10.
      "On the table you find a menu."
      Locate menu At window_table.
  End Verb.
  
  Verb order
    Check object Is consumable
      Else 
        "You can't order $o"
    Does
      Make self ordered.
  End Verb.
  
  Verb l
    Does Before
      Describe self.
  End Verb.
  
End The window_table.

The coat_check Isa location
  Name coat 'check' room
  
  Description
    "You are standing in front of the coat check-in."
  
  Exit e, east To restaurant_entrance.
  
  Verb 'check', leave
    Check object Here
      Else 
        "I cant see any $o here."
    And object In inventory
      Else 
        "You are not carrying that."
    Does Only
      "You leave your $o to the tired man behind the counter."
      Locate object In wardrobe.
      Score 10.
      If tag In wardrobe Then
        Locate tag In inventory.
        "You get a tag to claim your belongings."
      End If.
  End Verb.
  
  Verb claim, get
    Check tag In inventory
      Else 
        "The man behind the counter refuses to give you anything unless you
         are able to show him your wardrobe tag."
    And object In wardrobe
      Else 
        "There is no $o checked here."
    Does Only
      "The tired man give you your $o"
      Locate object In inventory.
  End Verb.
  
  Verb get
    Check object Not In wardrobe
      Else 
        "The $o is checked in the wardobe. You can't just take it."
  End Verb.
  
End The coat_check.

The first_room Isa location
  Name big room near 'the' entrance
  
  Description
    "This is a big room with many tables. Most of them seems to be vacant."
  
  Exit e, east To second_room
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Exit w, west To restaurant_entrance
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Verb sit
    Does
      "You choose a nice table that seems to be vacant and sit down looking
       for a waiter."
      Make self seated.
  End Verb.
  
End The first_room.

The second_room Isa location
  Name nice inner room
  
  Description
    "The inner room is smaller than the first room, but it is quite nice.
     In one corner a fire place has a fire burning."
  
  Exit e, east To window_table
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Exit w, west To first_room
    Check self Is Not seated
      Else 
        "Better stand up first."
  End Exit.
  
  Verb sit
    Does
      "You choose the chair that seems least probably to break down for the
       next hour and sit down."
      Make self seated.
  End Verb.
  
End The second_room.

The kitchen Isa location
  
  Description
    "The kitchen looks like you could expect. People are working here and
     there and waiters keep running through the door to the south to the
     restaurant. To the west is the wash room."
  
  Exit s, south To window_table.
  
  Exit w, west To wash_room.
  
End The kitchen.

The wash_room Isa location
  Name room 'with' dirty plates
  
  Description
    "This is the place where all the dirty plates wind up. Piles and piles
     of plates and glasses can be seen."
  
  Exit e, east To kitchen.
  
End The wash_room.
When waiter1 Is raving And self Is Not seated Then
  Make waiter1 Not raving.
  Use Script s3 For waiter1.


Start At outside_restaurant.
  "Well through the customs at Orly, you finally succeed in getting hold of
   a cab. Since you're not expected at the subsidary until four a'clock, you
   ask the driver to get you to a place where you can do something about
   your growing hunger. Somehow you manage to explain what you want and
   after a neck-breaking ride he lets you off outside a restaurant."
  

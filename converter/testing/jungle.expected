########## jungle ##########


The path Isa location
  
  Description
    "You are standing on a barely visible path in the middle of nowhere.
     The path looks like it's been walked by bare feet (or rather paws) for
     many a year. From the small amount of light reaching the ground here I
     should say the path runs in almost straight north/south direction. On
     both sides of the path is the deepest, darkest jungle you've ever seen.
     I really wouldn't recommend going that way. The path itself isn't much
     of a place to hold on to either. You get the impression that the
     vegetation is trying hard to recapture even this tiny part of land. The
     trees on both sides seems to come closer and there are vines hanging
     down almost touching your head."
  
  Exit north To bank.
  
  Exit south To clearing.
  
  Exit east, west To jungle.
  
End The path.

The bank Isa location
  
  Description
    "The path ends here on the south side of a wide river. On the ground
     you can see lots of paw prints (some pretty big ones, too). The obvious
     guess is naturally that this is a common place for the wild animals to
     stop by for a drink or two (and maybe a bite too). The river itself
     doesn't seem to be too dangerous - it's neither too wide nor too rapid
     - but those logs with a pair of eyes give you second thoughts."
  
  Exit north, swim To river.
  
  Exit south To path.
  
  Exit east, west To jungle.
  
End The bank.

The trees Isa location
  
  Description
    "You have now ended up high above the ground in the middle of the trees
     and vines. The vegetation is so thick up here that it seems almost like
     a green floor."
  
  Exit down To path.
  
End The trees.

The river Isa location
  
  Description
    "Defying the obvious horrors of the river you try for the northern
     river bank. One crocodile immediately chops your left foot of, but you
     makes it almost to the middle of the river before another merciful
     crocodile finish you off."
    Quit.
  
End The river.

The clearing Isa location
  
  Description
    "Here the jungle opens up a bit and the path takes you straight into a
     clearing. The path seems to continue on the south side of the clearing
     some fifty paces away."
  
  Exit north To path.
  
  Exit east, west To jungle.
  
  Exit south To camp
    Check hero Is repelling
      Else 
        "The tiger opens its big mouth and lets out a terrifying growl.
         Apparently it won't let you pass."
    Does
      "When you approaches the tiger it looks confused. Then it really
       takes in your smell. It suddenly bolts, turns and takes off into the
       jungle."
      Locate tiger At nowhere.
  End Exit.
  
End The clearing.

The jungle Isa location
  
  Description
    "Now you've really done it. Didn't I tell you NOT to enter the jungle."
  
  Exit north, south, east, west To jungle
    Does
      "Stumbling around in the jungle trying to make your way through the
       damp vegetation that almost seems to reach out for you, you suddenly
       stumble onto a snake, which disapprove very clearly of you stepping
       on it. One bite in the leg and you have had it."
      Quit.
  End Exit.
  
End The jungle.

The camp Isa location
  
  Description
    "Here is the scattered parts of what ones was the camp of your
     expedition. The sight of it makes your memory come back. When you were
     attacked last night of a herd of wild elephants everyone fled in panic.
     You yourself ran straight into a tree and must have lost both
     conciousness and memory. 'Well, hope the computer still works.' you
     think. 'I think I stick to computer adventures, at least for the
     immediate future.'"
    Quit.
  
End The camp.

The nowhere Isa location
  
End The nowhere.

The tiger Isa object
  At clearing 
  Description
    "An enormous tiger is standing here blocking your way."
  
End The tiger.

The notebook Isa object
  In inventory 
  Description
    "The book is called 'The Jungle Book: Tricks and Tips'. It also has
     your name on it."
  
  Verb take
    Does
      Locate object In inventory.
      "Taken!"
  End Verb.
  
  Verb drop
    Does
      Locate object Here.
      "Dropped!"
  End Verb.
  
  Verb read
    Does
      "You open the book and glance over the notes. It is really a very
       strange mixture. Something about a tree you shouldn't hide under when
       it rains, 'cause some kind of bugs will start falling of its leaves,
       something else about a certain kind of fungus, which grows up among
       the vines and when eaten is a strong tiger repellant and something
       about how to make a fire from wet moss. Here are page after page of
       useful hints of how to survive in the jungle, all in your own hand
       writing."
  End Verb.
  
End The notebook.

The vine Isa object
  At path 
  Description
    "A particulary long and thick vine is hanging down just beside you."
  
  Verb climb
    Does
      "The vine is quite slippery, but you still manage to climb well into
       the trees."
      Locate hero At trees.
  End Verb.
  
End The vine.

The fungus Isa object
  At trees 
  Description
    "Some kind of vaguely familiar fungus is growing here on a vine."
  
  Verb take
    Does
      Locate object In inventory.
      "Taken!"
  End Verb.
  
  Verb drop
    Does
      Locate object Here.
      "The fungus immediately clings to a new vine."
  End Verb.
  
  Verb eat
    Does
      "You try a bit of the fungus. It doesn't taste bad although it isn't
       that delicious either. You swallow the rest of it almost without
       chewing. After a short while a strange odour starts perspiring from
       your body."
      Locate fungus At nowhere.
      Make hero repelling.
  End Verb.
  
End The fungus.
Syntax
  take_inventory = 'inventory'.

Synonyms
  i = 'inventory'.
  

Verb take_inventory
  Does
    List inventory.
End Verb.

Syntax
  'look' = 'look'.

Synonyms
  l = 'look'.
  

Verb 'look'
  Does
    Look.
End Verb.

Syntax
  'quit' = 'quit'.

Synonyms
  q = 'quit'.
  

Verb 'quit'
  Does
    Quit.
End Verb.


The hero Isa actor
  
  Is 
    Not repelling.
End The hero.


Start At path.
  "$p'Oh, my head. It hurts. Why am I out here when I've got this kind of
   headache? And where is 'here'? And who am I?'"
  

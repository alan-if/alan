########## weather ##########


The outside Isa location
  
  Description
    "You are outside in the Sibirian winter night."
  
End The outside.
Syntax
  w = 'wait'.

Synonyms
  'w' = 'wait'.
  

Verb w
  Does
    "Time goes by."
End Verb.

Syntax
  l = 'look'.

Synonyms
  'l' = 'look'.
  

Verb l
  Does
    Look.
End Verb.


The weather Isa object
  
  Is 
    Not snowing.
    Not snow_coming.
    Not wind_coming.
    Not cold_coming.
  Description
    If weather Is snowing Then
      "Big snow flakes are falling."
    End If.
  
End The weather.
Event weather_scheduler
  Describe weather.
  If weather Is Not snow_coming Then
    Schedule start_snowing After 8.
    Make weather snow_coming.
  End If.
  If weather Is Not wind_coming Then
    Schedule wind_coming After 5.
    Make weather wind_coming.
  End If.
  If weather Is Not cold_coming Then
    Schedule cold_coming After 4.
    Make weather cold_coming.
  End If.
  Schedule weather_scheduler After 1.
End Event weather_scheduler.

Event start_snowing
  Make weather snowing.
  "You feel something gently touching your chin. It's a snow flake."
  Schedule stop_snowing After 5.
End Event start_snowing.

Event stop_snowing
  Make weather Not snowing.
  "The snowing appears to be lighter."
  Make weather Not snow_coming.
End Event stop_snowing.

Event wind_coming
  "You can feel a cold wind sweeping down from the Sibirian wastelands."
  Make weather Not wind_coming.
End Event wind_coming.

Event cold_coming
  "The temperature is NOT that of sunny Southern California. It's rather
   quite chilly."
  Make weather Not cold_coming.
End Event cold_coming.


Start At outside.
  Schedule weather_scheduler After 0.
  

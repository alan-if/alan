-- bibliothek.ala
----------------------------------------------------
location Bibliothek
----------------------------------------------------
description 
  "Bibliothek:  NO: Flur, N: Wohnzimmer.$p
   Die Bibliothek ist eigentlich zum Lesen nicht geeignet. Es gibt hier zwar 
   Kataloge und dicke Leitz-Ordner auf einem langen
   Mahagonischreibtisch und viele Ledereinbände in 
   einem hohen Bücherregal aus Eiche, es fehlt aber ein venünftiger
   Stuhl. Lediglich ein Hocker mit Kunstlederbezug steht in der
   Ecke.  Er sieht aus, als gehöre er nicht hierher."
  exit NO to Flur1.
  exit N to Wohnzimmer.
end location.

object Kataloge at Bibliothek
is takeable.
description ""
verb untersuche,lies does
  "Kataloge verschiedener Versandhäuser. Außerdem Drucksachen von
   Buchhandlungen, ein Telefonbuch und einge ältere Zeitungen."
   end verb.
end object.

object Katalog at Bibliothek
description ""
verb untersuche,lies does
  "Es steht nichts drin, was Dich hier weiterbringt."
   end verb.
end object.
synonyms Drucksache, Drucksachen, Telefonbuch, Zeitung, Zeitungen= Katalog.

object Ordner at Bibliothek
is takeable.
description ""
verb untersuche,lies does
  "In diesen Ordnern sind alle Rechnungen enthalten, welche das Haus 
   betreffen, vom Schornsteinfeger bis zur Müllabfuhr. Die Unterlagen
   scheinen ziemlich komplett zu sein, denn der älteste Ordner trägt
   die Aufschrift '1951-1953'."
   end verb.
end object.
synonyms Leitz, Leitzordner=Ordner.

object Rechnung at Bibliothek
is takeable.
description ""
verb untersuche,lies does
  "Alle zu lesen ist schier unmöglich. Bestimmte Rechnungen
   herauszusuchen sollte zu schaffen sein, z.B. LIES die RECHNUNGEN VOM 
   SCHORNSTEINFEGER."
   end verb.
verb lies_mit_zweitem_objekt does
  if  (ind) = Maler then  
    "In all den Jahren findet sich nur eine einzige Malerrechnung. 
     Sie stammt aus dem Jahre 1961 und betrifft das Bad:$n
     $tAlten Anstrich entfernt, Putzschäden behoben.$n
     $tRohre mit weißer Ölfarbe gestrichen.$n
     $tDen gesamten Raum 2x mit Bleiweißtünche angestrichen.$n
     $n
     Material$t$t 358,--$n
     43 Stunden$t$t1376,--$n
     $t$t$t$t_________$n
     $t$t$t$t1734,--$n"
     score 1.
     score.
  else "Du findest nicht von Interesse."
  end if.
end verb.
end object.

synonyms Rechnungen=Rechnung.
syntax lies_mit_zweitem_objekt = 'lies' (obj) 'vom' (ind) where  ind isa object
else "Sorry?"

object Maler at bibliothek
description ""
end object.

object Schornsteinfeger at bibliothek
description ""
end object.
synonyms Müllabfuhr, Gärtner, Klempner, Installateur
=Schornsteinfeger.

object Bücher at Bibliothek
description ""
verb untersuche does
  "Die Bücher sind verstaubt und wurden wohl Jahre nicht gelesen. Auffällig ist
    der Band 'Ba-Do' des 24-bändigen Brockhaus. Das Buch steht etwas vor und
    ist auch ohne Staub."
end verb.
end object.

object Buch at Bibliothek
description ""
verb lies, untersuche does
  "Dafür, alles zu lesen, ist jetzt aber keine Zeit.
   Wichtige Dinge im Lexikon
   nachzusehen wäre aber machbar: LIES IM LEXIKON ÜBER was-auch-immer."
end verb.
end object.

synonyms Ledereinbände,Einbände, Brockhaus =Buch.

object lexikon at Bibliothek
description ""
verb lies_mit_ind does
  if  (ind) = Bleiweiss then
      "*Bleiweiß* ist basisches Bleikarbonat. Es wird hergestellt, indem
       Bleiacetat in der Wärme mit Alkalikarbonat versetzt wird. In der
       Natur kommt es als Hydrocerussit vor.$n
       Bleiweiß wird als Malerfarbe verwendet. Von allen weißen Malerfarben
       hat es den schönsten Glanz, zudem eine große Deckkraft und  gutes
       Haftvermögen. Deshalb konnte es sich trotz seiner Giftigkeit
       (->Bleivergiftung) und Empfindlichkeit gegen Schwefelwasserstoff
       (braune Verfärbung durch Bildung von Bleisulfid) als Malerfarbe
       behaupten.$n"
     score 1.
     score.
  else if (ind) = Bleivergiftung then
     "*Bleivergiftung* Die Giftigkeit von Bleiverbindungen war schon den
      Römern bekannt. Blei wird vom Margen-Darm-Trakt schlecht (10-20%%)
      von der Lunge besser (>80%%) aufgenommen. Durch Beeinflussung des
      Enzymsystems wird die Bildung von Erythtrozyten gestört. Die
      Vergiftung führt weiter zu Degeneration der motorischen Nerven.$n
      Symptome sind Letharie, Übelkeit, Schwindelgefühle und Kopfschmerzen
      sowie im fortgeschrittenen Stadium Bewegungsstörungen$n
      Blei lagert sich in den Kochen und auch in den Haaren ab und kann dort
      noch nach jahrhunderten mit chemischen Methoden nachgewiesen werden.
      So gilt nach Untersuchung von historischen Haarproben als gesichert,
      das Napoleon letztlich an einer Bleivergiftung starb."
    score 1.
    score.
  else "Das ist eher von geringem Interesse."
  end if.
  end if.
end verb.
end object.
syntax lies_mit_ind='lies' 'im' (obj) über (ind) where ind isa object
else "Dieser Eintrag bringt Dich nicht weiter hier."

object Bleiweiss at bibliothek
description ""
end object.

object Bleivergiftung at bibliothek
description ""
end object.


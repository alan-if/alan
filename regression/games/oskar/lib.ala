OBJECT ATTRIBUTES
  NOT entnehmbar.
  NOT ablegbar.
  NOT zu_öffnen.
  NOT zu_schliessen.
  geschlossen.
  NOT verschliessbar.
  verschlossen.
  not steckbar.
  not metallisch.
SYNONYMS
   -- für Kommandos: Nord/Süd/Ost/West/hoch/runter/rein/raus
   nord,norden= n.
   sud,süden =s.
   west,westen=w.
   ost,osten=o.
   hinauf,rauf = hoch.
   hinunter,hinab = runter.
   hinein,eintreten = rein.
   hinaus,rausgehen = raus.
   -- alle mal benutzen, damit's keine Warnungen gibt
   LOCATION wasweissich EXIT
     n,s,o,w,hoch,runter,rein,raus TO wasweissich.
   end location.


SYNONYMS
   -- für eingebaute Worte
   der,die,das, den, dem  = the.
   alle, alles = all.
   ausser = but.


-- Verben, 1 Wort

VERB   'ende'   DOES QUIT. END VERB.
SYNTAX 'ende' = 'ende'.
synonyms bye,'exit','quit'=ende.

VERB   'schau'  DOES LOOK. END VERB.
SYNTAX 'schau' = 'schau'.
synonyms l, umsehen, sieh = schau.

VERB   'save'   DOES SAVE. END VERB.
SYNTAX 'save' = 'save'.

VERB   'load'   DOES RESTORE. END VERB.
SYNTAX 'load' = 'load'.

VERB   inventar DOES LIST inventory. END VERB.
SYNTAX inventar = 'inventar'.
SYNONYMS liste,i = 'inventar'.

VERB punkte DOES SCORE. END VERB.
SYNTAX punkte = 'punkte'.

verb warte does "Zeit vergeht..." end verb.
syntax warte = 'warte'.

-- Verben, 2 und mehr Wörter
-- Es ist nützlich, Objekte ausdrücklich als ablegbar zu kennzeichen.
-- Dann kann man 'Schüttgüter' in Raumbeschreibungen verstecken
-- und braucht sich nict drum zu kümmern, was passiert, wenn
-- sie irgendwoanders rumliegen (keine u.U. auffällige Objektbeschreibung).
VERB weglegen,weglegen1, wegwerfen,hinlegen,hinlegen1
     CHECK obj IN inventory
     ELSE "Du hast so was gar nicht."
     AND object is ablegbar else "Das solltest Du lieber behalten."
     DOES LOCATE obj HERE. "Weggelegt."
END VERB.
SYNTAX
 weglegen = 'lege' (obj) 'weg' .
 weglegen1 = 'leg' (obj) 'weg' .
 wegwerfen = 'wirf' (obj) 'weg' .
 hinlegen = 'leg' (obj) 'hin' .
 hinlegen1 = 'lege' (obj) 'hin' .

VERB nimm
  CHECK obj is entnehmbar ELSE
    "Belaste Dich nicht mit unnützen Dingen!"
  AND obj NOT IN inventory ELSE "Du hast es bereits."
  DOES LOCATE obj IN inventory. "Ich hab es genommen."
END VERB.
SYNTAX nimm = nimm (obj) * WHERE obj ISA OBJECT
ELSE "Du kannst keine Personen nehmen!"


-- Getragene Gegenstände

CONTAINER inventory
   LIMITS COUNT 20 THEN
	 "$nDu kannst nichts mehr tragen, es rutscht Dir aus den Händen."
   HEADER "Du trägst:"
   ELSE "Du stehst mit leeren Händen da."
END CONTAINER.

LOCATION limbo
-- für verschwundene Objekte
   exit pause to limbo.
END LOCATION.

-- Syntaces

-- und zum Schluß: HILFE

syntax lies = lies (obj).
syntax untersuche = untersuche (obj).
synonyms betrachte,u,x = untersuche.

$include 'hilfe.ala'




Location Office
Description
If chair is used then
"You are sitting in your office on a hot summer day. It's early in the"
else
"You are standing in your office on a hot summer day. It's early in the"
end if.
"morning and the heat is already getting to you. Beads of sweat trickle
down your face. A small fan is sitting on a nearby table,"
if fan is on then
"barely creating a breeze. A radio is sitting next to it."
else
"which is currently turned off. A radio is sitting next to it."
end if.

"Your old desk is standing directly before you,
with an assortment of papers scattered across its surface. To your 
left is a file cabinet and to your right is an overfilled waste
paper basket. 
$n 
$n
Hanging from your walls are a few landscapes, mostly painted by
yourself. They are nothing special, but they do give the room a
somewhat brighter look." 
locate door1 at office.
if door1 is open then
"To the north is your secretary's office. To your south is an open window."
else
"To the north is a closed door. To your south is an open window." 

end if.
Exit s To office2
check 
"You rather not jump out of your window at this time. Perhaps later you'll
give it a try."
end exit.

exit n to office2
check Door1 is open
else
"You need to first open your door."
does 
"You walk into your secretary's office."
end exit.
End Location.


Object basket  Name waste paper basket  AT office
container
Description


Verb examine, Look_at does only
"It is a small wicker basket currently overflowing with wadded up
paper."
End Verb.

verb Look_into does only
"You look into the basket hoping to find something of use. A small
business card catches your eye."
locate card in basket.
end verb.

verb clean, 'empty' does only
"You would rather have your secretary dump your garbage. You have
better things to do, like taking a nap."
end verb.
End Object.


Object radio  Name old radio  AT office
is not on.
is not used.
Description


Verb Examine, look_at does only
If radio is on then
"The radio is currently turned on."
else
"The radio is currently turned off."
end if.

End Verb.

verb listen_to, hear does only
if radio is not on then
"You listen to the radio for a few minutes, but quickly lose interest.
This may be due to the fact that it is not turned on."
else
"You listen to a radio newscast, but it is nothing special."
end if.
end verb.

verb turn_on does only
if radio is on then
"It is already on!"
else
if radio is not used then
"You turn the radio on. A brief newscast about the McCarthy hearings 
precedes a selection of classical music. You have never been involved
in politics before, but you really can't stand that McCarthy fellow."
make radio on.
make radio used.
else
make radio on.
"You turn on the radio and listen to some Gershwin."
end if.
end if.
end verb.


verb turn_off does only
if radio is on then
"You switch off the radio."
make radio not on.
else
"It is already off!"
end if.
end verb.

verb 'use' does only
if radio is on then
"You switch off the radio."
make radio not on.
else
"You switch on the radio."
make radio on.
end if.
end verb.

End Object.

Object card  Name business card  AT nowhere

is not read.
is takeable.
Description

verb examine, look_at, read does only
if card in inventory then
if glasses is worn then
"The card reads: $n Franklin Office Supplies$n
 $n ""Your only stop for your general office needs"" $n $n
 
 15 Locust Avenue $n
 Los Angeles, CA 11382
$n (202) 757-3838  $n $n "
if card is not read then
make card read.
"On the back of the card is the name, Thea Harbou, with the message,
""Please call - urgent"" $n $n

Now you remember where the card came from. Two days ago, while in
Sammy's, the local drinking hole, your remember a dame handing you
this card, asking for you to call back. It is a shame you can't remember
too much about that evening, simply that you came back home to sleep
it off.  You think back, but can't get a clear picture of her. Perhaps,
she was hitting on you. You can hope, anyway...
$n $n
Maybe you should give her a call.$n $n"
end if.
else

"You can't quite make out what the card says without your reading glasses.
The print is very small. Either that, or your eyes are getting worse."
end if.

else
"You are not holding the business card."
end if.
end verb.

verb show does only
"You have no need to do that."
end verb.

verb give does only
"You have no need to do that."
end verb.

End Object.




Object chair  Name office chair  AT office
is used.
Description


Verb examine, look_at does only
"It is a nice leather chair. A few small rips are present, but otherwise
it is in good condition. More importantly, it is quite comfortable."

End Verb.

verb sit_down_in, sit_in, sit_on, 'use' does only 
if chair is used then
"You are already sitting down."
else
"You sit down in your chair."
make chair used.
end if.
end verb.

verb stand_up_from, leave does only
if chair is used then
"You stand up."
make chair not used.
else
"You are already standing up."
end if.
end verb.

End Object.




Object gun1  Name snub nose gun  AT office
is not loaded.
is saftey.


description


Verb examine, look_at does only
"This blue steeled beauty has saved your life a number of times."
if gun1 is loaded then
"It is currently loaded."
else
"The revolver is currently empty."
if gun1 is not loaded then
"You remember that you are out of shells, and will try to pick some
up on your way home today."
end if.
end if.
end verb.

verb shoot does only
if gun1 is not loaded then
"Your gun is currently not loaded."
else
"You have no need to shoot your gun at this time."
end if.
end verb.

verb 'use' does only
if gun1 is not loaded then
"Your gun is currently not loaded."
else
"You have no need to shoot your gun at this time."
end if.
end verb.

verb take does only
if gun1 in inventory then
"You already are holding your revolver!"
else
"You pick up your revolver."
locate gun1 in inventory.
end if.
end verb.

verb load does only
if gun1 is loaded then
"It is already loaded!"
else
if bullets in inventory then
"You quickly load your gun."
make gun1 loaded.
else
"You don't have any bullets!"
end if.
end if.
end verb.

end object.



Object desk  Name old messy desk  AT office

Description


Verb examine, look_at does only
"Your desk is quite a mess. A few loose papers are located on top of it.
Your phone is buried somewhere under them."
if gun1 at office then
"Your snub-nose revolver is sitting on top of your desk."
end if.
if drawer is open then
"Your desk drawer is currently open."
else
"Your desk drawer is closed."
end if.
End Verb.

verb open does only
"You can't open a desk. Try opening the drawer instead."
end verb.

End Object.



Object deskkey  Name desk key  AT nowhere

Description

verb examine, look_at does only
"It is the key to your desk drawer."
end verb.

verb drop does only
"You would rather hold onto it for now."
end verb.

verb use_on does only
if obj1<> deskkey then
"You can't do that."
else
if obj2=drawer then 
"You unlock the drawer using the key."
make drawer unlocked.
else
"You have no need to do that."
end if.
end if.
end verb.

verb 'use' does only
"What would you like to use it on?"
end verb.

End Object.


Object glasses  Name pair 'of' reading glasses  in fakedrawer
is takeable.
is not worn.
Description


Verb Examine, Look_at does only
"They are your reading glasses. A sign of middle age, you guess."

End Verb.

verb 'use',wear does only
if glasses in inventory then
if glasses is worn then
"They are already on! Perhaps senility is also setting in."
else
"You put the glasses on."
make glasses worn.
end if.
else
"You do not have your glasses."
end if.
end verb.

verb take_off, 'remove', take_thing_off does only
if glasses in inventory then
if glasses is worn then
"You take off your glasses."
make glasses not worn.
else
"You are not wearing your glasses!"
end if.
else
"You do not have your glasses."
end if.
end verb.
End Object.



Object phone Name telephone  AT office
is not used.
Description


Verb Examine, look_at does only
"It is your regular telephone."

End Verb.

verb 'use', pickup, dial, call_on does only
if card is not read and phone is not used then
"You don't have anyone to call at the moment."
else
"You pick up the phone and give Thea a call. A soft spoken woman
answers. After a few minutes of conversation, you discover
that she wants to hire you to find her husband. You ask her to
stop by the office. $n $n"
"After waiting for about half an hour, Julie shows your potential
client in. She appears to be in her early 30's, slender, tall ,
and with long black hair. She is wearing a tight fitting black dress, 
which shows off her hourglass figure. 
You ask her to sit down, and she gracefully moves into a
nearby seat. $n $n
She hands you a black and white photo. ""That is my husband, the
tall one with the mustache."", she says with an German accent.
""Me and my Joseph came over to America as soon as the war ended.
Four years ago he disappeared, no trace, nothing. I looked endlessly
for him, to no avail. I just happened to run across this photo 
in a film magazine. It seems my Joseph has gotten himself some
sort of job in Hollywood, in the movies."" $n $n
You nod knowingly. You have had plenty of cases of husbands 
leaving wives in the past. Some just get tired of their wives, some
just want a new life. You explain that if you do manage to find him,
he likely will not want to come back.$n $n
""Perhaps"", Thea says. ""One thing though, before he left he didn't know
I was pregnant. We now have a five year old son.""
""That does change things"", you tell her. She hands you a few other
doucuments relating to her husband, and a small bracelet. ""This is
all I can pay you for now, I don't have much money. After you find
my husband, I'll get more for you.""
 $n $n
Normally you don't barter your services. However, being that both of
you seem desparate, you agree. $n 
""I'll get back to you as soon as I find out anything."" You see
Thea out of the office."
make phone used.
locate documents in inventory.
locate photo in inventory.
locate bracelet in inventory.
end if.
end verb.
End Object.


Object documents Name 'set' 'of' documents  AT nowhere
is not read.
is not translated.
Description


Verb Examine, look_at, read does only
if documents is not read then
if documents is not translated then
"The documents date back before the war, all the way back
to the early thirties. The print is in German, so you
don't know what to make of it. "
else

"The old man has translated a few paragraphs for you.
There are some old pictures of Joesph Harbou, one
next to some German Dame, a filmmaker of some sort, Leni Riefenstahl.
From reading over the papers, it looks as if Harbou was involved
with some Nazi propaganda films in the early thirties. Once the
war started, he and his wife left the country and came to the United
States. That's about all you can figure out from them."
make documents read.
end if.
else
"There is nothing else in them of any use to you."
end if.
End Verb.

verb show, give does only
"Please state who you sish to show them to."
end verb.

verb show_to, give_to does only
if act=broker then
if documents is not translated then
"You hand the documents to the old man. ""Well let's see here."", he
mumbles out. ""I know a bit of German, not a whole lot though."".
He takes out a pen and starts scribbling away. After a few minutes he hands
the papers back to you. ""That's all I can translate, friend. Hope
it is of some help." 
make documents translated.
else
"There is no need to do that."
end if.
end if.
end verb.
End Object.


Object bracelet  Name bracelet  AT nowhere

Description


Verb Examine, look_at does only
"It is quite nice. Not sure of it's value, but it looks like
it is worth something."

End Verb.

verb give, sell does only
"Who do you wish to give it to?"
end verb.

verb give_to, sell_to does only
if act=broker then
"You hand the bracelet to the broker, hoping to get decent money for
it. ""I'll give you ten dollars"", the man says. ""I'm being generous
too."". You accept the offer and take the money."
locate bracelet at nowhere.
locate cash in inventory.
else
"You don't want to do that."
end if.
end verb.

verb wear does only
"It's not quite your style."
end verb.

End Object.


Object coins Name few coins  in inventory
Description


Verb Examine, look_at does only
"They are simply a few loose coins, nothing special."

End Verb.

verb give does only
if hero at sidewalk then
"Don't just give away your coins, buy something."
else
"No one wants your meager riches."
end if.
end verb.

End Object.

Object photo  Name photo  AT nowhere
is not identified.
is not named.
is not perfect.

Description


Verb Examine, look_at does only
"It's a pretty good picture of Joseph Harbou, a tall man with
a thin mustache. He appears to be in his early forties, at least when
the picture was taken."
If photo is not identified then
"You can't quite remember which film this photo is from. It looks
familiar, though."
end if.
End Verb.

verb show_to, give_to does only

If photo is identified or photo is named then
"There is no need to show the photo at this moment."
else
If hero at office2 then
"You ask Julie to take a look at the photo. She's always reading those
movie magazines, maybe she knows the name of the film. $n $n
""Let's see, it's one of those crime films. Not 'Crime city', hmm,
I know, it's from 'The Postman always rings twice', with John Garfield.
That's him in the foreground. Don't tell me you have Garfield as a
client, bigshot.""
""No, no, just the fellow in the back, the one with the mustache."",
you tell her. $n ""No idea who he is, just some extra it looks like"",
Julie replies. $n
You thank her for the help. Hmm, not a bad film if you
remember. Can't quite remember the studio though."
make photo named.
else
"No one wants to look at you photo."
end if.
end if.
end verb.
End Object.

Object drink1  Name half a quart 'of' whisky in fakedrawer
has drunk 0.
is takeable.
description
verb examine, look_at does only
if drunk of drink1 > 2 then
"A quarter full bottle of whisky."
else
"A half full bottle of whisky."
end if.
end verb.

verb drink does only
if drunk of drink1 > 3 then
"You have had enough to drink."
else

if drunk of drink1 > 2 then
"You have another drink. You find yourself singing an old Bing
Crosby song, and although unintelligble to most listeners, you
think is sounds quite good. As you finish the song, you decide you've
had more than enough to drink."
set drunk of drink1 to drunk of drink1 +1.
else
if drunk of drink1 >1 then
"You quickly throw down another drink. You are starting to feel a
bit woozy. Perhaps you should cut down on the drinking."
set drunk of drink1 to drunk of drink1 +1.
else
"You gulp down a quick blast. It sure hits the spot."
set drunk of drink1 to drunk of drink1 +1.
end if.
end if.
end if.
end verb.


End Object.



Object fakedrawer Name fakedrawer1  AT nowhere
Container
Description


End Object.


object drawer Name top desk drawer AT office
Container
is not unlocked.
is not open.

Description

Verb examine, look_at, look_into does only
if drawer is open then
"Your desk drawer is currently open."
list drawer.
else
"Your desk drawer is currently closed."
end if.

End Verb.

verb open does only
if drawer is open then
"It is already open."
else
if drawer is not unlocked then
"You need to unlock it first."
else
"You open the desk drawer."
empty fakedrawer in drawer.
make drawer open.

end if.
end if.
end verb.

verb close does only
if drawer is open then
"You close the desk drawer."
empty drawer in fakedrawer.
make drawer not open.
else
"The drawer is already closed."
end if.
end verb.

verb lock does only
if deskkey in inventory then
if drawer is not unlocked then
"It is already locked."
else
if drawer is not open then
"You lock the desk drawer."
make drawer not unlocked.
else
"You need to close the drawer first."
end if.
end if.
else
"You don't have the desk key."
end if.
end verb.

verb unlock does only
if drawer is not unlocked then
if deskkey in inventory then
"You unlock the desk drawer."
make drawer unlocked.
else
"You don't have the key to the drawer."
end if.
else 
"It is already unlocked."
end if.
end verb.
End Object.


Object cabinet1 Name file cabinet AT office

Description


Verb examine, look_at does only
"It contains tons of useless facts about previous cases."


End Verb.


Verb open does only
"There is nothing of importance in your file cabinet. If you want
to check any data, you may wish to ask your secretary."
end verb.


End Object.


Object pictures1 Name painted pictures AT office


Description


Verb examine, look_at does only
"The pictures are nothing special. At least you had fun painting them."


End Verb.

verb take does only
"Leave the pictures where they are."
end verb.
End Object.


Object papers Name pile 'of' loose papers AT office
description

Verb examine, look_at does only
"They are just scraps of paper from earlier cases. None are of
importance now."


End Verb.

verb take does only
"You have no need for the papers."
end verb.

End Object.


Object window Name office window AT office

Description


Verb examine, look_at does only
"It is letting some fresh air."

End Verb.
verb close does only
"You would rather leave it open."
end verb.

End Object.


Object fan Name old fan  AT office
is on.
Description


Verb examine, look_at does only
if fan is on then
"It is blowing gently in your direction."
else
"The fan is currently turned off."
end if.
End Verb.

verb Turn_on, turn_fan_on does only
if fan is on then
"It is already on!"
else
"You turn on the fan."
make fan on.
end if.
end verb.

verb Turn_off, turn_fan_off does only
if fan is on then
"You turn off the fan."
make fan not on.
else
"It is already off."
end if.
end verb.

verb switch, press does only
If fan is on then
"You turn the fan off."
make fan not on.
else
"You turn the fan on."
make fan on.
end if.
end verb.
End Object.


Object table Name small old table AT office

Description


verb push does only
"You like the table where it is."
end verb.

End Object.


Object door1 Name your office door  AT office
is not open.
Description


Verb examine, look_at does only
if door1 is open then
"Your door is currently open."
else
"Your door is currently closed."
end if.


End Verb.

verb open does only
if door1 is open then
"It is already open."
else
"You open the door."
make door1 open.
end if.
end verb.

verb close does only
if door1 is open then
"You close the door."
make door1 not open.
else
"The door is already closed."
end if.
end verb.


End Object.

Location office2
Description
"You are standing in your secretary's office."

if secretary is not visited then
"Julie is currently sitting 
at her desk typing away. ""Hello Jim."", she says as she gives you
a quick glance. 
$n $n
You give her a smile and tell her to go on with her work.
$n $n"
make secretary visited.
end if.
if secretary is not glassed and glasses is worn then
"""Hello, four eyes, Julie says with a smirk."" You ignore the
comment."
make secretary glassed.
end if.
"The room is quite small, and can barely be called an office. It is
more of an reception area for potential clients. Besides a small desk
which your secretary is working at, there are a set of chairs against the
far wall, and a file cabinet to your direct right. 
$n $n
There are a couple of pictures hanging here, none of which are your
landscapes. Julie decorated this room, and it actually looks
a lot better than your own office."
locate door2 at office2.
locate door1 at office2.

if door2 is open then
"Your office is to the direct south. To the west is a open door."
else
"Your office is to the direct south. To the west is a closed door." 
end if.
Exit s To office
check door1 is open
else
"You need to open your office door first."
does
"You walk into your office."
end exit.

exit w to hallway
check door2 is open
else
"You need to open the door first."
does
"You walk into the hallway."
end exit.

End Location.


Object door2  Name outer office door  AT office2
is not open.
Description


Verb examine, look_at does only
"The office door contains your name painted onto the glass,
Jim Novak, Private Investigator. A small crack travels along 
the edge of the glass, a reminder of one bad day when you slammed
the door a bit too hard.$n $n"

if door2 is open then
"The office door is currently open."
else
"The office door is currently closed."
end if.

End Verb.

verb open does only
if door2 is open then
"It is already open!"
else
"You open the office door."
make door2 open.
end if.
end verb.

verb close does only
if door2 is open then
"You close the office door."
make door2 not open.
else
"It is already closed!"
end if.
end verb.

End Object.



Object Case  Name case  AT office2
is not done.
Description
End Object.


Object drawer2 Name desk drawer  AT office2

Description

End Object.


Object keys2 Name desk key  AT office2

Description

End Object.

actor secretary  Name pretty Secretary  AT office2
is not visited.
is not glassed.
is not shot.
Description

verb shoot does only
if gun1 in inventory then
if secretary is not shot then
"You raise your gun at Julie. Julie yells, ""Hey, put that thing away.
You never know when it could go off!"". You oblige, apologizing for
your stupid joke. It was a joke, wasn't it? It almost seems as though
someone else was controling your actions..."
make secretary shot.
else
"Julie wasn't fond of your attempt earlier. I wouldn't advise it again."
end if.
else
"You aren't carrying your gun."
end if.
end verb.

Verb examine, look_at does only
"Julie is quite pretty and unfortunately a bit young for you. She
has short blond hair and blue eyes. She is currently typing away."

End Verb.

verb ask_for, talk_about, ask_about does only

if obj=desk2 or obj=keys2 or obj=drawer2 then
if deskkey in inventory then
"""I already gave you my key. If you've lost that one you're in trouble."""
else
"""Lost your key again? Here's my copy. Don't lose it again, or
you'll be locked out forever."", Julie replies."
locate deskkey in inventory.
end if.
else
if obj=secretary then
"""You know everything you need to know about me, Jim."", Julie
answers with a smile."
else
if obj=case then
"""You don't have any cases at the moment, something you should be used
to by now."", Julie says with a smile."
else
if obj=photo then
if photo is named then
"""I don't know anything more about the photo."""
else
"""Give it to me and let me take a good look at it."""
end if.
else
"""I don't know anything about that."""
end if.
end if.
end if.
end if.
end verb.
End Actor.

Object desk2  Name small desk  AT office2

Description


Verb examine, look_at does only
"You bend over Julie's desk to take a look. ""Hey buddy."", she snaps
at you. ""If you want something, just ask. It's hard enough doing this
work without your big nose stuck in my face.""
$n $n
You apologize and look in another direction."

End Verb.

End Object.


Object pictures Name painted pictures AT office2

Description


Verb examine, look_at does only
"You don't recognize the artist, but they're generally pretty good."

End Verb.

verb take does only
"Julie wouldn't like that."
end verb.

End Object.

Object cabinet  Name file cabinet  AT office2

Description


Verb examine, look_at, open does only
"You do not currently need anything from the file cabinet."

End Verb.

End Object.

Location hallway
Description
"You are standing in a hallway directly outside of your office.
You have the only office on this floor of the building, which is
a bit rundown. The wallpaper, which seems to be barely clinging to 
the walls is a dark grey, and the light grey carpet is quite worn.

A set of stairs are to your south. The hallway continues to the north,
and your office is to the east."
locate door2 at hallway.

Exit E To office2
check door2 is open
else
"You need to open your office door first."
does
"You walk into the office."
end exit.
exit s to stairway
check gun1 in inventory 
else "You don't like leaving the building without your trusty gun."
does "You walk over to the stairway."
end exit.
exit n to hallway2.
End Location.


Object wallpaper  Name wallpaper  AT hallway

Description

verb look_at, examine does only
"It is similar to the wallpaper in your apartment, a nice dingy grey."
end verb.
End Object.


Object carpet Name carpet  AT hallway

Description

End Object.



Location hallway2
Description
"You are standing in the hallway in front of a partially painted over
window. It is sealed shut, although a slight breeze can be felt
through a small crack near its' top. Through the window you can
see the alley below. The only exit is to the south."

Exit s To hallway.

End Location.



Object window2 Name sealed window  AT hallway2

Description


Verb examine, look_at, open does only
"The window is sealed shut."


End Verb.

verb look_through, look_into does only
"All you can see is the dirty alley below."
end verb.
End Object.

Location stairway 
Description
"You are standing before a stairwell leading to the ground floor. Your
office is to the north."

Exit n To hallway.
exit s to stairs.
End Location.


Object crack Name small crack  AT hallway2

Description


End Object.


Object stairs15  Name stairs  AT stairway

Description


Verb 'use' does only
"You walk down the stairs."
locate hero at stairs.

End Verb.


End Object.

Location stairs
Description
"The stairs lead down to the street level. You can hear the hustle
and bustle of everyday life as the sounds of the outside
world make their way into the building. Exits lead down and north.
" 

Exit n To stairway.
exit d to street.

End Location.


Object stairs1 Name stairs AT stairs
Description


Verb 'use' does only
"You walk up the stairs."
locate hero at stairway.

End Verb.

End Object.


Object sounds Name sounds  AT stairs

Description


Verb listen_to, hear does only
"They are just normal sounds."
End Verb.

End Object.

Location Street
Description
"You are standing outside the building which houses your office.
This area of town is generally quite busy, at least during business
hours. A few people brush across you as they walk by in a hurry.
$n $n Exits lead up to your office 
and east and west along the sidewalk."


Exit u To stairs.
exit e to sidewalk.
exit w to sidewalk2.
End Location.


Object people Name people  AT street

Description


End Object.


Object sidewalk22 Name sidewalk  AT street

Description


Verb examine, Look_at does only
"Boy, is it dirty."

End Verb.



End Object.


Location Sidewalk2

Description
"You are standing before an old pawn shop. The barred windows
protect the gathered riches of the poor and downtrodden of the
neighborhood. Visible exits lead east and north."

Exit e To Street.
exit N to Shop
check case is not done
else "The store is currently closed."
end exit.

End Location.


Object sidewalk24 Name sidewalk  AT sidewalk2

Description


End Object.


Object windows Name barred windows  AT sidewalk2

Description


Verb open does only
"You wouldn't want to do that."

End Verb.

verb look_into does only
"There is an assortment of goods located inside."
end verb.
End Object.


Object bars Name bars AT sidewalk2

Description

End Object.

Location Shop
Description
"You are standing inside a pawn shop. It is cluttered with various
goods. An old man, most likely the owner, is sitting behind the
counter. "

Exit s To sidewalk2.

End Location.


Object store  Name store  AT shop

Description

End Object.

Object objects Name objects  AT shop

Description


End Object.


actor broker  Name old broker  AT shop

Description

verb examine, look_at does only
"The owner is an old man, probably about seventy. He is thin as a rail
and has a shock of white hair. His suit hangs from his frame as
though it was two sizes too big. The old man's face is wrinkled as
a prune and his eyes are locked into a fixed gaze staring across
the store, as though he his mentally cataloging every item within.
""Can I help you Sir?, the old man asks. He has a slight European
accent, although you can't exactly place it."
end verb.
verb talk does only
"""What would you like to talk about, young man?"", the owner
asks."
End Verb.

verb ask_about, talk_about does only

if obj=documents then
"""Well, let me take a look at 'em."""
else
if obj=bracelet then
"""That's a nice look'n piece of jewerly. I'll offer you ten bucks
for it."""
else
if obj=store then
"""I've owned this store for thirty years. Yep, me and this place
go back a long time. Seen good times and bad. Take a look about.
If there's anything you want here, just let me know."""
else
"""I don't reckon I know anything important about that."""
end if.
end if.
end if.
end verb.
End actor.

Object counter Name counter  AT shop

Description



End Object.


Object goods Name items  AT shop

Description


End Object.

Location sidewalk
Description
"You are standing in front of Harold's newstand, your favorite
place to check out the local gossip. Harold is standing behind
his stand reading a newspaper. $n $n
The only exit is to the west."
if case is done then
"As you look about, you notice a large headline on one of the newspapers."
end if.


Exit w To street
check case is not done
else "You should check the newspaper headline before leaving,
it could be important."
end exit.

end location.


actor haroldman  Name harold AT sidewalk
is not shot.
Description

verb shoot does only
if haroldman is not shot then
"You raise your gun, as to shoot Harold. He backs up, a bit nervously,
and stutters out, ""Quick foolin' Jim."". You pull the trigger,
click, click. The gun is not loaded. Thank God. Harold looks at you
like you are crazy and says sarcastically, ""Nice Joke."" He goes
about his work, although he seems to be watching you out in a careful
manner. What made you do it? It almost seems as if someone else
was controlling your actions. Ordering you about with commands..."
make haroldman shot.
else
"You already tried it once. Not again..."
end if.
end verb.

Verb Examine, Look_at does only
"Harold is standing next to the newsstand. He is an ordinary looking
man, nothing special about him."

End Verb.

verb Talk does only
"""What's going on?"", you ask Harold. He simply answers, ""Nothing
much""."
end verb.

verb talk_about, ask_about does only
"""I know nothin' about nothin'."", Harold answers. Whatever that means."
end verb.
End actor.


Object newsstand1 Name newsstand  AT sidewalk

Description


Verb examine, look_at does only
"Piles of newspapers, magazines and pulp digests are lined up in
neat rows. Harold is standing next to the stand, looking
over a magazine."

End Verb.

End Object.



Object newspaper1  Name newspaper AT sidewalk

Description

verb examine, read, look_at does only
"You can't read the newspaper from here. You need to buy one."
end verb.

verb take, pick_up does only
"That would be stealing!"
end verb.

Verb buy does only
If case is done then
"You buy one of the newspapers and look over the first page.
In glaring letters, Top Director Killed in Accidental Fire.
Samuel Cranston's trailer 'accidently' caught on fire
last night. They didn't find a body, but they really
didn't expect to. The trailer was burnt to a cinder. $n $n
You never expected Haswell to go this far. You guess he decided
his one star, Sarah Cooper was worth anything to protect.
Even the murder of her husband. $n $n
You decide to take action. You call your police buddy,
Larry Kohler, to set up a meeting. You head over to Sammy's Bar,
waiting to meet him."
locate hero at bar.
else
"You've already read today's newspaper. Nothing special, a day
like any other day."
end if.
End Verb.



End Object.

Object magazine  Name film magazine AT sidewalk

Description

verb take, pick_up does only
if magazine not in inventory then
"That would be stealing!"
else
"You already have a magazine."
end if.
end verb.

Verb buy does only
if magazine in inventory then
"You don't need any more magazines."
else
if photo is named then
if coins not in inventory then
"You don't have any money to buy a magazine."
else
"You buy one of the film magazines."
locate magazine in inventory.
end if.
else
"You don't need any magazines at the moment."
end if.
end if.
End Verb.

verb read, open does only
if magazine in inventory then
if photo is named then
"You flip through the film magazine looking for any references
to the film 'The Postman always rings twice'. With luck, you manage to find a small
article with the information you need. The film was produced by
M-G-M, and their studio is not too far from here."
make photo perfect.
else
"You look through the magazine. You spot a few interesting film ads,
but nothing important."
end if.
else
"You pick up a magazine and start leafing through it. ""Hey fella"".
Harold barks, ""This ain't a lendin' library"". You quickly put
the magazine back down."
end if.
end verb.

End Object.



Object car1a Name car  AT sidewalk

Description
"Your car is parked here."



verb enter, open, drive, 'use' does only
if case is done then
"You have nowhere to go at the moment."
else
If photo is perfect then
if documents is not read then
"You first want to understand a little more about this case
before searching for Harbou. If you could only understand a
little more about the documents you were given."
else
"You get into your car and head out over to M-G-M, a short drive
away. As you head up to the studio gates, you try to think of
an excuse to get in."
locate hero at guardbooth.
end if.
else
"You have no need to drive anywhere at the moment."
end if.
end if.
end verb.
end object.


Object doorc1 Name car door  AT sidewalk

Description


Verb 'use', open does only
"If you wish to drive your car, simply state so."

End Verb.

End Object.


Location Bar
Description
"You are standing inside a dirty nightclub with the monicker, Sammy's.
It is pretty quiet for the time of day. You spot one or two regulars
sitting at the bar."

Exit s To estate
check "You would rather not leave so soon."
end exit.
End Location.


Object bar1 Name bar  AT bar

Description


Verb Examine, Look_at does only
"There is nothing special about it. Just your ordinary, run down
establishment. Maybe that's why you like it so much."

End Verb.

End Object.

object stool1 at bar
Description
verb sit, 'use', sit_on does only
if car_lot is ready then
"You are sitting at the bar. An old gentleman is serving drinks with
a blank expression on his face. Sitting to your left is a young
woman, face heavily made up, nursing a drink. $n $n
You ask the bartender for a whisky, straight up.
There is nothing like good whisky, unlike the rotgut you usually drink.
After few shots you feel
much better. You think over the case and come to the conclusion
that a trip out to Independent Studios could spread a little
light on the situation.
 You borrow a phone from the bartender and quickly call
information for Independent's address.
It's not that far away, and the way you drive (fast), it shouldn't
take more than 45 minutes."
locate hero at Station.  
else
"You sit down on a nearby bar stool and order a drink.
A whisky, straight up, just the way you like it. By your third
drink Larry finally shows up. ""Hey buddy, got any news for me?"",
Larry asks. You inform him about the entire case, and what has transpired
during the last 24 hours. Larry looks very interested, and makes a 
quick call to the station. ""I'll have a couple cops go out
to Mr. Haswell's home. In the meantime, take
it easy and keep your eyes open."" As Larry gets up, you mention
that perhaps you will take a quick ride out there. You follow
behind the police to a large house. The police enter, then quickly
leave. No luck, he isn't home. You don't give up so easilly, though.
After the police drive away, you walk up to the house. Haswell may
or may not be home, but at least you would like to get some clues."
locate hero at house4.
end if.
end verb.
end object.

Location House4
Description
"You are standing outside a posh two story house. The grounds appear 
well maintained, with a long row of neatly manicured shrubs
located along the edge of the property. The porch is to the north,
and a small gate is to your east."

Exit n To porch.
exit e to gate.

End Location.





Object house1  Name large house AT house4

Description


Verb Examine, look_at does only

"It looks like a very nice house."

End Verb.

End Object.


Object grounds Name green shrubs AT house4

Description


Verb Examine, look_at does only
"You think they must have a crew of groundskeepers maintaining 
their yard."
End Verb.

End Object.

Location porch
Description
"You are standing on the porch in front of a closed door. A large
wicker chair is standing to your direct right. The only exit
is to the south."

Exit s To house4.
exit n to inside_hallway
does
"You walk through the front door into the house."
end exit.
End Location.



Object chairp  Name wicker chair  AT porch

Description


Verb Examine, look_at does only
"It is simply a white wicker chair."
End Verb.

verb sit_in, sit_on does only
"You would rather not rest at this time."
end verb.

End Object.


Object porch2  Name porch  AT porch

Description


Verb Stand_on does only
"You already are!"


End Verb.

End Object.


Object frontdoor  Name front door AT porch

Description


Verb Examine, look_at does only
"The front door is currently closed."


End Verb.

verb knock_on, knock, open does only
"That would not be wise, as even if Haswell was home, he wouldn't let
you in."
end verb.
End Object.

Location gate
Description
"A small wooden gate is located here. To your south you can see a large
back yard."
if gate3 is open then
"The gate is currently open. Exits lead south and west."
else
"The gate is currently closed. The only exit is to the west."
end if.
Exit w To house4.
exit n to backyard
check gate3 is open
else "You can't, as the gate is closed."
end exit.

End Location.


Object gate3  Name wooden gate AT gate
is open.
Description


Verb Examine, look_at does only
"It is a simple, wooden gate."
if gate3 is open then
"The gate is currently open."
else
"The gate is currently closed."
end if.

End Verb.

verb open, unlock does only
if gate3 is open then

"It is already open."
else
"You open the gate."
make gate3 open.
end if.
end verb.

verb close, lock does only
if gate3 is open then
"You close the gate."
make gate3 not open.
else
"It is already closed!"
end if.
end verb.

End Object.

Location backyard
Description
"You are standing in the backyard of the Haswell estate. It is 
quite large. A set of lawn furniture is visible
to your left, and a row of shrubs is to your right. To the west is a
patio and to the south is a tennis court.
A closed gate is to your direct north."

Exit w To patio.
exit n to court.
exit s to gate.
exit e to patio
check "You look a bit foolish as you try to walk through the shrubs,
as the branches tear at you clothes and scratch your face. You manage
to make your way through one of the smaller shrubs when you come
face to face with a large man holding a set of hedge trimmers.
""Are you some kind of nut, the man exclaims. I have to fix that
now!"" You feel like an idiot walking through the Haswell's shrubs,
and quickly try to think of something to tell the man. 
""I was looking for clues, you nervously sputter out. I'm an investigator
working on a case for the Haswell's.""

The man looks at you oddly and says, ""Well, I don't know what kind
of clues you will find back here. Being an investigator you should
have noticed you could have just walked around the shrubs instead
of through them. Maybe you oughta go back to detective school or 
somethin'.""

He then goes back to his work cutting the shrubs, as you decide to
walk around to the other side. "
end exit.
 
End Location.

Object furniture Name lawn furniture  AT backyard

Description


Verb Examine, look_at does only
"There is nothing special about the lawn furniture."
End Verb.

End Object.


Object shrubs2 Name green shrubs  AT backyard

Description


Verb climb does only
"That would be unwise."
End Verb.

End Object.


Location court
Description
"You are standing on on a tennis court located behind the Haswell's
house. The court appears to have been recently used, as a few tennis 
balls are lying about. The court seems to be in decent shape, except
for a few small cracks located close to the net. Behind the court, to its
direct right, is a small metal shed. The only exits are east and south."

Exit s To backyard.
exit e to shed.
End Location.


Object court1 Name tennis court AT court

Description


Verb Examine, look_at does only
"You notice a small duffel bag located at the edge of the court."
locate duffel_bag here.

End Verb.

End Object.

Object net Name tennis net  AT court

Description


Verb Examine, look_at does only
"The net seems to be relatively new."
End Verb.

End Object.


Object cracks Name cracks  AT court

Description


Verb Examine, look_at does only
"There is nothing special about them."
End Verb.

End Object.


Object balls  Name tennis balls AT court

Description


Verb Examine, look_at does only
"They are ordinary tennis balls."

End Verb.

verb Play_with does only
"You don't have time to play any tennis."
end verb.

End Object.


Object duffel_bag Name duffel bag  AT nowhere

Description

verb open does only
"You need to pick up the bag first."
end verb.

Verb take, pick_up does only
"You pick up the canvas bag and search through it. The bag contains
nothing of interest except a small flashlight. You remove the light,
and put the bag back down."
locate flashlight in inventory.
End Verb.

End Object.



Location shed
Description
"You are standing before a small metal shed. Overgrown weeds and bushes
surround its sides. The rusted shed door is closed. The only exit is to
the west."

Exit w To court.
End Location.



Object shed1 Name shed  AT shed

Description


Verb examine, look_at does only
"It is an old and unused building."

End Verb.

verb enter does only
"Try opening the shed door first."
end verb.

End Object.


Object shed_door  Name rusty shed door  AT shed
is not used.
Description


Verb Examine, look_at does only
"The shed door is currently closed."

End Verb.


verb open, 'use' does only
if shed_door is used then
"There is no need to go back in there."
else
if flashlight not in inventory then
"You open the door and enter the shed. You look about, but it is
too dark to see anything, so you leave."
else
"You open the door and enter the shed. You turn on your flashlight
and look about. The shed is filled mostly with debris and garbage.
You find a small can of oil, which you take.
You also notice some old rags and empty gasoline cans sitting in the
corner of the shed, however. Hmm, you wonder if Haswell used these
to burn Cranston to death. You exit the shed, a bit more certain
Haswell committed the murder."
locate oil1 in inventory.
end if.

make shed_door used.
end if.
end verb. 

End Object.


Object flashlight Name flashlight  AT nowhere

Description


Verb Examine, look_at does only
"It is an ordinary flashlight. It is rather small, but should still
provide decent light."

End Verb.

verb 'use', turn_on does only
"You would rather not waste the batteries at this time."
end verb.

End Object.

Location inside_hallway
Description
"You are standing in a small hallway. To your north is the livingroom.
To the south is the porch and a staircase is to the east."

Exit n To livingroom.
exit s to porch.

exit e to staircase.
End Location.


Object hallway6a Name hallway  AT inside_hallway

Description


End Object.

Location livingroom
Description
"You are standing in a large, well furnished living room. There are a
set of sofas to your left, with a large coffee table directly in front of
them. In the corner of the room is a glass case, holding a collection
of guns.

A large portrait is hanging above the fireplace.
Across the room to your direct right is a partially closed window. The back door
is located  to the northeast.

A staircase is located to the west. Other exits lead north and south."

Exit s To inside_hallway.
exit w to staircase.
exit n to patio
does
"You climb out through the window onto the patio."

end exit.
exit ne to patio
check "You start to walk over to the back door, but hear someone
walking about nearby. You spot a maid in the hallway, close to the
door. You decide it would be safer not to approach this area."
end exit.
End Location.




Object gcase  Name gun case  AT livingroom

Description


Verb examine, look_at does only
"It is a glass case holding a small collection of fire arms.
It seems Haswell is fond of guns. You must remember to be careful if you ever 
run into him."

End Verb.

verb open does only

"It is locked shut."
end verb.
End Object.


Object window2a Name window   AT livingroom

Description


Verb examine, look_at does only
"It is the window you crawled through to enter the house."

End Verb.

verb 'use', open does only
"You climb through the window onto the patio."
locate hero at patio.
end verb.
End Object.


Object sofas  Name soft sofas AT livingroom

Description


Verb Examine, look_at does only
"You notice nothing special about the furniture."
End Verb.

verb sit_on, 'use' does only
"No sitting on the job for you."
end verb.
End Object.



Object portrait Name large portrait AT livingroom

Description


Verb Examine, look_at does only
"It is a portrait of Mr. Haswell. It appears to be have made when
he was a bit younger, or perhaps the artist was just being kind."

End Verb.

End Object.



Object firep Name fireplace  AT livingroom

Description

End Object.

Location staircase
Description
"You are standing in front of a set of stairs. The livingroom is to the
east, while a hallway leads north."
exit u to hallway3.
exit e to livingroom.
Exit n To hallway2
check "You hear a servant walking about the hallway, so you turn back.
You would hate to get thrown out."
end exit.
End Location.


Object stairsh Name stairs AT stairway

Description

verb climb_up, climb, enter, 'use' does only

"You walk up the stairs."
locate hero at hallway3.
end verb.
End Object.

Location hallway3
Description
"You are standing at the top of the staircase. Exits lead north,
and down."

Exit d To staircase.
exit n to hallway4.
End Location.


Object stairs2 Name stairs AT hallway3

Description


Verb Climb_down does only
"You walk down the stairs."
locate hero at stairway.
End Verb.

End Object.



Location hallway4
Description
"You are standing in the hallway before two rooms opposite 
one another. Exits lead south, east and west."

Exit s To hallway3.
exit e to maidroom.
exit w to den.

End Location.


Object hall4 Name hallway AT hallway4

Description

End Object.

Location maidroom
Description
"You are standing in what appears to be the maid's quarters. A small
desk is standing to your direct left, and the bed is next to it.
The room is sparcely furnished and decorated minimially.
The only exit is to the west."

Exit w To hallway4.

End Location.


Location den
Description
"You are standing in the den. It appears to have been recently 
used, as several papers and articles are lying about on the desk.
A leather chair is next to the desk, and a small sofa is across
from it. It the corner of the room is a small statue of a Roman soldier.
There are several paintings of landscapes hanging on the wall.
The only exit is to the east."
Exit e To hallway4.

End Location.



Object desk3 Name desk  AT den
is not searched.
Description


Verb examine, look_at does only
"You look over the papers on the desk, and come across an interesting
note. It simply reads, Important matter
to discuss at my home, Meet me today, Sarah Cooper. The letterhead
gives the address 1081 Vancouver Lane, Los Angeles. You quickly
(and silently) leave the house and drive out to the address written
down. You pull up to a large estate."
make desk3 searched.
locate hero at estate.
End Verb.

End Object.

Location patio
Description
"You are standing on a large patio. The back door to the house is
currently closed. A small window is located here, and is open slightly.
The only exit is to the east, leading to the backyard."

Exit e To backyard.
End Location.


Object back_door  Name back door  AT patio

Description


Verb examine, look_at, open does only
"The door is currently locked."

End Verb.

End Object.


Object oil1 Name can AT nowhere

Description


Verb 'use' does only
"What would you like to use it on?"

End Verb.

verb put_on does only
if obj2=window then
"You carefully oil the window."
locate oil1 at nowhere.
make back_window oiled.
else
"You have no need to oil that."
end if.
end verb.

verb drink does only
"You take a quick swallow of the oil. Before it hits the back of your
throat, you gag and spit it out. What were you thinking? You're not
the tin man."
end verb.

End Object.


Object back_window Name small window  AT patio
is not oiled.
Description


Verb enter, open, 'use', raise does only
if back_window is oiled then
"You silently raise the window, creating an opening big
enough to climb through. You lift yourself up and through the
opening into the house. You close the window after entering."

locate hero at living_room.
else
"You start to lift up the window, but it begins to make a loud,
creaking sound. You stop, as you are afraid of creating attention
to yourself."
end if.
End Verb.

verb oil does only
if back_window is oiled then
"It is already oiled!"
else
if oil1 in inventory then
"You carefully oil the window."
make back_window oiled.
locate oil1 at nowhere.
end if.
end if.
end verb.

End Object.



Object car2 Name car  AT estate

Description


Verb 'use', enter, open, drive does only
"You shouldn't chicken out and leave now. See the case through."

End Verb.



End Object.


Object doorc2 Name car door  AT estate

Description


Verb open, 'use' does only
"If you would like to drive the car, simply state so."

End Verb.

End Object.

Location estate
Description
"You are standing outside a large house. The gardens and surrounding
land are very well mantained. The front door is directly before you.
Your car is parked here."
Exit n To hallway1a
does
"You knock on the front door, with no response. You slowly turn the
handle, and it is unlocked. As you enter the house you quickly
notice that all the lights are off."
end exit.
End Location.


Object gardens Name gardens AT estate

Description

End Object.


Object house1a Name majestic house AT estate

Description

End Object.


Object door8 Name front door  AT estate

Description

verb examine, look_at does only
"It is currently closed."
end verb.

Verb open, 'use' does only
"You knock on the front door with no response. You slowly turn the
handle, and it is unlocked. As you enter the house you quickly
notice that all the lights are off."
locate hero at hallway1a.

End Verb.

End Object.


Location hallway1a
Description
"You are standing inside the house of Sarah Cooper. The lights
are currently turned off, although since it is twilight outside,
you can sort of make out where everything is. Exits lead
north and south."

Exit n To dining_room.
exit s to estate
check
"You think you should take a look about the house before leaving."
end exit.
End Location.


Object lights Name house lights  AT hallway

Description


Verb Turn_on, 'use' does only
"You try to turn them on, but it seems the power has been cut."

End Verb.

End Object.

Location dining_room
Description

"You are standing in the dining room. There is a dining table set
with fine china directly before you. A large fireplace is located
directly behind the table.

Exits lead north and South."
exit n to living_room.
exit s to hallway1a.
end location.


Object table1 Name table AT dining_room

Description


End Object.


Object fplace Name fireplace  AT dining_room

Description

End Object.


location living_room
description
"You are standing in the living room. A love seat and coffee table
are in front of you.
A bookcase is to your direct right."
if couch is not visited then
"A couch is sitting in the corner, and it appears that two people are
sitting in it. It is quite dark, however, and you can't be sure."
else
"On the couch are sitting the dead bodies of Sarah Cooper and Frank Haswell.
Their throats have both been slit, and their blood has soaked into
the couch."
end if.
"To your east is a sliding glass door and the hallway is to the south."

Exit s To dining_room.
exit e to back_patio.

End Location.




Object couch Name couch  AT living_room
is not visited.
Description


Verb examine, look_at does only
if couch is not visited then
"As you approach the couch you realize who is sitting there, (laying
there might be a better term). Both Sarah Cooper and Frank Haswell are
laid out with their throats slit wide open. Blood has soaked into the couch
creating a dark red stain.  You take a step back, trying to comprehend
what has happened. Obviously Haswell did murder Sarah and himself,
but then who...?"
make couch visited.
else
"The couch is soaked red from the dripping blood."
end if.
End Verb.

verb sit, sit_on does only
"You carefully move the dead bodies over, trying to find a good spot
to sit. Unfortunately, the blood has soaked all over the couch and there 
does not appear to be a dry spot. Anyway, you would rather not sit
next to dead bodies. They give you the creeps."
end verb.
End Object.


Object blood Name blood  AT living_room

Description

End Object.


Object bodies Name  Frank Haswell   AT living_room
is not searched.
Description


Verb examine, look_at does only
"He is dead as a doornail. You look through his jacket for clues,
but do not find any."
if bodies is not searched then
"You do discover a small box of bullets in his pocket. The box
is ripped open, leading you to believe he tried to use them before
he died. You remove the bullets and take them."
make bodies searched.
locate bullets at hero.
end if.
End Verb.

End Object.


Object bullets  Name bullets AT nowhere

Description


Verb 'use', load does only
if gun1 is loaded then
"Your gun is already loaded!"
else
"You load your gun."
make gun1 loaded.
end if.


End Verb.

verb put_in does only
if gun1 is loaded then
"Your gun is already loaded!"
else
"You load your gun."
make gun1 loaded.
end if.
end verb.

End Object.



Object bodies2 Name Sarah cooper  AT living_room

Description


Verb Examine, look_at does only
"She is beautiful, even dead. Although the large slit in her
throat doesn't help her looks any. You do not find any clues."

End Verb.

End Object.


Object bodies3 Name bodies  AT living_room

Description


Verb examine, look_at does only
"Please state which body you wisk to examine."

End Verb.

End Object.

Object furn Name coffee table   AT living_room

Description


End Object.




Object seat2 Name love chair  AT living_room

Description

End Object.


Location back_patio
Description
"You are standing on the patio. The sun is setting and the sky is
lit with a crimson haze. Lawn furniture is scattered about here, as though
a fight took place. Drops of blood are smeared on the cement.
A large pool is to your east and a glass door is to the west."

Exit e To pool.
exit w to living_room.
End Location.


Object patio1  Name patio  AT back_patio

Description


Verb examine, look_at does only
"The patio is a mess."

End Verb.

End Object.


Object lawn Name furniture AT back_patio

Description


Verb examine, look_at does only
"The lawn furniture is scattered randomly about."

End Verb.


End Object.


Object blood2 Name drops 'of' blood  AT back_patio

Description


Verb examine, look_at does only
"They lead away from the house."

End Verb.

End Object.



Object door11 Name glass door AT living_room

Description


Verb open, 'use', examine, look_at does only
"It is open."

End Verb.

verb close does only
"You would rather leave it open at this time."
end verb.

End Object.

Location pool
Description
"You are standing before a large pool. It is presently empty, needing
to be refilled. To your direct right are a set of bushes. You are
not sure, but you think you see something moving behind them."

Exit w To back_patio.

End Location.


Object  pool2 Name large pool  AT pool

Description


Verb Examine, Look_at, enter does only
"The pool is empty. You try not to get too close, as if you fell in, it
could be very dangerous."

End Verb.

End Object.


Object bushes2 Name bushes  AT pool
Description


Verb examine, look_at does only
"You carefully look about the bushes next to the pool. You think you spot
something, and unfortunately your suspicions are confirmed. A man pops
up from behind, holding a large knife. Just the sort of knife which
slit the throats of Sarah Cooper and Frank Haswell. The man is
horribly deformed, with scars are burns all over his face. At first
you have trouble recognizing the man, but after a few seconds it
comes to you. That is Samuel Cranston, or whatever he calls himself
now. He must have escaped the fire, most likely set by Haswell,
and is now seeking revenge. You are his next target. Cranston
swings the knife across your chest, just missing you. You
immediately pull out your trusty gun and aim at his chest.
You get ready to shoot when Cranston leaps at you and slits"
if gun1 is loaded then
"your right arm. You pull your arm back and shoot, hitting him in the
left leg. He leaps at you like some mad animal, knocking your gun away.
You fall backwards, losing your balance and slipping over the side of the
empty swimming pool. You hang on for dear life, with nothing but cement
below you. "
locate hero at poolside.
else
"your right arm. You pull your arm back and shoot, click, click ...
How stupid! You forgot to load your gun! Cranston takes his knife
and slowly slits your throat. You feel your blood ooze over your body,
and you feel as though you are losing consciousness. The last thing
you remember is Cranston standing over you with a wide smile on his face.
Then you die."
end if.

End Verb.

End Object.



Location poolside
Description
"You are hanging over the edge of a large, empty pool. Nothing
but hard cement is below you. Standing directly above is the
deformed Cranston, smiling gleefully. He is stepping on your
left hand, causing you extreme pain. You have an immediate urge to
let go, but doing so would mean falling. There are no visible exits."

End Location.


Actor Cranston  Name Samuel Cranston  AT poolside

Description

verb examine, look_at does only
"He is not a pretty sight."
end verb.

Verb attack does only
"You can't hit him and still hang on."


End Verb.

End actor.


Object leg  Name left leg AT poolside

Description


Verb pull does only
"You reach up with your hand and grab Cranston's left leg, directly
where is was shot. He screams in agony, and tries to remove your
hand. You pull harder, and his leg gives out. He falls over the
side of the pool, smashing his head on the hard cement. Blood seeps
out from his crushed skull, and he appears to be dead. $n $n
You pull yourself up and onto firm ground. Looking down into
the pool, you see what is left of Cranston. His body is a mangled
clump, with his brains splattered on the cement below. The police
must have heard your gunshot, as they arrive about five minutes later.
Your buddy Larry, arrives with them. You explain what has happened,
and your theory on the murders. After examining the scene, Larry
says it all seems to check out. After a few days of recuperation,
you decide to finish the case by seeing Thea. She has already heard
the news, and actually made out pretty well on the deal. It seems
since she was still the legal wife of Cranston, she is entitled to
receive his full estate and savings, which is quite a sum. Thea
is now a rich woman. She gives you a large payment for your
services and thanks you enormously. It seems at least some good
came out of this case, one which you will never forget..."


End Verb.

End Object.

Location Guardbooth 
Description
"You are parked at the gates outside M-G-M. A small security booth
is to your direct left. A tall, husky man is standing there, looking
at you with a suspicous eye. ""Can I help you, buddy?"", the guard asks.
The security gate is currently lowered, blocking further passage
into the studio. The road back to your office is to your direct
south."


Exit s to sidewalk
does
"You decide to turn around and head back to the office. Perhaps
it will give you some time to think of a way into the studio."
end exit.

End Location.


Object gates  Name gates AT guardbooth

Description


Verb Open, 'use' does only
"I don't think the guard would allow that."

End Verb.

End Object.



Object Cash Name wad 'of' bills  AT nowhere

Description


Verb Examine, look_at does only
"You have ten dollars."

End Verb.

verb give does only
"Who do you wish to give it to?"
end verb.

verb give_to does only
if act= guard then
"You hand the guard ten dollars, with the idea of gaining entrance
to the studio. The guard looks at the money and for an instant you think
he will throw you out. He glances about, and quickly waves you by.
""Don't cause any problems, and be quick."", the guard advises.
You thank him and go on your way."
locate cash at nowhere.
locate hero at lot.
else
"Don't give away your money. You may eventually need it."
end if.
end verb. 
End Object.

Actor Guard Name Security guard  AT guardbooth
Description


Verb ask_about, ask_for, talk_about does only
if obj=gates then
"""I can't let you in unless you have a good reason."""
else
"""I don't know anything about $o""."
end if.
End Verb.




End actor.


Object car_lot Name car  AT lot
is not ready.
Description


Verb 'use', enter, drive does only
if suit1 is not fixed then
"You have things to do here before you leave."
else
if suit1 is not ready
then
"You have a few items that don't belong to you."
else
"You enter your car and drive out to Sammy's bar."
locate hero at bar.
end if.
end if.
End Verb.

End Object.

Location Lot 
Description
if suit1 is not used then
"You are standing in front of a large administration building.
You imagine the head honchos should be in 
there. Further to your east is another
 huge building. Your
car is parked here." 
else
make suit1 not used.
"You tilt your head downwards and in you best ""fake"" voice
ask to see Mr. Peterson. You explain that you are an important
producer for Universal and have an appointment.
She looks at you strangely for a moment, then nervously looks through
an appointment book. As she does so, you quickly slip into Peterson's
office. $n $n
""What are you doing in here!"", Peterson yells.
Standing before you is Mr. Peterson, buck naked accept for a pair
of boxer shorts. His ""secretary"", a young, well endowed blond,
is lying on a nearby couch. As soon as she sees you, she frantically trys
to cover her nude body (and a fine body it is). She quickly covers
up and makes a mad dash out of the office. Peterson leaves the
room for a second, holding his business suit. He arrives back in
after a minuet or so, fully dressed.
""Miss Stevens, I thought I told you no visitors!"", Mr. Peterson bellows.
The receptionist explains how you sneaked in wearing a disguise and she tried
to stop you. Mr. Peterson quickly grabs the phone and states firmly
that he is calling the police. He changes his tone once you tell
him you are a private investigator. ""My God, Stella must have caught on.
Whatever, she's paying you to spy on me, I'll double it."", Peterson
sputters out. You assume Stella is his wife and decide to play along.
""Well maybe I won't have to make a report back to Stella, as
long as you help me out here."", you try to say in a convincing manner.
You walk over to the desk and inquire about Joseph
Harbou. You take out the photo you have of him.$n $n
""I don't know who Joseph Harbou is, but the man in the picture is
Samuel Cranston, one of the big directors for Independent Studios.
I remember he was in 'Postman' as an extra when he was just starting out.
He's big now, married to Sarah Cooper.""
Mr. Peterson looks confused, as though he is trying to figure
out what Cranston has to do with his girl chasing.
You quickly thank him for his help before he gets too curious.
Peterson nervously opens his wallet, and pulls out a few loose
bills. ""I only have a few bucks on me, but I can get more at the
bank. What is Stella paying you, 200 hundred? 300?""
You take a buck, figuring it will do for gas money coming out here.
""No need, you've helped me enough"", you tell him,
and quickly leave the building. In the distance you hear Peterson
yell out, ""Remember, not a word to Stella."". $n $n
Sarah Cooper, she's one of the biggest dames in Hollywood. She's
a screen idol, and he's married to her. Something strange
is going on here, and you plan to get to the bottom of it.
A little lubricating oil for the brain is what you need,
and Sammy's bar isn't too far from here."
locate buck in inventory.
make car_lot ready.
end if.
Exit n To Administration
check suit1 is not fixed
else "You have no need to go back in there."
end exit.
exit e to walk1.
End Location.


Location administration
Description
"The inside of this building is as impressive as the outside. A
large reception desk is located to your direct left. Portraits
of the studio heads are hanging from the walls surrounding you, and
a large bust of Louis B. Mayer is located on a pedistal to your
direct right.  The wall to your left is ordained with framed film
posters, all hits from M-G-M.

$n $n
The floor is carpeted with deep plush and the walls are painted a
solid white. The only visible exit is to the south."

Exit s To lot.

End Location.


Actor receptionist  Name Receptionist  AT administration
is not visited.
Description
"There is a young receptionist sitting here. ""Can I help you,
Sir?"", she asks."

Verb Examine, look_at does only
"She appears to be in her early thirties, and had curly brown hair and
a ruddish complexion."

End Verb.

verb talk does only
if receptionist is visited then
if suit1 is worn and mustache is worn and glasses is worn then
make suit1 used.
make suit1 fixed.
locate hero at lot.
else
"There doesn't seem to be much point in trying to talk to her.
At least, not at this time."
end if.
else
"You express your interest in seeing someone in charge regarding
a case you are working on. ""Oh, you're in the police!"", the
receptionist says as her face lights up. She seems
to lose interest when you tell her that you're just a private
investigator.
""Well, Mr. Peterson is busy at the moment sir. If you would like
to make an appointment, perhaps you could see him at a later date.""
You thank her, but decline to make an appointment. You can't wait
forever, you've got to see him today."
make receptionist visited.
end if.
end verb.

verb ask_for, talk_about does only
if obj=receptionist then
"""I'm too busy right now to chat, sir."""
else
"You can't do that."
end if.
end verb.
End actor.

Location station
description
if car3 is not ready then
"You arrive at the studio in good time
and manage to get an appointment
with Frank Haswell, head of Independent Studios. Independent isn't
as nearly elaborate as M-G-M, but it is still pretty big. They put out
a few films a year, most of them with their star, Sarah Cooper.
You are taken to a backstage studio and told to wait a few minutes.
Mr. Haswell should see you shortly.
It appears they are setting
up a shot for a film they are working on. To your far right, a few extras
are walking about. You spot Sarah Cooper in the far distance preparing
for a scene.
After waiting a few minutes, Mr. Haswell shows up. He is wearing a dark
suit, apparently trying to hide his massive girth. He waddles over
to you with a strange grin on his face, as though he is making fun of
you. ""Well, I hear you're a private eye. We did a couple of detective
flicks a few years back. They did okay, no hits though."" He rambles
on for a few minutes, before you interrupt him with an inquiry about
Frank Haswell. His face immediately loses that grin. ""I don't know
what you're getting at fella, but I don't like it. Frank is a
 good American. Just because he's a success, married to Sarah Cooper,
all the lowlifes come showing up trying to make a fast buck. Well
it won't work fella, not this time."" He motions for a guard to 
escort you off the lot. You quickly show the documents
and photo of Frank proving to Haswell that you have proof of his
past marriage and his past life.
His face turns white. He now tries a different tactic. ""Look
buddy, we don't want any trouble, right? It won't look good
for the husband of Sarah Cooper to be both a bigamist and an ex-Nazi.
How much do you want, how 'bout five grand?"" Now that's more money
than you ever seen at one time. You do have your morals though.
You turn down the offer and leave the set. You decide to let
him think a bit about information and get back to him later. You've
basically solved the case for Thea, although you are not sure
how she will react. You leave the studio and head for home.
You stop at a nearby gas station, as you are running low."
make car3 ready.
else
"You are located at a gas station. An attendant is standing in the
far distance talking with a customer. You are getting impatient."
end if.
End Location.


Object car3 Name car AT station
is not ready.
Description


Verb Examine, Look_at does only
"It is running low on fuel. You are currently parked."

End Verb.

verb 'use', drive, leave does only
"Wait until you get gas first."
end verb.

End Object.


Object door5 Name car door  AT station

Description


Verb 'use', open does only
"Don't leave your car, the attendant will fill it up. Eventually..."

End Verb.

End Object.


Object radio2 Name car radio  AT station

Description


Verb 'use', turn_on does only
"It is busted."

End Verb.

End Object.


Object horn Name car horn  AT station

Description


Verb 'use', honk does only
"You honk for the attendant, who finally decides to service your car.
He quickly fills it up and after paying him a buck, you are on your way.
You arrive at your apartment about eleven, and are bushed after a
good day's work. You hit the sack and wake up early, (at least early for
you) at about 9:00. You head out for the office to start a new day,
and hopefully close the case."
locate buck at nowhere.
locate hero at sidewalk.

End Verb.

End Object.

Location Walk1
Description
"You are standing before a huge grey building. A few palm trees
are planted along side its entrance. To your east is another
guard booth, and to the west is the administration building."

Exit w To lot.
exit n to wardrobe.
exit e to lot
check "You would rather not risk anther encounter with a guard.
You don't have any more money, for one thing..."
end exit.
End Location.


Object ptrees Name palm tree  AT walk1

Description


Verb examine, look_at does only
"You watch as they sway back and forth."

End Verb.

verb climb does only
"You are not a monkey."
end verb.

End Object.


Object gbuilding Name grey building  AT walk1

Description

verb look_at, examine does only
"It is impressive."
end verb.

Verb enter does only
"You walk into the building."
locate hero at lot.

End Verb.

End Object.

Location Wardrobe
Description
"You are standing inside the warddrobe building. Hangers upon hangers
of costumes, dresses and various clothes are located here. A few
busy workers are scurrying about taking racks of clothes to different
areas of the building. $n $n"
if suit1 is fixed and suit1 is not ready then
"You place the suit and other items back where you got them from."
locate suit1 at wardrobe.
locate mustache at nowhere.
locate glasses2 at nowhere.
locate pocket1 at wardrobe.
make suit1 ready.
end if.
Exit s To Walk1.

End Location.


Object workers Name worker AT wardrobe

Description
End Object.


Object racks Name clothes racks  AT wardrobe

Description

verb examine, look_at does only
"You look over the racks of clothes"
if receptionist is visited then
if suit1 not in inventory then
"spotting a nice grey suit hanging nearby. It looks like it
will fit you, as well. Hmm.."
else
", not finding anything of interest."
end if.
else
",not finding anything of interest. There are a few nice suits, etc.,
but nothing of importance at the moment."
end if.
end verb.

End Object.

Object suit1  Name business suit  AT wardrobe
is not worn.
is not used.
is not fixed.
is not ready.
Description


Verb Examine, look_at does only
"It is a grey flannel business suit, quite nice."

End Verb.

verb wear does only
if suit1 in inventory then
"You are already wearing it."
else
"You need to take it first."
end if.
end verb.

verb take does only
if suit1 is fixed or suit1 is worn then
"You do not need a suit."
else
"You take the suit and try it on. Not a perfect fit, but good enough.
You think you'll ""borrow"" this suit for a while."
locate suit1 in inventory.
make suit1 worn.
locate pocket1 in inventory.
end if.
end verb.


End Object.


Object pocket1 Name pocket at wardrobe
container
Description


Verb examine, look_at does only
if suit1 in inventory then
"You look inside the pocket's of the jacket. A pair of dark glasses
and a fake mustache are inside."
locate glasses2 in inventory.
locate mustache in inventory.
else
"You need to take the suit to look inside the pockets."
end if.

End Verb.

End Object.


Object glasses2 Name dark glasses  AT nowhere
is not worn.
Description


Verb Examine, look_at does only
"They are simple, dark glasses."

End Verb.

verb wear, 'use' does only
if glasses is worn then
"You raise the dark glasses to your head and try to put them on.
but they won't fit for some reason. You really are becoming senile,
you are still wearing your reading glasses."
else
if glasses2 is worn then
"You already have them on."
else
"You put on the glasses."
make glasses2 worn.
end if.
end if.
end verb.

verb 'remove', take_off does only
if glasses2 is worn then
"You remove the glasses."
make glasses2 not worn.
else
"You are not wearing any glasses!"
end if.
end verb.
End Object.


Object mustache  Name fake mustache  AT nowhere
is not worn.
Description

verb grow does only
"You concentrate for a few minutes on facial hair growth, but nothing
happens."
end verb.

Verb Examine, look_at does only
"It is one of those fake mustaches worn in the movies."

End Verb.


verb wear, 'use' does only
if mustache is worn then
"It is already on!"
else
"You stick on the fake mustache. There is still enough glue on it
to make it stick."
make mustache worn.
end if.
end verb.

verb 'remove', take_off does only
if mustache is worn then
"You take off the mustache. It stings a bit."
make mustache not worn.
else
"You are not wearing a mustache!"
end if.
end verb.
End Object.


Object buck Name buck  AT nowhere

Description


Verb spend, give, 'use' does only
"There is no need to spend your money right now."

End Verb.

End Object.


Location nowhere
Description

End Location.


Start at office.

" The Hollywood Murders
$n
 An Interactive Mystery
$n
$n
 Written by Michael Zerbo $n
 
$n
$n
$n
Your name is Jim Novak, a private investigator living in a suburb
of Los Angeles. You have a small rented office located in a run down
part of the neighborhood, an old jalopy and a few dollars to your name.

You haven't had much of a caseload recently, or actually anytime that you
can remember. You've managed to scratch out a living in this line
of work, but just barely. Maybe your luck will change. 
It actually can't get much worse. Or so you think... "



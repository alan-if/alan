-- SKEPPET, det första Adventure som är skrivet med ALAN.
-- Uppdaterat till Alan 1.0 (900318 ThoNi)

OPTIONS
    Language Swedish.
    Width 80.
    Length 24.



OBJECT ATTRIBUTES

        takable.
        readable.
        NOT neutrum.
        NOT plural.


-- Globala Verb definitioner

SYNTAX l = l.
VERB l
  DOES
    LOOK.
END VERB.


SYNTAX ta = ta (o)*.
VERB ta

    CHECK OBJECT IS NOT takable
        ELSE
                "Du kan inte ta"
                IF OBJECT ARE plural THEN
                        "dem."
                ELSIF OBJECT IS neutrum THEN
                        "det."
                ELSE
                        "den."
                END IF.
    AND OBJECT NOT IN inventory
        ELSE
                "Du bär redan på"
                IF OBJECT ARE plural THEN
                        "dem."
                ELSIF OBJECT IS neutrum THEN
                        "det."
                ELSE
                        "den."
                END IF.
    DOES
                LOCATE OBJECT IN inventory.
                "OK!"
                IF OBJECT ARE plural THEN
                        "Tagna."
                ELSIF OBJECT IS neutrum THEN
                        "Taget."
                ELSE
                        "Tagen."
                END IF.

END VERB.

VERB släpp

        CHECK OBJECT IN inventory
            ELSE
                "Du bär inte på"
                IF OBJECT ARE plural THEN
                        "dem."
                ELSIF OBJECT IS neutrum THEN
                        "det."
                ELSE
                        "den."
                END IF.

	DOES
                LOCATE OBJECT HERE.
                "OK!"
                IF OBJECT ARE plural THEN
                        "Släppta."
                ELSE
                        "Släppt."
                END IF.

END VERB.


SYNTAX inventera = i.

VERB inventera DOES

        LIST inventory.

END VERB.

CONTAINER inventory
  HEADER "Du bär på"
  ELSE "Du är tomhänt."
END CONTAINER inventory.


SYNTAX vänta = vänta.

VERB vänta DOES

        MAKE time NOT immediate.
        "Tiden lider, och Du med den."
        IF time IS morning
        THEN 
                "Det är nu mitt på dagen, och solen står nästan rakt upp."
                MAKE time NOT morning.
                MAKE time noon.
        ELSIF time IS noon
        THEN
                "Solen vandrar sin stilla ban, och sänker sig vid horisonten."
                MAKE time NOT noon.
                MAKE time evening.
                "Dina föräldrar ropar på Dig. De tänker visst fara hem,"
                "men själv har Du ingen lust att åka ännu."
                LOCATE föräldrar AT p_plats.
                IF hjälte IS synlig
                THEN
                        "Så får Din far syn på Dig. Han stirrar med bestämd"
                        "blick rakt på Dig, och Du förstår att Du inte kan"
                        "smita undan. Du går honom till mötes."
                        LOCATE hero AT p_plats.
-- Här hade jag tänkt skicka honom till p_plats via filtplats, men eftersom
-- det inte funkade i flera led fick det bli såhär.
                END IF.
        END IF.

END VERB.

VERB klättra DOES

        "Det finns absolut ingen möjlighet för Dig att ta dig upp här."

END VERB.

VERB läs

    CHECK OBJECT IS readable
        ELSE "Du kan inte läsa"
            IF OBJECT IS neutrum THEN
		"den."
            ELSE
		"det."
	END IF.

    DOES
	"Vad vill Du jag ska läsa? En godnattsaga?"

END VERB.


-- Abstrakta object

OBJECT time

        IS morning.
        NOT noon.
        NOT evening.
        immediate.
        
END OBJECT.

OBJECT hjälte

        IS NOT fast.
        synlig.

END OBJECT.


-- LOCATIONS och fasta objekt

LOCATION klippa
        
    DESCRIPTION
        "Stranden slutar här i en tvär bergvägg, som Du inte har någon
         chans att klättra uppför."
        IF time IS evening
        THEN
                "I skenet från den nedgående solens sista strålar skimmrar
                 klippan i alla tänkbara nyanser av rött och guld."
        END IF.
        "Nyfiken försöker Du gå runt den, men
         finner att klippan verkar sträcka sig ända ned till havet."
        MAKE time NOT immediate.
        
        EXIT s TO klippa
	    DOES
                "Ett stängsel hindrar Dig från att hamna på motorvägen,
                 som stryker tätt förbi strandens södra sida."
        END EXIT.
        EXIT so TO p_plats.
        EXIT o TO filtplats.
        EXIT no TO västanhav.
        EXIT n TO västanhav.
        EXIT nv TO klippa
	    DOES
                "Klippan är alldeles för brant för att Du ska ha en
                 chans att ta dig upp den vägen."
        END EXIT.
        EXIT v TO klippa
	    DOES
                "Klippan är alldeles för brant för att Du ska ha en
                 chans att ta dig upp den vägen."
        END EXIT.
        EXIT sv TO klippa
	    DOES
                "Klippan är alldeles för brant för att Du ska ha en
                 chans att ta dig upp den vägen."
        END EXIT.

-- Om det hade gått säga STAY (eller BACK) i den LOCATION man kom till,
-- hade man kunnat implementera flera (omöjliga) platser som en enda.

END LOCATION.

LOCATION p_plats

    DESCRIPTION
        "Du står på Strandbadets stora parkeringsplats, och ser ut
         över stranden."
        DESCRIBE grinden.
        DESCRIBE anslagstavlan.
        IF time IS morning
        THEN
                "Så här tidigt är det ännu inte många bilar här."
        ELSIF time IS noon
        THEN
                "Parkeringsplatsen är alldeles fullbelagd utav allehanda
                 fordon."
        ELSIF time IS evening
        THEN
                "Nu har de flesta åkt hem. Endast enstaka bilar syns
                 fortfarande kvar vid stranden."
        END IF.
        IF föräldrar AT filtplats
        AND pengarna AT NOWHERE
        THEN
                "Från stranden hör Du Dina föräldrar ropa på Dig!"
                MAKE time immediate.
        ELSIF föräldrar HERE
        THEN
                        "Så sitter Du åter i bilen på väg hemåt den här
                         gången. Visst har det varit en fin dag, men något
                         säger Dig ändå att Du missat något. Någonstans
                         där bakom dig känns det som om ett Äventyr låg
                         och lurade, ett Äventyr som nu aldrig skulle bli
                         verklighet."
			QUIT.

        END IF.
        IF föräldrar IS NOT repeting
        THEN
                "Oh, vad Du är trött på att de alltid skall vilja hålla
                 reda på Dig. Som om Du vore någon barnunge! Du är ju
                 trots allt 17 år."
                MAKE föräldrar repeting.
        END IF.
        
        EXIT o TO glasskiosk.
        EXIT n TO filtplats.
        EXIT no TO strandmitt.
        EXIT v TO klippa
	    DOES
                "Du vandrar hit och dit utan särskilt mål."
        END EXIT.
        EXIT nv TO klippa.

END LOCATION.


OBJECT anslagstavlan AT p_plats

    IS NOT takable.

    DESCRIPTION
        "Intill grinden står en stor anslagstavla."

    VERB läs DOES ONLY
        "På anslagstavlan trängs förra veckans dansbandsaffischer med
         smålappar om borttappade badbyxor. Ett anslag väcker ditt
         intresse, eftersom det verkar vara ganska nytt (det är i alla
         fall inte sönderslitet ännu, och det är dessutom klistrat ovanpå
         de övriga). Det är ett meddelande från Strandens astronomiska
         förening. Snabbt skummar Du igenom texten, och får veta att
         det i kväll kommer att vara ett ovanligt kraftigt tidvatten
         på grund av solens och månens inbördes förhållande."
    END VERB.

END OBJECT.


OBJECT grinden AT p_plats

    IS NOT takable.

    DESCRIPTION
        "Rakt fram står grinden öppen till stranden."

END OBJECT.

LOCATION glasskiosk

    DESCRIPTION
        "Här står en glasskiosk, som verkar gå bra. Det är kanske
         inte så konstigt i den här värmen."
        DESCRIBE glass.
        "En stor strut skulle kanske inte vara så dumt!"

        MAKE time NOT immediate.

    EXIT v TO p_plats
	CHECK hjälte IS NOT fast
	    ELSE "Du kan inte röra dig."
    END EXIT.

    EXIT nv TO filtplats
	CHECK hjälte IS NOT fast
	    ELSE "Du kan inte röra dig."
    END EXIT.

    EXIT n TO strandmitt
	CHECK hjälte IS NOT fast
	    ELSE "Du kan inte röra dig."
    END EXIT.

    EXIT no TO sandslott
	CHECK hjälte IS NOT fast
	    ELSE "Du kan inte röra dig."
    END EXIT.

    EXIT o TO kiosk
	CHECK hjälte IS NOT fast
	    ELSE "Du kan inte röra dig."
    END EXIT.
        
END LOCATION.


OBJECT glass AT glasskiosk

    DESCRIPTION
        "Här finns glass av alla slag."

    MENTIONED
        "storstrut"

    VERB ta DOES
        "Du ser glassförsäljaren försvinna in i det bakre lagret. Snabbt
         sticker Du fram en hand över disken för att snatta en glass.
         Aj, aj !!! Det skulle Du inte ha gjort. Försäljaren kommer
         tillbaka utan förvarning, och med ett språng är han framme
         hos Dig. Hans stora näve griper Dig om handleden och vrider om.
         Glassen faller ur din hand, men han släpper inte taget."
        MAKE hjälte fast.
    END VERB.

    VERB ät DOES
        IF glass IN inventory THEN
            "Du slukar glassen i stora tuggor. Kanske inte det
             bästa sättet att äta glass på, men - Mmmm vad gott!!"
            LOCATE glass AT glasskiosk.
        ELSE
            "Då får Du allt köpa en glass först."
        END IF.
    END VERB.

    VERB köp DOES
        IF pengarna IN inventory
        AND pengarna IS NOT slut
        THEN
            IF glass IN inventory THEN
                "Försäljaren, en ovanligt otrevlig typ, vänder sig till
                 Dig och säger: 'Ät upp den Du har först. Jag har så
                 många kunder här som verkar behöva en glass mer än Du."
            ELSE
                "Försäljaren tar dina pengar och sticker till dig en strut.
                 Det var egentligen inte alls en sådan Du ville ha, men
                 han är redan hos nästa kund."
                LOCATE glass IN inventory.
                IF pengarna IS lite THEN
                    MAKE pengarna slut.
                ELSIF pengarna IS tillräckligt THEN
                    MAKE pengarna lite.
                ELSIF pengarna IS mycket THEN
                    MAKE pengarna tillräckligt.
                END IF.
            END IF.
        ELSE
            "Du har inte tillräckligt med pengar."
        END IF.
    END VERB.

    VERB släpp DOES
        IF glass IN inventory THEN
            "Du slänger glassen ifrån dig. Får man inte vad man vill
             kan man lika gärna vara utan, resonerar du.
             Glassen hade hur som helst nästan smält redan. Det lilla
             som var kvar försvinner snabbt i den heta solen."
            LOCATE glass AT glasskiosk.
        ELSE
            "Du bär inte på någon glass."
        END IF.
    END VERB.

END OBJECT.

LOCATION filtplats
        
    DESCRIPTION
        "Här är ett fint ställe att ligga på."
        IF föräldrar HERE THEN
                DESCRIBE föräldrar.
        END IF.
        
        MAKE time NOT immediate.

        EXIT s TO p_plats.
        EXIT v TO klippa.
        EXIT sv TO klippa
	    DOES
                "Du strövar omkring och hamnar i en av strandens mera
                 avlägsna delar. Längst i söder stöter Du på ett stängsel
                 och vänder norrut."
        END EXIT.
        EXIT o TO strandmitt.
        EXIT so TO glasskiosk.
        EXIT nv TO västanhav.
        EXIT n TO västanhav.
        EXIT no TO medelhav.

END LOCATION.


OBJECT föräldrar AT filtplats

        IS NOT takable.
        NOT repeting.
        plural.

    DESCRIPTION
        "Dina föräldrar har redan brett ut sin filt här i sanden,
         och slängt sig ned på den för att inte missa en gnutta sol."
        IF time IS immediate
        THEN
                "$p- Bra att Du kom, säger Din mor. Här får Du lite
                 pengar ifall Du vill köpa något. Hon sticker åt Dig
                 några sedlar."
                LOCATE pengarna IN inventory.
        END IF.
        
END OBJECT.

LOCATION strandmitt

    DESCRIPTION
        "Du står mitt på stranden bland en massa okända människor."
        IF föräldrar AT filtplats
        THEN
           "Borta i väster kan Du se dina föräldrar utfläkta på er
            gamla vanliga utflyktsfilt."
        END IF.
        IF skylten HERE THEN
                DESCRIBE skylten.
        END IF.
        "I öster är ett gäng i full
         färd med att bygga ett sandslott, som verkar vara något
         utöver det vanliga. Längs strandens södra kant ligger
         kiosker och småaffärer av diverse slag."
        MAKE time NOT immediate.

        EXIT v TO filtplats.
        EXIT s TO glasskiosk.
        EXIT sv TO p_plats.
        EXIT so TO kiosk.
        EXIT o TO sandslott.
        EXIT no TO östanhav.
        EXIT n TO medelhav.
        EXIT nv TO västanhav.

END LOCATION.


OBJECT skylten AT strandmitt
    IS
	readable.

    DESCRIPTION
        "En gammal skylt står här."

    VERB läs DOES ONLY
        "$i'VARNING för kraftiga underströmmar!$iSimma inte för långt ut
	 ifrån stranden.'"
    END VERB.

END OBJECT.

LOCATION kiosk

    DESCRIPTION
        "Här finns en liten butik, som verkar sälja allt möjligt."
        IF time IS evening
        THEN
           DESCRIBE innehavaren.
        ELSE
           "Det verkar inte finnas någon här att handla av för tillfället."
        END IF.

        EXIT v TO glasskiosk.
        EXIT nv TO strandmitt.
        EXIT n TO sandslott.

END LOCATION.


OBJECT innehavaren

        IS NOT takable.
        NOT synlig.

    DESCRIPTION
        IF innehavaren IS NOT synlig
        THEN
           LOCATE innehavaren HERE.
           LOCATE kniven HERE.
           LOCATE lappen HERE.
           LOCATE träbenet HERE.
           LOCATE kroken HERE.
           MAKE innehavaren synlig.
        END IF.
        "När Du kliver på, får Du en skymt av innehavaren, och av
         någon anledning får det Dig att känna Dig illa till mods.
         Du har dock svårt att riktigt sätta fingret på vad det är
         som ger dig denna känsla. Kan det vara sättet han tittar
         på dig med det öga, som inte är gömt bakom den svarta lappen?
         Kan det måhända vara ljuden, när han haltar fram på sitt
         träben eller slår den krok, som sitter där armen slutar,
         i bordet? Eller är det helt enkelt frågan om hans röst, när
         han med tänderna hårt slutna"
        IF kniven HERE THEN "kring sin kniv" END IF.
        "väser fram:$p- 'Ikväll grabben !  Ikväll är det dags ...'"

END OBJECT.


OBJECT kniven 

    DESCRIPTION
        IF kniven NOT AT kiosk
        THEN
                "Det ligger en blank kniv här."
        END IF.

    MENTIONED "kniv"

    VERB ta DOES
        IF kniven AT kiosk
        AND innehavaren AT kiosk
        THEN
                "När Du sträcker Dig fram för att ta kniven ur munnen på den
                 vildsinte innehavaren, blir han en smula vred. Med en snabb
                 högerkrok förpassar han Dig till de sälla jaktmarkerna."
                LOCATE hero AT NOWHERE.
        ELSIF kniven IN inventory
        THEN
                "Du bär redan på den."
        ELSE
             LOCATE kniven IN inventory.
                "OK!"
           END IF.
    END VERB.

        VERB köp DOES
           IF kniven AT kiosk
           AND innehavaren AT kiosk
           THEN
                "Innehavaren tittar sniket på, medan Du plockar upp pengar
                 ur Din ficka."
                IF pengarna IS slut
                OR pengarna IS lite
                THEN
                   "Nedlåtande skakar han på huvudet och säger:
                    $i- 'Nej, du grabben. Det där räcker inte.'"
                ELSE
                   "Raskt rycker han åt sig hela Din pengabunt, och spottar
                    nonchalant ut kniven.
                   $i- 'Här har Du', väser han. 'Det är en äkta sjörövarkniv,
                    så använd den väl!'"
                   MAKE pengarna slut.
                   "Du böjer Dig fascinerat ned och plockar upp kniven."
                   LOCATE kniven IN inventory.
                END IF.
           ELSE
                "Det finns ingen här att betala till."
           END IF.
        END VERB.

END OBJECT.


OBJECT lappen

  IS NOT takable.

  MENTIONED "lapp"

END OBJECT.


OBJECT träbenet

        IS neutrum.
        NOT takable.

  MENTIONED "träben"

END OBJECT.


OBJECT kroken

        IS NOT takable.

  MENTIONED "krok"

END OBJECT.

LOCATION sandslott
        
    DESCRIPTION
        "Ett fantastiskt sandslott håller på att växa fram här.
         Nyfiken går Du runt slottet, och finner att det faktiskt
         går att gå in i borgen. Du sätter Dig på borggården. Det
         känns skönt att ha ett ställe där man kan gömma sig för
         föräldrarnas vakande blickar." 
        MAKE hjälte NOT synlig.

    EXIT v TO strandmitt
        DOES
            MAKE hjälte synlig.
    END EXIT.

    EXIT sv TO glasskiosk
        DOES
            MAKE hjälte synlig.
    END EXIT.

    EXIT s TO kiosk
        DOES
            MAKE hjälte synlig.
    END EXIT.

    EXIT n TO östanhav
        DOES
            MAKE hjälte synlig.
    END EXIT.

    EXIT nv TO medelhav
        DOES
            MAKE hjälte synlig.
    END EXIT.

END LOCATION.

LOCATION västanhav

    DESCRIPTION
        MAKE hjälte synlig.
        IF time IS evening THEN
                "Tidvattnet har varit mycket kraftigt idag, och nu under
                 ebb ligger en stor bit av den tidigare havsbotten framför
                 dina fötter. Vid klippan i väster kan Du se en mörk öppning,
                 omkring en meter hög."
        ELSE
                "Du simmar omkring i det behagliga vattnet. Strömmen är
                 ganska stark, och Du kommer ofrivilligt att hamna ganska
                 nära klippan i väster. Klippan är här liksom på stranden
                 tämligen ogästvänlig, där den tornar upp sig ur vattnet."
        END IF.

    EXIT sv TO klippa.
    EXIT s TO klippa.
    EXIT so TO filtplats.
    EXIT o TO västanhav
	CHECK
            "Du försöker simma österut, men vågorna och de starka
             strömmarna hindrar dig."
    END EXIT.
    EXIT no TO nordanhav.
    EXIT n TO nordanhav.
    EXIT nv TO nordanhav.
    EXIT v TO ostviken
        CHECK time IS NOT evening
            ELSE "Du simmar mot den branta klippan, som fortsätter ut
                  i havet. Det finns ingenstans att gå i land här."
	AND time IS NOT morning
	    ELSE "Det går inte att gå däråt nu."

	DOES
            IF time IS noon THEN
                        "Just som Du tänker vända inåt stranden, är det
                         något som fångar din blick. Det utåtgående
                         tidvattnet har sjunkit undan krafigt den senaste
                         timmen, och precis i vattenbrynet syns ett mörkt
                         hål. Nyfiken simmar Du närmare och finner vad som
                         verkar vara ingången till en tunnel. Försiktigt
                         tar Du dig fram genom den halvt vattenfyllda
                         gången mot det ljus som syns i andra änden. Efter
                         bara ett ögonblick är Du igenom."
            ELSIF time IS evening THEN
                        "Du skyndar Dig fram i leran till öppningen. Du
                         tittar in, och ser antydan till en ljusning längst
                         in. Halvt hukande tar du Dig in, och efter ett
                         ögonblick är Du igenom."
            END IF.
    END EXIT.

END LOCATION.

LOCATION medelhav

    DESCRIPTION
        "Du flyter omkring i vattnet strax utanför stranden. Inne på
         stranden ser Du Dina föräldrar lapande sol.
         Du ser också ett antal småkiosker.
         I väster tar stranden så småningom slut intill en brant
         klippa."

    EXIT sv TO filtplats.
    EXIT s TO strandmitt.
    EXIT so TO sandslott.   
    EXIT o TO medelhav
	CHECK
            "Du försöker simma österut, men vågorna och de starka
                 strömmarna hindrar dig."
    END EXIT.
    EXIT no TO nordanhav.
    EXIT n TO nordanhav.
    EXIT nv TO nordanhav.
    EXIT v TO västanhav.
        
END LOCATION.

LOCATION östanhav

    DESCRIPTION
        "Du har hamnat i vattnet långt bort på strandens östra kant.
         Här är det ensamt i vattnet. På stranden innanför dig är det
         dock fullt med folk, som håller på att bygga på ett enormt
         sandslott."

    EXIT sv TO strandmitt.
    EXIT s TO sandslott.
    EXIT so TO sandslott.   
    EXIT o TO östanhav
	CHECK
            "Du försöker simma österut, men vågorna och de starka
             strömmarna hindrar dig."
    END EXIT.
    EXIT no TO nordanhav.
    EXIT n TO nordanhav.
    EXIT nv TO nordanhav.
    EXIT v TO medelhav.
        
END LOCATION.

LOCATION nordanhav
        
    DESCRIPTION
        "Du har simmat alldeles för långt ut från stranden, och egentligen
         skulle Du ha sugits ned av de kraftiga underströmmarna och
         drunknat. Eftersom detta inte står i vår makt får Du en chans
         att fortsätta. Grattis ! "

    EXIT sv TO västanhav.
    EXIT s TO västanhav.
    EXIT so TO västanhav.

END LOCATION.

LOCATION ostviken

    DESCRIPTION
        MAKE hjälte NOT synlig.
        "Här, dold djupt inne bland de höga, ogästvänliga klipporna,
         finner Du en liten vik. Det verkar nästan som en naturlig
         båthamn, men Du kan inte se några båtar här nu."
        IF time IS evening
        THEN
                "Du är på väg tillbaka till stranden, då den nedåtgående
                 solens sista strålar plötsligt reflekteras i något
                 vid strandkanten längst bort i viken."
        END IF.

    EXIT o TO västanhav.
    EXIT v TO västviken
	DOES
            IF time IS evening THEN
                "Nyfikenheten får Dig att besluta Dig
                 för att undersöka det hela närmare."
            END IF.
    END EXIT.

END LOCATION.


LOCATION västviken

    DESCRIPTION
       "Längst in i viken är marken stenig och ogästvänlig. Vassa klippor
         sticker upp ur vattnet."
        IF time IS evening
        THEN
                "Till Din förvåning ser Du lågt ute i vattnet ett helt
                 skeppsskrov, som antagligen brukar ligga under vatten
                 normalt, men som nu blottlagts av det ovanligt kraftiga
                 tidvattnet."
        END IF.

    EXIT n TO skäret.
    EXIT o TO ostviken.

END LOCATION.


LOCATION NOWHERE 
END LOCATION.


OBJECT pengarna AT NOWHERE

        IS plural.
        mycket.
        NOT tillräckligt.
        NOT lite.
        NOT slut.       

    DESCRIPTION
        "En mindre summa pengar finns här."

    ARTICLE

    MENTIONED
        "lite pengar"

END OBJECT.

LOCATION skäret
    DESCRIPTION
        "Så här långt ute i vattnet kan du knappt se stranden."
        DESCRIBE skrovet.
        LOCATE hålet HERE.
        DESCRIBE hålet.

    EXIT 'in' TO innanför.
    EXIT s TO västviken.

END LOCATION.

OBJECT skrovet AT skäret
    DESCRIPTION
        "Ett gammalt rostigt stålskrov ligger här på skäret. Det
         salta vattnet har tärt hårt på skrovet. Rosten färgar
         till och med det annars klara vattnet rött när det
         kluckar mot skrovet."

    VERB undersök DOES
        "Så här utanpå kan man inte direkt finna något intressant,
         om man inte har ett speciellt intresse av sönderrostande
         båtar. Du står dessutom så nära att du inte riktigt kan
         få någon överblick över skrovets utseeende."
    END VERB.
END OBJECT.

OBJECT hålet AT skäret
    DESCRIPTION
        "I sidan på skrovet, mitt framför näsan på dig finns ett
         stort hål (nåja, du skulle i alla fall komma igenom
         det!). Man skulle faktiskt kunna tro att det är detta hål
         som är den direkta orsaken till att skrovet ligger här
         och rostar sönder."

    VERB undersök DOES
        "Hålet verkar vara gjort av någon projektil eller annat
         trubbigt föremål som trängt igenom sidan utifrån.
         Kanterna är ganska vassa men rosten och vattnet har
         hjälpts åt att slipa bort det värsta, och en fullvuxen
         karl skulle numera kunna ta sig igenom utan att skada sig
         (om man är försiktig!)."
    END VERB.
END OBJECT.

LOCATION innanför

    DESCRIPTION
	"Här inne i det rostande skrovet, är det ganska mörkt, men
	hålet släpper in lite av ljuset utifrån. Lite vatten skvalpar
	fortfarande kring dina fötter, det rinner kanske aldrig ur,
	hålet är ju några decimeter ifrån botten på skrovet. Förut
	ser man en dörr, och akterut en mörk öppning in till ett
	annat utrymme."
	LOCATE hålet HERE.
	DESCRIBE hålet.

    EXIT ut TO skäret.
    EXIT förut TO trapp2
	CHECK
            "Dörren är stängd."
    END EXIT.
    EXIT akterut TO motorrummet.
    EXIT 'in' TO motorrummet.
END LOCATION.

OBJECT dörren AT innanför
    MENTIONED "dörr"
    VERB undersök DOES
        "Dörren ser relativt oskadad ut, men den buktar oroväckande
         utåt, mot dig."
    END VERB.

    VERB öppna DOES
        "Visserligen får du ett gott grepp om handtaget, men hur
         du än tar i så kan du inte rubba dörren en millimeter."
    END VERB.
END OBJECT.

LOCATION motorrummet

    DESCRIPTION
	"Här är motorrummet i skeppet. På både styrbords och babords
	sida av rummet finns öppningar, den på babordssidan är något
	ljusare än den andra."

    EXIT babord TO innanför.
    EXIT styrbord TO håliga_rummet.

END LOCATION.

OBJECT turbinerna AT motorrummet
    DESCRIPTION
        "Två enorma dieselturbiner fyller nästan hela rummet.
         Lukten av olja och dieselbränsle ligger tung här. Detta
         är märkligt då ju skrovet i övrigt tyder på att det legat
         här i många, många år, så man tycker att lukten borde
         gått ur nu."
END OBJECT.

OBJECT skräpet AT motorrummet
    DESCRIPTION
        "En hel del skräp ligger här på durken. Gamla rostiga
         burkar, sladdar och trasor är spridda överallt."
END OBJECT.

LOCATION håliga_rummet
    DESCRIPTION
        "Här har rosten bitit in duktigt i skrovet."
    DESCRIBE hålen.
    "Både för- och akterut finns öppningar i skrovets skott."

    EXIT akterut TO motorrummet.
    EXIT förut TO trapp1.
END LOCATION.

OBJECT hålen AT håliga_rummet
    DESCRIPTION
        "Överallt i sidan på skrovet finns små, små hål som
         släpper in lite ljus, precis så mycket att du ser vad du
         gör."

    VERB undersök DOES
        "Hålen varierar i storlek mellan 1 och 5 millimeter, och
         genom ett av de större kan du skymta vattnet utanför."
    END VERB.
END OBJECT.


LOCATION trapp1
    DESCRIPTION
	"Det här verkar var ett av lastrummen i skeppet. Helt tomt
	och utan några som helst kännetecken förutom den trappa som
	börjar borta vid styrbordsskottet och leder upp."
	LOCATE trappan HERE.

    EXIT akterut TO håliga_rummet.
    EXIT upp TO trapp_top.
    EXIT förut TO lastrum1.
END LOCATION.

OBJECT trappan AT trapp1
    VERB undersök DOES
        "Trappan är gjord av järn, med trappsteg av galler, ungefär
         som man föreställer sig en trappa i ett gammalt järnskepp.
         Ganska rostig förstås, men ganska stadig ändå."
    END VERB.
END OBJECT.

LOCATION trapp_top
    DESCRIPTION
         "Trappan leder upp, men efter en bit ser du att själva hålet
         är övertäckt av en massa brädor. Längre upp än så här kan
         du inte komma."

    EXIT ner TO trapp1.
END LOCATION.

OBJECT brädorna AT trapp_top
    VERB undersök DOES
        "Brädorna ligger tungt staplade precis för nedgången till
         trappan, och inte ens Karl-Alfred skulle kunna rubba dem."
    END VERB.
END OBJECT.

LOCATION lastrum1
    DESCRIPTION
        "Detta är ytterligare ett av de tomma lastrummen på skeppet.
         Vad det har fraktat en gång i tiden är svårt att avgöra, men
         någon äcklig goja kletar fast på dina fötter. Känns inte
         lukten av tång starkare nu?"

    EXIT babord TO lastrum2.

END LOCATION.

LOCATION lastrum2
    DESCRIPTION
	"Detta är ytterligare ett av de tomma lastrummen på skeppet.
	Vad det har fraktat en gång i tiden är svårt att avgöra, men
	någon äcklig goja kletar fast på dina fötter. Känns inte
	lukten av tång starkare nu?"

    EXIT akterut TO trapp2.
    EXIT styrbord TO lastrum1.
END LOCATION.

LOCATION trapp2
    DESCRIPTION
        "Det här verkar var ett av lastrummen i skeppet. Helt tomt
	och utan några som helst kännetecken förutom den trappa som
	börjar borta vid babordsskottet och leder upp."
	LOCATE trappan HERE.
	DESCRIBE vattenpölen.

    EXIT förut TO lastrum2.
    EXIT upp TO babords_hall_1.
END LOCATION.

OBJECT vattenpölen AT trapp2
    IS
        NOT stor.

    DESCRIPTION
        "I den aktre änden av lastrummet har samlats en stor pöl
         med vatten. På grund av skrovets lutning kan väl inte
         detta rinna ut när tidvattnet sjunker, och det står en
         bra bit upp på en dörr i den änden av rummet. Dörren
         buktar ordentligt utåt, in mot rummet på aktre sidan av
         det skottet."
END OBJECT.

LOCATION babords_hall_1

    DESCRIPTION
	"Ovanför trappan som leder ned till lastrummet börjar en lång
	hall. Den sträcker sig akteröver förbi ett antal hytter.
	Åt styrbordsidan ser du en annan hall leda akterut."
	LOCATE trappan HERE.

    EXIT akterut TO babords_hall_2.
    EXIT styrbord TO styrbords_hall_1.
    EXIT ned TO trapp2
        CHECK vattenpölen IS NOT stor
	    ELSE
		"När du försöker gå nedför trappan möts du av en massa
 		 vatten. Antagligen har tidvattnet stigit igen, och fyllt
	 	 de undre delarna av skrovet med vatten. Eftersom du inte
		 har någon dykardräkt eller andra hjälpmedel blir du
		 tvungen att stanna här uppe."
    END EXIT.

END LOCATION.

LOCATION babords_hall_2

    DESCRIPTION
        "Förut syns den förliga änden av hallen. Rakt framför dig är
         ingången till Hytt nummer 1. Hallen fortsätter akterut."

    EXIT förut TO babords_hall_1.
    EXIT akterut TO babords_hall_3.
    EXIT 'in' TO hytt1.

END LOCATION.


LOCATION hytt1

    EXIT babord TO babords_hall_2.

END LOCATION.


LOCATION babords_hall_3

    DESCRIPTION
        "Akterut syns den akterliga änden av hallen. Rakt framför dig är
         ingången till Hytt nummer 2. Hallen fortsätter föröver."

    EXIT förut TO babords_hall_2.
    EXIT akterut TO babords_hall_4.
    EXIT 'in' TO hytt2.

END LOCATION.


LOCATION hytt2

    EXIT babord TO babords_hall_3.

END LOCATION.


LOCATION babords_hall_4

    DESCRIPTION
        "Hallen sträcker sig vidare förut. Framför dig är ingången
         till Hytt Nummer 3. Akterut viker hallen av runt hörnet
         åt styrbord."

    EXIT 'in' TO hytt3.
    EXIT förut TO babords_hall_3.
    EXIT akterut TO hallslut.
    EXIT styrbord TO hallslut.

END LOCATION.

LOCATION hytt3

    DESCRIPTION
        "Hytt 3"

    EXIT babord TO babords_hall_4.

END LOCATION.


LOCATION hallslut

    DESCRIPTION
        "Här är den aktra änden av två hallar som både åt styrbords- och
         babordssidan leder föröver. Akterut leder en stor glasad ingång
         till en stor salong."
        MAKE vattenpölen stor.  -- Lagom mycket utforskat här uppe, stäng
                                -- in honom!

    EXIT styrbord TO styrbords_hall_4.

    EXIT babord TO babords_hall_4.

    EXIT förut TO babords_hall_4
        CHECK
            "Du måste nog välja den ena eller den andra hallen!"
    END EXIT.

    EXIT akterut TO salongen.

END LOCATION.

LOCATION styrbords_hall_1

  DESCRIPTION
    "Här börjar en lång hall. Den sträcker sig akteröver förbi ett
     antal hytter. Föröver ligger en annan hytt. Åt babordsidan
     ser du en annan hall leda akterut."
    DESCRIBE bläckfisken.

    EXIT akterut TO styrbords_hall_2
        CHECK bläckfisken IS NOT levande
            ELSE "Du ska inte tro att bläckfisken släpper sitt tag bara
                  för att du vill det!!"
    END EXIT.
    EXIT förut TO kaptens_hytt
        CHECK bläckfisken IS NOT levande
            ELSE "Du ska inte tro att bläckfisken släpper sitt tag bara
                  för att du vill det!!"
    END EXIT.
    EXIT babord TO babords_hall_1
        CHECK bläckfisken IS NOT levande
            ELSE "Du ska inte tro att bläckfisken släpper sitt tag bara
                  för att du vill det!!"
    END EXIT.
END LOCATION.

OBJECT bläckfisken AT styrbords_hall_1
    IS
        levande.

    DESCRIPTION
        IF bläckfisken IS levande THEN
            "Utfläkt över golvet ligger en STOR tolvarmad bläckfisk.
             Hans tallriksstora ögon blickar fisklikt på dig, medan
             två av hans tentakler greppar hård kring dina fotleder.
             Hur du än sliter och slingrar dig har du inte en chans
             att komma loss!"
        ELSE
            "Utfläkt över golvet ligger de slemmiga resterna av en
             STOR bläckfisk."
        END IF.

    VERB döda DOES
        IF kniven IN inventory THEN
            "Efter en stunds vilt hackande i bläckfiskens kropp med
             kniven märker du att de slemmiga massornas kraft
             mattas och till sist sjunker slemklumpen till golvet.
             Du är fri!"
            MAKE bläckfisken NOT levande.
        ELSE
            "Du har inget att döda den med!"
        END IF.
    END VERB.
END OBJECT.

LOCATION styrbords_hall_2
    DESCRIPTION
        "Förut syns den förliga änden av hallen. Rakt framför dig är
         ingången till Hytt nummer 4. Hallen fortsätter akterut."

    EXIT förut TO styrbords_hall_1.
    EXIT akterut TO styrbords_hall_3.
    EXIT 'in' TO hytt4.
END LOCATION.

LOCATION hytt4
    EXIT styrbord TO styrbords_hall_2.
END LOCATION.

LOCATION styrbords_hall_3
    DESCRIPTION
        "Förut syns den förliga änden av hallen. Rakt framför dig är
         ingången till Hytt nummer 5. Hallen fortsätter akterut."

    EXIT förut TO styrbords_hall_2.
    EXIT akterut TO styrbords_hall_4.
    EXIT 'in' TO hytt5.
END LOCATION.

LOCATION hytt5
    EXIT styrbord TO styrbords_hall_3.
END LOCATION.

LOCATION styrbords_hall_4
    DESCRIPTION
        "Hallen sträcker sig vidare förut. Framför dig är ingången
         till Hytt Nummer 3. Akterut viker hallen av runt hörnet
         åt babord."

    EXIT 'in' TO hytt6.
    EXIT förut TO styrbords_hall_3.
    EXIT akterut TO hallslut.
    EXIT babord TO hallslut.
END LOCATION.

LOCATION hytt6
    EXIT styrbord TO styrbords_hall_4.
END LOCATION.

LOCATION kaptens_hytt
    DESCRIPTION
        "Detta verkar vara kaptenens hytt."
        DESCRIBE skrivbord.

    EXIT akterut TO babords_hall_1.
END LOCATION.

OBJECT skrivbord AT kaptens_hytt
    DESCRIPTION
        "Mot ena skottet, under en lucka står ett stort skrivbord av ek.
         På bordet, liksom överallt annars ligger en seg slemmig hinna
         av saltvatten, tång, sjögräs och halvruttna musslor."
        DESCRIBE lådan.

    VERB undersök DOES
        "Den slemmiga geggan smetar av sig på dina händer, men du kan
         ändå inte finna några gömda fack eller annat intressant."
    END VERB.
END OBJECT.

OBJECT lådan AT kaptens_hytt
    IS
        stängd.

    DESCRIPTION
        "I en av hutscharna sitter fortfarande en av lådorna kvar."
        IF lådan IS stängd THEN
            "Lådan är stängd."
        ELSE
            "Lådan är öppen,"
            IF skrinet IS inuti THEN
               "och i den ligger ett skrin."
            ELSE
               "och alldeles tom."
            END IF.
        END IF.

    VERB öppna DOES
        IF lådan IS stängd THEN
            "Ett tag verkar det som om lådan sitter fast, men efter
             att ha slitit ett tag i den får du upp den."
            IF skrinet IS inuti THEN
                "I lådan ligger ett litet skrin."
                LOCATE skrinet HERE.
            ELSE
                "Lådan är alldeles tom."
            END IF.
            MAKE lådan NOT stängd.
        ELSE
            "Den är ju redan öppen!"
        END IF.
    END VERB.

    VERB stäng DOES
        IF lådan IS stängd THEN
            "Den är ju redan stängd!"
        ELSE
            "Stängd."
            LOCATE skrinet AT NOWHERE.
            MAKE lådan stängd.
        END IF.
    END VERB.
END OBJECT.

OBJECT skrinet
    IS
        inuti.

    DESCRIPTION
        IF skrinet IS NOT inuti THEN
            "På durken här ligger ett litet skrin av plåt."
        END IF.

    VERB tag DOES
        IF skrinet IN inventory THEN
            "Du har det redan."
        ELSE
            MAKE skrinet NOT inuti.
            LOCATE skrinet IN inventory.
        END IF.
    END VERB.

    VERB öppna DOES
        "Efter lite lirkande får du upp locket till skrinet. Inuti
         hinner du just få en glimt av något glittrande och något
         annat föremål som verkar sprött, när du plötsligt mer anar
         än ser en rörelse snett bakom dig. En oerhörd explosion
         i din hjärna, din blick mörknar, stjärnor och raketer snurrar
         runt i en fanatisk dans. Till sist försvinner också dessa och
         allt blir totalt mörker...."
        LOCATE hero AT mörker.
    END VERB.
END OBJECT.

LOCATION mörker
    DESCRIPTION
        "Allt är totalt mörker."

    EXIT akterut, förut, styrbord, babord, upp, ner TO mörker
        CHECK
            "Du kan naturligtvis inte röra dig heller!"
    END EXIT.
END LOCATION.

LOCATION salongen
    DESCRIPTION
        "Den gamla salongen verkar ha används även som matsal, på borden
         och även på golvet ligger gamla tallrikar och bestick strödda
         lite här och där."

    EXIT förut TO hallslut.
END LOCATION.


START AT p_plats.

"Äntligen lördag!

$pDet är en härlig känsla att få komma ned till stranden igen. Första
gången på året är alltid lika spännande. Där Du sitter i baksätet i
Dina föräldrars bil tänker Du med längtan på sanden och det härliga
vattnet.

$pNu kan Du se havet genom framrutan på bilen. Din far kör fram till
parkeringen, betalar och ställer bilen på en plats där det finns hopp
om åtminstone en smula skugga. Så stiger ni ur, Dina bägge föräldrar
och Du. Din far och mor tar sitt pick och pack och går norrut in på
stranden. Själv stannar Du efter ett ögonblick och bara ser Dig
omkring."

LOCATE föräldrar AT filtplats.

